
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ВетисОбщегоНазначения.РазрешенаОтладка() Тогда
		Элементы.ДатаИзменения.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьСервер();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСервер()
	
	_ТаблицаВетис = Новый ТаблицаЗначений;
	_ТаблицаВетис.Колонки.Добавить("name", Новый ОписаниеТипов("Строка"));
	_ТаблицаВетис.Колонки.Добавить("guid", Новый ОписаниеТипов("Строка"));
	
	Макет = Справочники[ВетисИмяСправочника.PackingForm()].ПолучитьМакет("СписокФормУпаковок");
	
	Для НомерСтроки = 1 По Макет.ВысотаТаблицы Цикл
		
		ТаблицаСтрока = _ТаблицаВетис.Добавить();
		
		ТаблицаСтрока.guid = Макет.Область(НомерСтроки, 1).Текст;
		ТаблицаСтрока.name = Макет.Область(НомерСтроки, 2).Текст;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(_Таблица.name КАК СТРОКА(150)) КАК name,
	|	ВЫРАЗИТЬ(_Таблица.guid КАК СТРОКА(36)) КАК guid
	|ПОМЕСТИТЬ ВТаблицаВетис
	|ИЗ
	|	&ТаблицаВетис КАК _Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_Таблица.Ссылка КАК Ссылка,
	|	_Таблица.Наименование КАК Наименование,
	|	_Соответствие.guid КАК _guid,
	|	_Ветис.name КАК name,
	|	_Ветис.guid КАК guid
	|ИЗ
	|	ВТаблицаВетис КАК _Ветис
	|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ВетисPackingForm КАК _Таблица
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _Соответствие
	|			ПО _Таблица.Ссылка = _Соответствие.Ссылка
	|		ПО (_Ветис.guid = _Соответствие.guid)
	|ГДЕ
	|	ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	_Ветис.name";
	
	Запрос.УстановитьПараметр("ТаблицаВетис", _ТаблицаВетис);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		_Новый = Ложь;
		
		_ЕстьИзменения = Ложь;
		
		Если Выборка.guid = NULL Тогда
			Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
			ВетисОбщегоНазначения.ВывестиСообщение("- " + Выборка.Наименование, "Ветис.PackingForm");
			Продолжить;
			
		ИначеЕсли Выборка.Ссылка = NULL Тогда
			_Новый = Истина;
			
		Иначе
			_ЕстьИзменения = Ложь
			ИЛИ НЕ Выборка.Наименование = СокрЛП(Выборка.name);
			
		КонецЕсли;
		
		Если _ЕстьИзменения ИЛИ _Новый Тогда
			
			Если _Новый Тогда
				_Элемент = Справочники[ВетисИмяСправочника.PackingForm()].СоздатьЭлемент();
				ВетисОбщегоНазначения.ВывестиСообщение("+ " + СокрЛП(Выборка.name), "Ветис.PackingForm");
			Иначе
				_Элемент = Выборка.Ссылка.ПолучитьОбъект();
				ВетисОбщегоНазначения.ВывестиСообщение("~ " + СокрЛП(Выборка.name), "Ветис.PackingForm");
			КонецЕсли;
			
			_Элемент.Наименование  = СокрЛП(Выборка.name);
			_Элемент.ДатаИзменения = ТекущаяДата();
			
			Попытка
				_Элемент.Записать();
				
				ВетисСоответствие.Добавить(_Элемент.Ссылка, Выборка.guid, Выборка.name);
				
			Исключение
				ВетисОбщегоНазначения.ВывестиСообщениеОбОшибке(ИнформацияОбОшибке(), "Ветис.PackingForm");
			КонецПопытки;
			
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры
