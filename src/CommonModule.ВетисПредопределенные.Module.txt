
Функция Получить(пНазначение, пПоУмолчанию = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	_Таблица.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ВетисПредопределенные КАК _Таблица
	|ГДЕ
	|	_Таблица.Назначение = &Назначение";
	
	Запрос.УстановитьПараметр("Назначение", пНазначение);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат пПоУмолчанию;
	ИначеЕсли Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Возврат Выборка.Значение;
	Иначе
		_список = Новый Массив;
		Пока Выборка.Следующий() Цикл
			_список.Добавить(Выборка.Значение);
		КонецЦикла;
		Возврат _список;
	КонецЕсли;
	
КонецФункции

Процедура Установить(пНазначение, пЗначение, пКомментарий = "", пЗаменить = Истина) Экспорт
	
	Если пЗаменить = Истина Тогда
		Удалить(пНазначение);
	КонецЕсли;
	
	_мз = РегистрыСведений.ВетисПредопределенные.СоздатьМенеджерЗаписи();
	_мз.Назначение = пНазначение;
	_мз.Значение = пЗначение;
	_мз.Комментарий = пКомментарий;
	
	Попытка
		_мз.Записать();
	Исключение
		ВетисЖурнал.ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

Процедура Удалить(пНазначение) Экспорт
	
	_нз = РегистрыСведений.ВетисПредопределенные.СоздатьНаборЗаписей();
	
	_нз.Отбор.Назначение.Установить(пНазначение);
	
	_нз.Записать();
	
КонецПроцедуры

Функция СтрокаВызова(пНазначение) Экспорт
	
	Возврат
	"ВетисПредопределенные.Получить("""+пНазначение+""");
	|";
	
КонецФункции
