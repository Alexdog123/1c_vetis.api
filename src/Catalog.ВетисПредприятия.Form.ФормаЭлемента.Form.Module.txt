
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Ветис.Настройки_ПоказыватьGuid() Тогда
		guid = Ветис.Соответствие_ПолучитьGuid(Объект.Ссылка);
	Иначе
		Элементы.guid.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСоздатьВСервисе(Команда)
	
	Перем _Ошибка, _Отказ;
	
	ModifyEnterprise(Истина, _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьВСервисе(Команда)
	
	Перем _Ошибка, _Отказ;
	
	ModifyEnterprise(Ложь, _Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьИзСервиса(Команда)
	
	Перем _Ошибка, _Отказ;
	
	ЗаполнитьИзСервиса(_Отказ, _Ошибка);
	
	Если _Отказ = Истина Тогда
		Сообщить(_Ошибка);
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьИзСервиса(пОтказ, пОшибка)
	
	Если Ветис.Версия_2_0() Тогда
		
		_Guid = ВетисDictionaryСлой1с.Enterprise(Объект.Ссылка);
		
		Если _Guid = Неопределено Тогда
			пОтказ = Истина;
			пОшибка = "Предприятие не сопоставлено";
			Возврат;
		КонецЕсли;
		
		_Enterprise = ВетисEnterpriseService_2_0.GetEnterpriseByGuid(_Guid, пОтказ, пОшибка);
		
		Если пОтказ = Истина Тогда
			Возврат;
		КонецЕсли;
		
		ВетисDictionaryСлой1с.EnterpriseКонвертировать(Объект, _Enterprise);
		
	Иначе
		
		_Guid = ВетисCerberusEnterpriseСлой1с.Enterprise(Объект.Ссылка);
		
		Если _Guid = Неопределено Тогда
			пОтказ = Истина;
			пОшибка = "Предприятие не сопоставлено";
			Возврат;
		КонецЕсли;
		
		_Enterprise = ВетисEnterpriseService.GetEnterpriseByGuid(_Guid, пОтказ, пОшибка);
		
		Если пОтказ = Истина Тогда
			Возврат;
		КонецЕсли;
		
		ВетисCerberusEnterpriseСлой1с.EnterpriseКонвертировать(Объект, _Enterprise);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ModifyEnterprise(пСоздать = Истина, пОтказ, пОшибка)
	
	ВетисMercuryApplicationsСлой1с.ModifyEnterprise(Объект, , пСоздать, пОтказ, пОшибка);
	
КонецПроцедуры

