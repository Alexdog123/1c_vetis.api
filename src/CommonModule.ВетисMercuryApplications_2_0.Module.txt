
#Область StockEntry

// Получение записи складского журнала по идентификатору.
Функция GetStockEntryByGuidRequest(guid, enterpriseGuid) Экспорт
	
	_Объект = Создать("GetStockEntryByGuidRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterpriseGuid     = enterpriseGuid;
	_Объект.GUID               = guid;
	
	Возврат _Объект;
	
КонецФункции

Функция GetStockEntryByGuidResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.GetStockEntryByGuidResponse, "GetStockEntryByGuidResponse", URI()).stockEntry;
	
КонецФункции

Функция GetStockEntryByGuid(guid, enterpriseGuid, issuerId = Неопределено, Отказ = Ложь) Экспорт
	
	_Request = GetStockEntryByGuidRequest(guid, enterpriseGuid);
	
	_Response = ВыполнитьЗапрос(_Request, "getStockEntryByGuidRequest", issuerId, Ложь);
	
	Если _Response = Ложь Тогда Отказ = Истина; Возврат Неопределено;	КонецЕсли;
	
	Возврат GetStockEntryByGuidResponse(_Response);
	
КонецФункции


// Получение актуального списка записей складского журнала.
// Параметры:
//  enterpriseGuid - Идентификатор предприятия
//  count - Максимальное запрашиваемое количество объектов в списке
Функция GetStockEntryListRequest(enterpriseGuid) Экспорт
	
	_Объект = Создать("GetStockEntryListRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterpriseGuid     = enterpriseGuid;
	_Объект.listOptions        = ВетисBase.ListOptions();
	
	Возврат _Объект;
	
КонецФункции

Функция GetStockEntryListResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.GetStockEntryListResponse, "GetStockEntryListResponse", URI()).stockEntryList;
	
КонецФункции

// Предназначена для получения списка записей складского журнала
// Используется самостоятельно в цикле Пока.
// Параметры:
//  enterpriseGuid - Идентификатор предприятия
//  issuerId       - Идентификатор ХС
//  пItem          - элемент списка, возвращаемый параметр
//  пПараметры     - для внутреннего использования, начальное значение - Неопределено
// Возвращаемое значение:
//	Булево         - Ложь значит список пройден весь
Функция GetStockEntryListСледующий(enterpriseGuid, issuerId = Неопределено, пItem, пПараметры) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("Индекс", 0);
		пПараметры.Вставить("ListRequest", GetStockEntryListRequest(enterpriseGuid));
		пПараметры.Вставить("Отладка", ВетисОбщегоНазначения.РазрешенаОтладка());
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListRequest.ListOptions.offset = пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			пItem = пПараметры.ListResponse.stockEntry[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	_Response = ВыполнитьЗапрос(пПараметры.ListRequest, "getStockEntryListRequest", issuerId, Ложь, пПараметры.Отладка);
	
	Если _Response = Ложь Тогда Возврат Ложь;	КонецЕсли;
	
	_ListResponse = GetStockEntryListResponse(_Response);
	
	пПараметры.Вставить("ListResponse", _ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		пItem = пПараметры.ListResponse.stockEntry[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


Функция GetStockEntryChangesListRequest(enterpriseGuid, beginDate, endDate = Неопределено) Экспорт
	
	_Объект = Создать("GetStockEntryChangesListRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterpriseGuid     = enterpriseGuid;
	_Объект.updateDateInterval = ВетисBase_2_0.DateInterval(beginDate, endDate);
	_Объект.listOptions        = ВетисBase_2_0.ListOptions();
	
	Возврат _Объект;
	
КонецФункции

Функция GetStockEntryChangesListResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.getStockEntryChangesListResponse, "GetStockEntryChangesListResponse", URI()).stockEntryList;
	
КонецФункции

Функция GetStockEntryChangesListСледующий(enterpriseGuid, issuerId = Неопределено, beginDate, endDate = Неопределено, пItem, пПараметры) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("Индекс", 0);
		пПараметры.Вставить("ListRequest", GetStockEntryChangesListRequest(enterpriseGuid, beginDate, endDate));
		пПараметры.Вставить("Отладка", ВетисОбщегоНазначения.РазрешенаОтладка());
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListRequest.ListOptions.offset = пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			пItem = пПараметры.ListResponse.stockEntry[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	_Response = ВыполнитьЗапрос(пПараметры.ListRequest, "getStockEntryChangesListRequest", issuerId, Ложь, пПараметры.Отладка);
	
	Если _Response = Ложь Тогда Возврат Ложь;	КонецЕсли;
	
	_ListResponse = GetStockEntryChangesListResponse(_Response);
	
	пПараметры.Вставить("ListResponse", _ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		пItem = пПараметры.ListResponse.stockEntry[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область VetDocument

// Получение ВСД
// Параметры:
//  enterpriseGuid - Идентификатор предприятия
//  vetDocumentUuid - Идентификатор ВСД
Функция GetVetDocumentByUuidRequest(vetDocumentUuid, enterpriseGuid) Экспорт
	
	_Объект = Создать("GetVetDocumentByUuidRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterpriseGuid     = enterpriseGuid;
	_Объект.uuid               = vetDocumentUuid;
	
	Возврат _Объект;
	
КонецФункции

Функция GetVetDocumentByUuidResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.GetVetDocumentByUuidResponse, "GetVetDocumentByUuidResponse", URI()).vetDocument;
	
КонецФункции

Функция GetVetDocumentByUuid(vetDocumentUuid, enterpriseGuid, issuerId = Неопределено, Отказ = Ложь) Экспорт
	
	_Request = GetVetDocumentByUuidRequest(vetDocumentUuid, enterpriseGuid);
	
	_Response = ВыполнитьЗапрос(_Request, "getVetDocumentByUuidRequest", issuerId, Ложь);
	
	Если _Response = Ложь Тогда Отказ = Истина; Возврат Неопределено;	КонецЕсли;
	
	Возврат GetVetDocumentByUuidResponse(_Response);
	
КонецФункции


// Получение всех ВСД предприятия
// Параметры:
//  enterpriseGuid - Идентификатор предприятия
//  vetDocumentType - vetd:VetDocumentType - Тип ВСД
//  vetDocumentStatus - vetd:VetDocumentStatus - Статус ВСД
Функция GetVetDocumentListRequest(enterpriseGuid, vetDocumentType = Неопределено, vetDocumentStatus = Неопределено) Экспорт
	
	_Объект = Создать("GetVetDocumentListRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterpriseGuid     = enterpriseGuid;
	_Объект.listOptions        = ВетисBase_2_0.ListOptions();
	
	Если НЕ vetDocumentType = Неопределено Тогда
		_Объект.vetDocumentType = vetDocumentType;
	КонецЕсли;
	
	Если НЕ vetDocumentType = Неопределено Тогда
		_Объект.vetDocumentStatus = vetDocumentStatus;
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

Функция GetVetDocumentListResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.GetVetDocumentListResponse, "GetVetDocumentListResponse", URI()).vetDocumentList;
	
КонецФункции

// Предназначена для получения всех ВСД предприятия
// Используется самостоятельно в цикле Пока.
// Параметры:
//  enterpriseGuid - Идентификатор предприятия
//  issuerId       - Идентификатор ХС
//  пItem          - элемент списка, возвращаемый параметр
//  пПараметры     - для внутреннего использования, начальное значение - Неопределено
// Возвращаемое значение:
//	Булево         - Ложь значит список пройден весь
Функция GetVetDocumentListСледующий(enterpriseGuid, issuerId = Неопределено, пItem, пПараметры) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("Индекс", 0);
		пПараметры.Вставить("ListRequest", GetVetDocumentListRequest(enterpriseGuid));
		пПараметры.Вставить("Отладка", ВетисОбщегоНазначения.РазрешенаОтладка());
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListRequest.ListOptions.offset = пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			пItem = пПараметры.ListResponse.vetDocument[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	_Response = ВыполнитьЗапрос(пПараметры.ListRequest, "getVetDocumentListRequest", issuerId, Ложь, пПараметры.Отладка);
	
	Если _Response = Ложь Тогда Возврат Ложь;	КонецЕсли;
	
	_ListResponse = GetVetDocumentListResponse(_Response);
	
	пПараметры.Вставить("ListResponse", _ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		пItem = пПараметры.ListResponse.vetDocument[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


Функция GetVetDocumentChangesListRequest(enterpriseGuid, beginDate, endDate = Неопределено) Экспорт
	
	_Объект = Создать("GetVetDocumentChangesListRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterpriseGuid     = enterpriseGuid;
	_Объект.updateDateInterval = ВетисBase_2_0.DateInterval(beginDate, endDate);
	_Объект.listOptions        = ВетисBase_2_0.ListOptions();
	
	Возврат _Объект;
	
КонецФункции

Функция GetVetDocumentChangesListResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.getVetDocumentChangesListResponse, "GetVetDocumentChangesListResponse", URI()).vetDocumentList;
	
КонецФункции

Функция GetVetDocumentChangesListСледующий(enterpriseGuid, issuerId = Неопределено, beginDate, endDate = Неопределено, пItem, пПараметры) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("Индекс", 0);
		пПараметры.Вставить("ListRequest", getVetDocumentChangesListRequest(enterpriseGuid, beginDate, endDate));
		пПараметры.Вставить("Отладка", ВетисОбщегоНазначения.РазрешенаОтладка());
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListRequest.ListOptions.offset = пПараметры.ListRequest.ListOptions.offset + пПараметры.ListRequest.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			пItem = пПараметры.ListResponse.vetDocument[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	_Response = ВыполнитьЗапрос(пПараметры.ListRequest, "getVetDocumentChangesListRequest", issuerId, Ложь, пПараметры.Отладка);
	
	Если _Response = Ложь Тогда Возврат Ложь;	КонецЕсли;
	
	_ListResponse = getVetDocumentChangesListResponse(_Response);
	
	пПараметры.Вставить("ListResponse", _ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		пItem = пПараметры.ListResponse.vetDocument[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область MergeOperation

Функция MergeStockEntriesRequest(enterprise, mergeOperation) Экспорт
	
	_Объект = Создать("MergeStockEntriesRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterprise         = enterprise;
	_Объект.mergeOperation     = mergeOperation;
	
	Возврат _Объект;
	
КонецФункции

Функция MergeStockEntriesResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.mergeStockEntriesResponse, "MergeStockEntriesResponse", URI());
	
КонецФункции

#КонецОбласти

#Область ProcessIncomingConsignment

// Операция предназначена для оформления входящей партии.
// Параметры:
//  delivery - vetd:Delivery - Фактические сведения о принимаемой партии продукции и сопровождающих ее документах
//  deliveryFacts - vetd:DeliveryFactList - Обобщенный результат приема партии продукции
//  discrepancyReport - vetd:DiscrepancyReport - Акт несоответствия
//  returnedDelivery - vetd:Delivery - Сведения о возвращаемой партии
Функция ProcessIncomingConsignmentRequest(delivery, deliveryFacts, discrepancyReport = Неопределено, returnedDelivery = Неопределено) Экспорт
	
	_Объект = Создать("ProcessIncomingConsignmentRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.delivery           = delivery;
	_Объект.deliveryFacts      = deliveryFacts;
	
	Если НЕ discrepancyReport = Неопределено Тогда
		Если ТипЗнч(discrepancyReport) = Тип("Массив") Тогда
			Для каждого _discrepancyReport Из discrepancyReport Цикл
				_Объект.discrepancyReport.Добавить(_discrepancyReport);
			КонецЦикла;
		Иначе
			_Объект.discrepancyReport.Добавить(discrepancyReport);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ returnedDelivery = Неопределено Тогда
		_Объект.returnedDelivery = returnedDelivery;
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

// Объект содержит сведения о записях в складском журнале продукции системы Меркурий.
Функция ProcessIncomingConsignmentResponse(пResponse) Экспорт
	
// http://vetrf.ru/vetrf-forum/posts/list/315/6855.page#39749
// В ответе ProcessIncomingResponse будут отдельные ВСД.
// В случае полного или частичного гашения будут возвращены данные погашенного ВСД
// и в случае частичного гашения также будут возвращены данные возвратного ВСД.
// В случае полного возврата партии возвращаются только данные возвратного сертификата.	
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.ProcessIncomingConsignmentResponse, "ProcessIncomingConsignmentResponse", URI());
	
КонецФункции

Функция ProcessIncomingConsignment(delivery, deliveryFacts, discrepancyReport = Неопределено, returnedDelivery = Неопределено, issuerId = Неопределено, пОтказ = Ложь) Экспорт
	
	_Request = ProcessIncomingConsignmentRequest(delivery, deliveryFacts, discrepancyReport, returnedDelivery);
	
	_Response = ВыполнитьЗапрос(_Request, "processIncomingConsignmentRequest", issuerId, Ложь);
	
	Если _Response = Ложь Тогда пОтказ = Истина; Возврат Неопределено;	КонецЕсли;
	
	Возврат ProcessIncomingConsignmentResponse(_Response);
	
КонецФункции

#КонецОбласти

#Область PrepareOutgoingConsignmentOperation

// Операция предназначена для оформления транспортной партии.
// Параметры:
//  delivery - vetd:Delivery - Сведения для оформления транспортного ВСД
Функция PrepareOutgoingConsignmentRequest(delivery) Экспорт
	
	_Объект = Создать("PrepareOutgoingConsignmentRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.delivery           = delivery;
	
	Возврат _Объект;
	
КонецФункции

Функция PrepareOutgoingConsignmentResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.PrepareOutgoingConsignmentResponse, "PrepareOutgoingConsignmentResponse", URI());
	
КонецФункции

Функция PrepareOutgoingConsignment(delivery, issuerId = Неопределено, пОтказ = Ложь) Экспорт
	
	_Request = PrepareOutgoingConsignmentRequest(delivery);
	
	_Response = ВыполнитьЗапрос(_Request, "prepareOutgoingConsignmentRequest", issuerId, Ложь);
	
	Если _Response = Ложь Тогда пОтказ = Истина; Возврат Неопределено;	КонецЕсли;
	
	Возврат PrepareOutgoingConsignmentResponse(_Response);
	
КонецФункции

#КонецОбласти

#Область ResolveDiscrepancyOperation

// Операция предназначена для устранения возможных несоответствий сведений об объёмах в складском журнале продукции системы Меркурий
// и фактических объёмов продукции на складе предприятия, выявленных по результатам проведенной инвентаризации.
// Параметры:
//  enterprise - ent:Enterprise - Площадка на которой осуществляется инвентаризация
//  inventoryDate - xs:dateTime - Дата проведения инвентаризации
//  responsible - argc:User - Ответственный за инвентаризацию
//  stockDiscrepancy - vetd:StockDiscrepancy - Сведения о результате инвентаризации (по одному факту расхождения)
//  discrepancyReport - vetd:DiscrepancyReport - Сведения об акте несоответствия
Функция ResolveDiscrepancyRequest(enterprise, inventoryDate, responsible, stockDiscrepancy, discrepancyReport) Экспорт
	
	_Объект = Создать("ResolveDiscrepancyRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.enterprise         = enterprise;
	_Объект.inventoryDate      = ВетисОбщегоНазначения.ВремяВетис(inventoryDate);
	_Объект.responsible        = responsible;
	_Объект.stockDiscrepancy   = stockDiscrepancy;
	_Объект.discrepancyReport  = discrepancyReport;
	
	Возврат _Объект;
	
КонецФункции

// Объект содержит сведения о результатах обработки заявки на регистрацию несоответствий
// в записях складского журнала, выявленных в результате инвентаризации.
Функция ResolveDiscrepancyResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.ResolveDiscrepancyResponse, "ResolveDiscrepancyResponse", URI()).stockEntryList;
	
КонецФункции

#КонецОбласти

#Область WithdrawVetDocumentOperation
// Если ВСД содержит ошибки, либо фактические сведения отличаются от того, что указано в ВСД,
// то такой документ может быть аннулирован, то есть такой документ становится недействительным.
// 
// Операция аннулирования через шлюз Ветис.API доступна ТОЛЬКО для транспортных сертификатов.
// Производственные сертификаты нужно аннулировать через веб-интерфейс через запись журнала или транзакцию.
// 
// После того как ВСД был аннулирован, объем продукции, на которую был выдан данный ВСД, возвращается в журнал предприятия-отправителя
// и взамен аннулированного врач может выдать новый ВСД на эту партию продукции, содержащий верные сведения.
// 
// Аннулировать ВСД может тот, кто его выписал, либо другой ветеринарный врач обслуживающий то предприятие, с которого данный ВСД был выписан.
// Если ВСД был погашен, то его аннулировать уже нельзя, то есть для аннулирования ВСД должен быть в статусе "Оформлен".

// Операция предназначена для аннулирования ВСД.
// Параметры:
//  vetDocumentId	- bs:UUID - Идентификатор ВСД, который аннулируется
//  withdrawReason - vetd:WithdrawReason - Причина аннулирования ВСД
//  withdrawDate - xs:dateTime - Дата аннулирования ВСД
//  vetDoctor - argc:User - Ветеринарный врач, ответственный за аннулирование ВСД
Функция WithdrawVetDocumentRequest(vetDocumentId, withdrawReason, withdrawDate, specifiedPerson) Экспорт
	
	_Объект = Создать("WithdrawVetDocumentRequest");
	
	_Объект.localTransactionId = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator          = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.vetDocumentId      = vetDocumentId;
	_Объект.withdrawReason     = withdrawReason;
	_Объект.withdrawDate       = ВетисОбщегоНазначения.ВремяВетис(withdrawDate);
	_Объект.specifiedPerson    = specifiedPerson;
	
	Возврат _Объект;
	
КонецФункции

// Объект содержит сведения об аннулированном ВСД.
Функция WithdrawVetDocumentResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.WithdrawVetDocumentResponse, "WithdrawVetDocumentResponse", URI()).vetDocument;
	
КонецФункции

Функция WithdrawVetDocument(vetDocumentId, withdrawReason, withdrawDate, issuerId = Неопределено, пОтказ = Ложь) Экспорт
	
	_Request = WithdrawVetDocumentRequest(vetDocumentId, withdrawReason, withdrawDate, ВетисПараметрыСоединения.Врач());
	
	_Response = ВыполнитьЗапрос(_Request, "withdrawVetDocumentRequest", issuerId, Ложь);
	
	Если _Response = Ложь Тогда пОтказ = Истина; Возврат Неопределено;	КонецЕсли;
	
	Возврат WithdrawVetDocumentResponse(_Response);
	
КонецФункции

#КонецОбласти

#Область ModifyBusinessEntityOperation

// Операция предназначена для регистрации новых ХС в реестре,
// а также для изменения информации об уже зарегистрированных.
// Параметры:
//  modificationOperation - ent:BEModificationOperation - Описание операции добавления/изменения информации о ХС.
Функция ModifyBusinessEntityRequest(modificationOperation) Экспорт
	
	_Объект = Создать("ModifyBusinessEntityRequest");
	
	_Объект.localTransactionId    = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator             = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.modificationOperation = modificationOperation;
	
	Возврат _Объект;
	
КонецФункции

// Объект содержит сведения о ХС, который был изменен или добавлен.
Функция ModifyBusinessEntityResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.ModifyBusinessEntityResponse, "ModifyBusinessEntityResponse", URI()).businessEntity;
	
КонецФункции

#КонецОбласти

#Область ModifyEnterpriseOperation

// Операция предназначена для регистрации новых площадок (поднадзорных объектов) в реестре,
// а также для изменения информации об уже зарегистрированных.
// Параметры:
//  modificationOperation - ent:ENTModificationOperation - Описание операции добавления/изменения информации о поднадзорном объекте.
Функция ModifyEnterpriseRequest(modificationOperation) Экспорт
	
	_Объект = Создать("ModifyEnterpriseRequest");
	
	_Объект.localTransactionId    = Строка(Новый УникальныйИдентификатор);
	_Объект.initiator             = ВетисMercuryVetdocument_2_0.User(ВетисПараметрыСоединения.ЛогинХС());
	_Объект.modificationOperation = modificationOperation;
	
	Возврат _Объект;
	
КонецФункции

// Объект содержит сведения о поднадзорном объекте, который был изменен или добавлен.
Функция ModifyEnterpriseResponse(пResponse) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПривестиКТипу(пResponse.ModifyEnterpriseResponse, "ModifyEnterpriseResponse", URI()).enterprise;
	
КонецФункции

#КонецОбласти


Функция Создать(пИмя, пФабрика = Неопределено) Экспорт
	
	Возврат ВетисОбщегоНазначения.Создать(пИмя, URI(), пФабрика);
	
КонецФункции

Функция URI()
	
	Возврат "http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2";
	
КонецФункции


Функция ФабрикаWS()
	
	_Кеш = ВетисПеременные.Получить("КешФабрики", Новый Соответствие);
	
	_фабрика = _Кеш.Получить("ApplicationManagementService");
	
	Если НЕ _фабрика = Неопределено Тогда
		Возврат _фабрика;
	КонецЕсли;
	
	_ФабрикаXDTO = ВетисXSD.ФабрикаОбщая();
	
	_Определения = ВетисXSD.Определения("ApplicationManagementService");
	
	// создаем врем. фабрику на основе пакетов из конфиги и web сервиса
	// нам нужны 2 пакета из ws, но пакет с application нужен локальный
	_URI = Новый Массив;  
	_URI.Добавить("http://api.vetrf.ru/schema/cdm/application/ws-definitions");  
	_URI.Добавить("http://api.vetrf.ru/schema/cdm/base/ws-definitions");  
	
	_ФабрикаWS = Новый ФабрикаXDTO(_Определения.ФабрикаXDTO.ЭкспортМоделиXDTO(_URI), _ФабрикаXDTO.Пакеты);  
	
	_Пакеты = Новый Массив;  
	_Пакеты.Добавить(_ФабрикаXDTO.Пакеты.Получить(URI()));
	_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://schemas.xmlsoap.org/soap/envelope/"));
	
	Для Каждого _Пакет Из _ФабрикаWS.Пакеты Цикл  
		_Пакеты.Добавить(_Пакет);
	КонецЦикла;  
	
	_фабрика = Новый ФабрикаXDTO(, _Пакеты);
	
	_Кеш = ВетисПеременные.Получить("КешФабрики", Новый Соответствие);
	
	_Кеш.Вставить("ApplicationManagementService", _фабрика);
	
	ВетисПеременные.Установить("КешФабрики", _Кеш);
	
	Возврат _фабрика;
	
КонецФункции

Функция ВыполнитьЗапрос(пRequest, пИмяФункции, issuerId = Неопределено, ПоУмолчанию = Неопределено, пОтладка = Неопределено, пОжидание = 5)
	
	Если пОтладка = Неопределено Тогда
		_Отладка = ВетисОбщегоНазначения.РазрешенаОтладка();
	Иначе
		_Отладка = пОтладка;
	КонецЕсли;
	
	ВетисПараметрыСоединения.ИнициализироватьНастройкиХС(issuerId);
	
	_Фабрика = ФабрикаWS();
	
	_Соединение = ПолучитьСоединение();
	
	//запрос на создание заявки
	
	_Application = ВетисApplication_2_0.Application(пRequest, пИмяФункции);
	
		_SubmitRequest = ВетисApplication_2_0.submitApplicationRequest(_Application);
		
			_Envelope = ВетисSoapEnvelope.Envelope(_SubmitRequest, "submitApplicationRequest");
			
				_ТекстЗапроса = ВетисОбщегоНазначения.Сериализовать(_Фабрика, _Envelope);
				
				Если _Отладка = Истина Тогда
					ВетисЖурнал.Добавить(_ТекстЗапроса);
				КонецЕсли;
				
				_Запрос = ПолучитьЗапрос(_ТекстЗапроса);  
				
				_Ответ = ПолучитьОтвет(_Запрос, _Соединение);  
				
				Если НЕ _Ответ.КодСостояния = 200 Тогда
					ВетисЖурнал.Добавить("Ошибка при отправке запроса");
					ВетисЖурнал.Добавить(_ТекстЗапроса);
					Возврат ПоУмолчанию;
				КонецЕсли;
				
			_Envelope = ВетисОбщегоНазначения.Десериализовать(_Фабрика, _Ответ.ПолучитьТелоКакСтроку("UTF-8"), _Envelope.Тип());
		
		_SubmitResponse = ВетисSoapEnvelope.Извлечь(_Envelope).submitApplicationResponse;
	
	Если НЕ (_SubmitResponse.Application.status = ВетисКонстанты.Status_ACCEPTED()) Тогда 
		ВетисЖурнал.Добавить("Запрос вернулся со статусом " + _SubmitResponse.Application.status);
		Возврат ПоУмолчанию; 
	КонецЕсли;
	
	//запрос на получение результатов обработки заявки
	
	_receiveRequest = ВетисApplication_2_0.receiveApplicationResultRequest(_SubmitResponse.Application.applicationId);
	
	_Envelope = ВетисSoapEnvelope.Envelope(_receiveRequest, "receiveApplicationResultRequest");
	
	_ТекстЗапроса = ВетисОбщегоНазначения.Сериализовать(_Фабрика, _Envelope);
	
	Если _Отладка = Истина Тогда
		ВетисЖурнал.Добавить(_ТекстЗапроса);
	КонецЕсли;
	
	_Запрос = ПолучитьЗапрос(_ТекстЗапроса);  
	
	Пока Истина Цикл
		
		_Ответ = ПолучитьОтвет(_Запрос, _Соединение);  
		
		Если НЕ _Ответ.КодСостояния = 200 Тогда
			ВетисЖурнал.Добавить("Ошибка при отправке запроса");
			ВетисЖурнал.Добавить(_ТекстЗапроса);
			Возврат ПоУмолчанию;
		КонецЕсли;
		
		_Envelope = ВетисОбщегоНазначения.Десериализовать(_Фабрика, _Ответ.ПолучитьТелоКакСтроку("UTF-8"), _Envelope.Тип());
		
		_receiveResponse = ВетисSoapEnvelope.Извлечь(_Envelope).receiveApplicationResultResponse;
		
		Если _receiveResponse.Application.Status = ВетисКонстанты.Status_IN_PROCESS() Тогда
			//ВетисЖурнал.Добавить("Ждем ответа");
		Иначе
			Если НЕ _receiveResponse.Application.status = ВетисКонстанты.Status_COMPLETED() Тогда
				ВетисЖурнал.Добавить(ВетисОбщегоНазначения.Сериализовать(_Фабрика, _receiveResponse.Application.errors));
				Возврат ПоУмолчанию;
			Иначе
				Если _Отладка = Истина Тогда
					ВетисЖурнал.Добавить(ВетисОбщегоНазначения.Сериализовать(_Фабрика, _receiveResponse.Application));
				КонецЕсли;
				
				Возврат ВетисApplication_2_0.ApplicationResult(_receiveResponse.Application);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ПолучитьЗапрос(пТекстЗапроса)
	
	_Запрос = Новый HTTPЗапрос("platform/services/ApplicationManagementService");  
	
	_Запрос.УстановитьТелоИзСтроки(пТекстЗапроса);
	
	Возврат _Запрос;
	
КонецФункции

Функция ПолучитьОтвет(пЗапрос, пСоединение = Неопределено)
	
	Если ТипЗнч(пЗапрос) = Тип("Строка") Тогда
		_Запрос = ПолучитьЗапрос(пЗапрос);
	Иначе
		_Запрос = пЗапрос;
	КонецЕсли;
	
	Если пСоединение = Неопределено Тогда
		_Соединение = ПолучитьСоединение();
	Иначе
		_Соединение = пСоединение;
	КонецЕсли;
	
	_Ответ = _Соединение.ОтправитьДляОбработки(_Запрос);
	
	//Если НЕ _Ответ.КодСостояния = 200 Тогда
	//	ВетисЖурнал.Добавить("Ошибка при отправке запроса");
	//КонецЕсли;
	
	Возврат _Ответ;
	
КонецФункции

Функция ПолучитьСоединение()
	
	_пс = ВетисПараметрыСоединения;
	
	Возврат Новый HTTPСоединение(_пс.Адрес(), _пс.Порт(), _пс.Логин(), _пс.Пароль(), , Истина);  
	
КонецФункции
