
Функция ОсновнойХС(пЗначение = Неопределено) Экспорт
	
	Если пЗначение = Неопределено Тогда
			
		_Значение = ВетисПеременные.Получить("ВетисХС", Неопределено);
		
		Если _Значение = Неопределено Тогда
			
			_Значение = Константы.ВетисОсновнойХС.Получить();
			
			ВетисПеременные.Установить("ВетисХС", _Значение);
			
		КонецЕсли;
		
		Возврат _Значение;
		
	Иначе
		
		Константы.ВетисОсновнойХС.Установить(пЗначение);
		
		ВетисПеременные.Установить("ВетисХС", пЗначение);
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьСписокПредприятийПоХС(пХС) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	_Таблица.Предприятие
	|ИЗ
	|	РегистрСведений.ВетисПредприятияХС КАК _Таблица
	|ГДЕ
	|	_Таблица.ХозяйствующийСубъект = &ХС
	|";
	
	Запрос.УстановитьПараметр("ХС", пХС);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	_Список = Новый Массив;
	Пока Выборка.Следующий() Цикл
		_Список.Добавить(Выборка.Предприятие);
	КонецЦикла;
	
	Возврат _Список;
	
КонецФункции

Процедура ПривязатьХСПредприятие(пХС, пПредприятие, пВСервисе = Неопределено) Экспорт
	
	Если пВСервисе = Неопределено Тогда
		_мз = РегистрыСведений.ВетисПредприятияХС.СоздатьМенеджерЗаписи();
		_мз.ХозяйствующийСубъект = пХС;
		_мз.Предприятие = пПредприятие;
		_мз.Прочитать();
		_ВСервисе = _мз.ВСервисе;
	Иначе
		_ВСервисе = пВСервисе;
	КонецЕсли;
	
	_ModificationType = ?(_ВСервисе, ВетисКонстанты.RegisterModificationType_DELETE(), ВетисКонстанты.RegisterModificationType_CREATE());
	
	Если ВетисXSD.Версия_2_0() Тогда
		_BusinessEntityGuid = ВетисDictionaryСлой1с.BusinessEntity(пХС);
		_EnterpriseGuid     = ВетисDictionaryСлой1с.Enterprise(пПредприятие);
		
		_Operation = ВетисMercuryVetdocument_2_0.BusinessEntityActivityLocationsModificationOperation(_ModificationType, _BusinessEntityGuid, _EnterpriseGuid);
		
		_BusinessEntityR = ВетисMercuryApplications_2_0.ModifyActivityLocations(_Operation, _BusinessEntityGuid);
		
	Иначе
		
		_BusinessEntityGuid = ВетисCerberusEnterpriseСлой1с.BusinessEntity(пХС);
		_EnterpriseGuid     = ВетисCerberusEnterpriseСлой1с.Enterprise(пПредприятие);
		
		_Operation = ВетисCerberusEnterprise.BusinessEntityActivityLocationsModificationOperation(_ModificationType, _BusinessEntityGuid, _EnterpriseGuid);
		
		_BusinessEntityR = ВетисMercuryApplications.ModifyActivityLocations(_Operation, _BusinessEntityGuid);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСвязиХСПредприятие() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	_Таблица.ХозяйствующийСубъект КАК ХозяйствующийСубъект,
	|	_Таблица.Предприятие,
	|	_Таблица.ВСервисе,
	|	_СоответствиеПредприятие.guid КАК EnterpriseGuid,
	|	_СоответствиеХС.guid КАК BusinessEntityGuid
	|ИЗ
	|	РегистрСведений.ВетисПредприятияХС КАК _Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _СоответствиеХС
	|		ПО _Таблица.ХозяйствующийСубъект = _СоответствиеХС.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _СоответствиеПредприятие
	|		ПО _Таблица.Предприятие = _СоответствиеПредприятие.Ссылка
	|ГДЕ
	|	ИСТИНА
	|	И ИСТИНА
	|ИТОГИ
	|	МАКСИМУМ(BusinessEntityGuid)
	|ПО
	|	ХозяйствующийСубъект";
	
	Результат = Запрос.Выполнить();
	ВыборкаХС = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаХС.Следующий() Цикл
		
		_список = Новый Массив;
		_Enterprise = Неопределено;
		_параметры = Неопределено;
		Если ВетисXSD.Версия_2_0() Тогда
			Пока ВетисEnterpriseService_2_0.GetActivityLocationListСледующий(ВыборкаХС.BusinessEntityGuid, _Enterprise, _параметры) Цикл
				_список.Добавить(_Enterprise.guid);
			КонецЦикла;
		Иначе
			Пока ВетисEnterpriseService.GetBusinessEntityEnterpriseListСледующий(ВыборкаХС.BusinessEntityGuid, _Enterprise, _параметры) Цикл
				_список.Добавить(_Enterprise.guid);
			КонецЦикла;
		КонецЕсли;
		
		Выборка = ВыборкаХС.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.EnterpriseGuid = NULL ИЛИ Выборка.BusinessEntityGuid = NULL ИЛИ _список.Найти(Выборка.EnterpriseGuid) = Неопределено Тогда
				_ВСервисе = Ложь;
			Иначе
				_ВСервисе = Истина;
			КонецЕсли;
			
			_мз = РегистрыСведений.ВетисПредприятияХС.СоздатьМенеджерЗаписи();
			_мз.ХозяйствующийСубъект = Выборка.ХозяйствующийСубъект;
			_мз.Предприятие = Выборка.Предприятие;
			_мз.ВСервисе = _ВСервисе;
			_мз.Записать();
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры
