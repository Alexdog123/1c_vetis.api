
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СкладскойЖурналПоследние = Истина;
	
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурнал.Отбор, "last", СкладскойЖурналПоследние,,СкладскойЖурналПоследние);
	
	Элементы.СписокСкладскойЖурналСкладскойЖурналПоследние.Пометка = СкладскойЖурналПоследние;
	
	Версия = ВетисXSD.Версия();
	
	Отладка = ВетисОбщегоНазначения.РазрешенаОтладка();
	
	ВСДОтборТип.Добавить(Перечисления.ВетисVetDocumentType.Транспортный,,Истина);
	ВСДОтборТип.Добавить(Перечисления.ВетисVetDocumentType.Возвратный,,Истина);
	ВСДОтборТип.Добавить(Перечисления.ВетисVetDocumentType.Производственный);
	ВСДОтборТип.Добавить(Перечисления.ВетисVetDocumentType.Входящий);
	ВСДОтборТип.Добавить(Перечисления.ВетисVetDocumentType.Исходящий);
	
	ВСДОтборСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Оформлен,,Истина);
	ВСДОтборСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Погашен);
	ВСДОтборСтатус.Добавить(Перечисления.ВетисVetDocumentStatus.Аннулирован);
	
	Элементы.ГруппаСкладскойЖурнал.Видимость = Отладка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВСДОтборОбновить();
	
КонецПроцедуры


&НаКлиенте
Процедура ВСДАннулировать(Команда)
	
	_ТекущиеДанные = Элементы.СписокВСД.ТекущиеДанные;
	
	Если _ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ _ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.Оформлен") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ _ТекущиеДанные.Тип = ПредопределенноеЗначение("Перечисление.ВетисVetDocumentType.Транспортный") Тогда
		Возврат;
	КонецЕсли;
	
	_Причина = "";
	
	_оо = Новый ОписаниеОповещения("ВСДАннулироватьОбработкаОповещения", ЭтаФорма, _ТекущиеДанные.Ссылка);
	
	ПоказатьВводСтроки(_оо, _Причина, "Причина аннулирования " + _ТекущиеДанные.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДСинхронизировать(Команда)
	//ВетисMercuryVetdocumentСлой1с.Синхронизировать(,ХС, Предприятие, ?(ЗначениеЗаполнено(ТипВСД), ТипВСД, Неопределено), ?(ЗначениеЗаполнено(СтатусВСД), СтатусВСД, Неопределено));
	//ВетисMercuryVetdocumentСлой1с.Синхронизировать(,ХС, Предприятие, ПредопределенноеЗначение("Перечисление.ВетисVetDocumentType.Входящий"));
	//ВетисMercuryVetdocumentСлой1с.Синхронизировать(,ХС, Предприятие);
	
	Для каждого _Статус Из ВСДОтборСтатус Цикл
		Если НЕ _Статус.Пометка Тогда Продолжить; КонецЕсли;
		Для каждого _Тип Из ВСДОтборТип Цикл
			Если НЕ _Тип.Пометка Тогда Продолжить; КонецЕсли;
			ВетисMercuryVetdocumentСлой1с.Синхронизировать(,ХС, Предприятие, _Тип.Значение, _Статус.Значение);
		КонецЦикла;
	КонецЦикла;
	
	Элементы.СписокВСД.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДАннулироватьОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВетисMercuryVetdocumentСлой1с.Аннулировать(Параметр, Результат);
	
	Элементы.СписокВСД.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДОтборТип(Команда)
	
	_оо = Новый ОписаниеОповещения("ВСДОтборТипОбработкаОповещения", ЭтаФорма);
	
	ВСДОтборТип.ПоказатьОтметкуЭлементов(_оо);
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДОтборТипОбработкаОповещения(пСписок, пПараметры) Экспорт
	
	Если НЕ пСписок = Неопределено Тогда
		ВСДОтборОбновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДОтборСтатус(Команда)
	
	_оо = Новый ОписаниеОповещения("ВСДОтборСтатусОбработкаОповещения", ЭтаФорма);
	
	ВСДОтборСтатус.ПоказатьОтметкуЭлементов(_оо);
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДОтборСтатусОбработкаОповещения(пСписок, пПараметры) Экспорт
	
	Если НЕ пСписок = Неопределено Тогда
		ВСДОтборОбновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВСДОтборОбновить()
	
	_список = Новый Массив;
	
	//бумажные (заведенные через 1с) видны всегда
	_список.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentType.ПустаяСсылка"));
	
	_заголовок = "";
	
	Для каждого _элемент Из ВСДОтборТип Цикл
		Если _элемент.Пометка Тогда
			_список.Добавить(_элемент.Значение);
			_заголовок = _заголовок + "," + Строка(_элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "Тип", _список, "ВСписке");
	
	Элементы.СписокВСДОтборТип.Заголовок = Сред(_заголовок, 2);
	
	
	_список = Новый Массив;
	
	_заголовок = "";
	
	//бумажные (заведенные через 1с) видны всегда
	_список.Добавить(ПредопределенноеЗначение("Перечисление.ВетисVetDocumentStatus.ПустаяСсылка"));
	
	Для каждого _элемент Из ВСДОтборСтатус Цикл
		Если _элемент.Пометка Тогда
			_список.Добавить(_элемент.Значение);
			_заголовок = _заголовок + "," + Строка(_элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокВСД.Отбор, "Статус", _список, "ВСписке");
	
	Элементы.СписокВСДОтборСтатус.Заголовок = Сред(_заголовок, 2);
	
	_группа = ВетисОбщегоНазначенияКлиентСервер.ДобавитьГруппу(СписокВСД.Отбор, "ХС", "ГруппаИли");
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(_группа, "Отправитель", ХС);
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(_группа, "Получатель", ХС);
	
	_группа = ВетисОбщегоНазначенияКлиентСервер.ДобавитьГруппу(СписокВСД.Отбор, "Предприятие", "ГруппаИли");
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(_группа, "ОтправительПредприятие", Предприятие);
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(_группа, "ПолучательПредприятие", Предприятие);
	
КонецПроцедуры


&НаКлиенте
Процедура СинхронизироватьСкладскойЖурнал(Команда)
	
	ВетисMercuryApplicationsСлой1с.СинхронизироватьСкладскойЖурнал(ХС, Предприятие);
	
	Элементы.СписокСкладскойЖурнал.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СкладскойЖурналПоследние(Команда)
	
	СкладскойЖурналПоследние = НЕ СкладскойЖурналПоследние;
	
	ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(СписокСкладскойЖурнал.Отбор, "last", СкладскойЖурналПоследние,,СкладскойЖурналПоследние);
	
	Элементы.СписокСкладскойЖурналСкладскойЖурналПоследние.Пометка = СкладскойЖурналПоследние;
	
КонецПроцедуры

&НаКлиенте
Процедура ВерсияПриИзменении(Элемент)
	
	ВетисXSD.ВерсияУстановить(Версия);
	
	ВетисXSD.Инициализировать();
	
	Оповестить("ВетисВерсия", ЭтаФорма.ИмяФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтладкаПриИзменении(Элемент)
	
	ВетисОбщегоНазначения.РазрешенаОтладка(Отладка);
	
	Элементы.ГруппаСкладскойЖурнал.Видимость = Отладка;
	
	Оповестить("ВетисОтладка", ЭтаФорма.ИмяФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтаФорма.ИмяФормы Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисВерсия" Тогда
		Версия = ВетисXSD.Версия();
	ИначеЕсли ИмяСобытия = "ВетисОтладка" Тогда
		Отладка = ВетисОбщегоНазначения.РазрешенаОтладка();
		Элементы.ГруппаСкладскойЖурнал.Видимость = Отладка;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура СкладскойЖурналПолучитьТекущий(Команда)
	
	ТекущиеДанные = Элементы.СписокСкладскойЖурнал.ТекущиеДанные;
	
	СкладскойЖурналПолучитьТекущийСервер(ТекущиеДанные.guid);
	
КонецПроцедуры

Процедура СкладскойЖурналПолучитьТекущийСервер(guid)
	//тест:
	//http://vetrf.ru/vetrf-forum/posts/list/1185/6855.page#46389
	//В записи журнала есть объект vetDocument, в котором возвращаются реквизиты ВСД. То есть при выполнении метода, например, GetStockEntryByGuid вы получите данные записи журнала и UUID ветеринарного сертификата, на основании которого эта запись была создана. 
	//результат:
	//работает только в 2.0
	
	Если ВетисXSD.Версия_2_0() Тогда
		_enterpriseGuid = ВетисDictionaryСлой1с.Enterprise(Предприятие);
		_issuerId       = ВетисDictionaryСлой1с.BusinessEntity(ХС);
		_StockEntry     = ВетисMercuryApplications_2_0.getStockEntryByGuid(guid, _enterpriseGuid, _issuerId);
		
		Если _StockEntry.vetDocument.Количество() > 0 Тогда
			_VetDocument    = ВетисMercuryApplications_2_0.getVetDocumentByUuid(_StockEntry.vetDocument[0].uuid, _enterpriseGuid, _issuerId);
		КонецЕсли;
	Иначе
		_enterpriseGuid = ВетисCerberusEnterpriseСлой1с.Enterprise(Предприятие);
		_issuerId       = ВетисCerberusEnterpriseСлой1с.BusinessEntity(ХС);
		_StockEntry     = ВетисMercuryApplications.getStockEntryByGuid(guid, _enterpriseGuid, _issuerId);
		
		Если _StockEntry.vetDocument.Количество() > 0 Тогда
			_VetDocument    = ВетисMercuryApplications.getVetDocumentByUuid(_StockEntry.vetDocument[0].uuid, _enterpriseGuid, _issuerId);
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.СписокСкладскойЖурналСкладскойЖурналПолучитьТекущий.Заголовок = "Тест "+_StockEntry.vetDocument.Количество();
	
КонецПроцедуры


&НаКлиенте
Процедура ПредприятиеПриИзменении(Элемент)
	
	ВСДОтборОбновить();
	
КонецПроцедуры


&НаКлиенте
Процедура ХСПриИзменении(Элемент)
	
	ВСДОтборОбновить();
	
КонецПроцедуры

