
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Имя") Тогда
		_Имя = Параметры.Имя;
	Иначе
		_Имя = "BusinessEntity";
	КонецЕсли;
	
	_Имя = Вычислить("ВетисИмяСправочника."+_Имя+"()");
	
	Список.ОсновнаяТаблица = "Справочник."+_Имя;
	
	Если Параметры.Свойство("Запрос") Тогда
		Список.ТекстЗапроса = Параметры.Запрос;
	Иначе
		Список.ТекстЗапроса = ТекстЗапроса(_Имя);
	КонецЕсли;
	
	Если Параметры.Свойство("ЗапросСоединения") Тогда
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "//СОЕДИНЕНИЯ", Параметры.ЗапросСоединения);
	КонецЕсли;
	
	Если Параметры.Свойство("ЗапросУсловия") Тогда
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "//УСЛОВИЯ", Параметры.ЗапросУсловия);
	КонецЕсли;
	
	Если Параметры.Свойство("ЗапросПараметры") Тогда
		Для каждого _Параметр Из Параметры.ЗапросПараметры Цикл
			Список.Параметры.УстановитьЗначениеПараметра(_Параметр.Ключ, _Параметр.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("Ключ") Тогда
		Элементы.Список.ТекущаяСтрока = Параметры.Ключ;
	КонецЕсли;
	
	Элементы.Список.ПодчиненныеЭлементы.Наименование.Видимость = Метаданные.Справочники[_Имя].ДлинаНаименования > 0;
	
	Элементы.Список.ПодчиненныеЭлементы.Код.Видимость = Метаданные.Справочники[_Имя].ДлинаКода > 0;
	
	Элементы.Список.ПодчиненныеЭлементы.Владелец.Видимость = Метаданные.Справочники[_Имя].Владельцы.Количество() > 0;
	
	Если Параметры.Свойство("Родитель") Тогда
		Если ТипЗнч(Параметры.Родитель) = Тип("СписокЗначений") Тогда
			ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "Родитель", Параметры.Родитель, "ВСписке");
		Иначе
			ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "Родитель", Параметры.Родитель);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Владелец") Тогда
		Элементы.Список.ПодчиненныеЭлементы.Владелец.Видимость = Ложь;
		Если ТипЗнч(Параметры.Владелец) = Тип("СписокЗначений") Тогда
			ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "Владелец", Параметры.Владелец, "ВСписке");
		Иначе
			ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, "Владелец", Параметры.Владелец);
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("Отборы") Тогда
		Для каждого _Отбор Из Параметры.Отборы Цикл
			Если ТипЗнч(_Отбор.Значение) = Тип("СписокЗначений") Тогда
				ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, _Отбор.Ключ, _Отбор.Значение, "ВСписке");
			Иначе
				ВетисОбщегоНазначенияКлиентСервер.ДобавитьОтбор(Список.Отбор, _Отбор.Ключ, _Отбор.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("Отображение") Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы[Параметры.Отображение];
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапроса(пИмя)
	
	_Текст =
	"ВЫБРАТЬ
	|	_Таблица.Ссылка КАК Ссылка,
	|//пк	_Таблица.Код КАК Код,
	|//пн	_Таблица.Наименование КАК Наименование,
	|	ВЫБОР
	|		КОГДА _Ветис.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Подсветка
	|ИЗ
	|	Справочник."+пИмя+" КАК _Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _Ветис
	|		ПО _Таблица.Ссылка = _Ветис.Ссылка
	|//СОЕДИНЕНИЯ
	|ГДЕ
	|	ИСТИНА
	|	И НЕ _Таблица.ПометкаУдаления
	|//ои	И НЕ _Таблица.ЭтоГруппа
	|//УСЛОВИЯ
	|	И Истина";
	
	Если Метаданные.Справочники[пИмя].ДлинаНаименования > 0 Тогда
		_Текст = СтрЗаменить(_Текст, "//пн", "");
	КонецЕсли;
	
	Если Метаданные.Справочники[пИмя].ДлинаКода > 0 Тогда
		_Текст = СтрЗаменить(_Текст, "//пк", "");
	КонецЕсли;
	
	Если Метаданные.Справочники[пИмя].Иерархический
		И Метаданные.Справочники[пИмя].ВидИерархии = Метаданные.СвойстваОбъектов.ВидИерархии.ИерархияГруппИЭлементов Тогда
		_Текст = СтрЗаменить(_Текст, "//ои", "");
	КонецЕсли;
	
	Возврат _Текст;
	
КонецФункции
