
Функция Логин() Экспорт
	Возврат ВетисПеременные.Получить("ВетисЛогин");
КонецФункции

Функция Пароль() Экспорт
	Возврат ВетисПеременные.Получить("ВетисПароль");
КонецФункции

Функция apiKey() Экспорт
	Возврат ВетисПеременные.Получить("ВетисКлючАПИ");
КонецФункции

Функция serviceId() Экспорт
	Возврат "mercury-g2b.service" + ВетисXSD.ВерсияПредставление(":");
КонецФункции

Функция ЛогинВрача() Экспорт
	Возврат ВетисПеременные.Получить("ВетисЛогинВрача");
КонецФункции

Функция Врач() Экспорт
	Если ВетисXSD.Версия_2_0() Тогда
		Возврат ВетисMercuryVetdocument_2_0.User(ЛогинВрача(), ВетисПеременные.Получить("ВетисВрач"), ВетисПеременные.Получить("ВетисДолжностьВрача"));
	Иначе
		Возврат ВетисArgusCommon.User(ЛогинВрача(), ВетисПеременные.Получить("ВетисВрач"), ВетисПеременные.Получить("ВетисДолжностьВрача"));
	КонецЕсли;
КонецФункции

//логин пользователя ХС
Функция ЛогинХС() Экспорт
	Возврат ВетисПеременные.Получить("ВетисЛогинХС");
КонецФункции

Функция issuerId() Экспорт
	Возврат ВетисПеременные.Получить("ВетисИдентификаторХС");
КонецФункции


Процедура Инициализировать() Экспорт
	
	#Если НЕ МобильноеПриложениеСервер Тогда
		Если НЕ РольДоступна("ВетисПользователь") Тогда
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	ИнициализироватьНастройкиХС();
	
	ВетисXSD.Инициализировать();
	
КонецПроцедуры

//Устанавливает параметры соединения для заданного или основного ХС
Функция ИнициализироватьНастройкиХС(пХС = Неопределено) Экспорт
	
	Если пХС = Неопределено Тогда
		_ХС = ВетисХозяйственныеОперации.ОсновнойХС();
		_issuerId = ВетисCerberusEnterpriseСлой1с.BusinessEntity(_ХС);
		
	ИначеЕсли ТипЗнч(пХС) = Тип(ВетисИмяСправочника.BusinessEntity("СправочникСсылка")) Тогда
		Если ВетисХозяйственныеОперации.ОсновнойХС() = пХС Тогда
			Возврат issuerId();
		Иначе
			_ХС = пХС;
			_issuerId = ВетисCerberusEnterpriseСлой1с.BusinessEntity(пХС);
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(пХС) = Тип("Строка") И НЕ ПустаяСтрока(пХС) Тогда
		Если issuerId() = пХС Тогда
			Возврат пХС;
		Иначе
			_ХС = ВетисCerberusEnterpriseСлой1с.BusinessEntity(пХС);
			_issuerId = пХС;
		КонецЕсли;
		
	Иначе
		Возврат Неопределено;
		
	КонецЕсли;
	
	_Настройки = РегистрыСведений.ВетисНастройкиХС.СоздатьМенеджерЗаписи();
	
	_Настройки.ХозяйствующийСубъект = _ХС;
	
	_Настройки.Прочитать();
	
	ВетисПеременные.Установить("ВетисЛогин",          _Настройки.Логин);
	ВетисПеременные.Установить("ВетисПароль",         _Настройки.Пароль);
	ВетисПеременные.Установить("ВетисЛогинВрача",     _Настройки.ЛогинВрача);
	ВетисПеременные.Установить("ВетисВрач",           _Настройки.Врач);
	ВетисПеременные.Установить("ВетисДолжностьВрача", _Настройки.ДолжностьВрача);
	
	ВетисПеременные.Установить("ВетисКлючАПИ",        _Настройки.КлючАПИ);
	
	ВетисПеременные.Установить("ВетисХС", _ХС);
	ВетисПеременные.Установить("ВетисИдентификаторХС", _issuerId);
	
	ИнициализироватьНастройкиПользователя(_ХС);
	
	Возврат _issuerId;
	
КонецФункции

Процедура ИнициализироватьНастройкиПользователя(пХС = Неопределено)
	
	_мз = РегистрыСведений.ВетисПользователи.СоздатьМенеджерЗаписи();
	_мз.Пользователь = Пользователи.ТекущийПользователь();
	_мз.ХозяйствующийСубъект = ?(пХС = Неопределено, ВетисХозяйственныеОперации.ОсновнойХС(), пХС);
	
	_мз.Прочитать();
	
	Если _мз.Выбран() Тогда
		ВетисПеременные.Установить("ВетисЛогинХС",  _мз.Логин);
	Иначе
		ВетисПеременные.Установить("ВетисЛогинХС",  "");
	КонецЕсли;
	
КонецПроцедуры
