
Процедура Инициализировать() Экспорт
	
	//вызывать при:
	// после изменения тестовый - рабочий
	// после изменения версии
	
	_версия = ВетисОбщегоНазначения.Версия();
	
	Если _версия = Перечисления.ВетисВерсияAPI.v2_0 Тогда
		_имя = "http://api.vetrf.ru/schema/platform/services/2.0-RC-last/";
		_суф = "_v2.0_pilot.wsdl";
		
		_таблица = Неопределено;
		
		WSDLДобавить(_таблица, "ApplicationManagementService", "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _Имя+"ApplicationManagementService_v1.1.wsdl");
		
		WSDLДобавить(_таблица, "DictionaryService", "http://api.vetrf.ru/schema/cdm/registry/dictionary/service/v2", "DictionaryServiceBindingQSService", "DictionaryServiceBindingQSPort", _Имя+"DictionaryService"+_суф);
		WSDLДобавить(_таблица, "EnterpriseService", "http://api.vetrf.ru/schema/cdm/registry/enterprise/service/v2", "EnterpriseServiceBindingQSService", "EnterpriseServiceBindingQSPort", _Имя+"EnterpriseService"+_суф);
		WSDLДобавить(_таблица, "IkarService",       "http://api.vetrf.ru/schema/cdm/registry/ikar/service/v2",       "IkarServiceBindingQSService",       "IkarServiceBindingQSPort",       _Имя+"IkarService"+_суф);
		WSDLДобавить(_таблица, "ProductService",    "http://api.vetrf.ru/schema/cdm/registry/product/service/v2",    "ProductServiceBindingQSService",    "ProductServiceBindingQSPort",    _Имя+"ProductService"+_суф);
		
		ВетисПеременные.Установить("wsdl", _таблица);
		
		_имя = "http://api.vetrf.ru/schema/platform/services/2.0-RC-last/";
		_суф = "_v2.0.xsd";
		
		_таблица = Неопределено;
		
		XSDДобавить(_таблица, _Имя+"application_v1.1.xsd",          "http://api.vetrf.ru/schema/cdm/application");
		XSDДобавить(_таблица, _Имя+"base_v1.1.xsd",                 "http://api.vetrf.ru/schema/cdm/base");
		XSDДобавить(_таблица, _Имя+"dictionary"+_суф,               "http://api.vetrf.ru/schema/cdm/dictionary/v2");
		XSDДобавить(_таблица, _Имя+"document"+_суф,                 "http://api.vetrf.ru/schema/cdm/mercury/vet-document/v2");
		XSDДобавить(_таблица, _Имя+"mercury_g2b_applications"+_суф, "http://api.vetrf.ru/schema/cdm/mercury/g2b/applications/v2");
		
		ВетисПеременные.Установить("xsd", _таблица);
		
	Иначе
		_имя = "http://api.vetrf.ru/schema/platform/";
		_суф = "_v1.4_pilot.wsdl";
		
		_таблица = Неопределено;
		
		WSDLДобавить(_таблица, "ApplicationManagementService", "http://api.vetrf.ru/schema/cdm/application/service", "ApplicationManagementServiceBindingQSService", "ApplicationManagementServiceBindingQSPort", _Имя+"services/ApplicationManagementService"+_суф);
		
		WSDLДобавить(_таблица, "DictionaryService", "http://api.vetrf.ru/schema/cdm/registry/service", "DictionaryServiceBindingQSService", "DictionaryServiceBindingQSPort", _Имя+"services/DictionaryService"+_суф);
		WSDLДобавить(_таблица, "EnterpriseService", "http://api.vetrf.ru/schema/cdm/registry/service", "EnterpriseServiceBindingQSService", "EnterpriseServiceBindingQSPort", _Имя+"cerberus/services/EnterpriseService"+_суф);
		WSDLДобавить(_таблица, "IkarService",       "http://api.vetrf.ru/schema/cdm/registry/service", "IkarServiceBindingQSService",       "IkarServiceBindingQSPort",       _Имя+"ikar/services/IkarService"+_суф);
		WSDLДобавить(_таблица, "ProductService",    "http://api.vetrf.ru/schema/cdm/registry/service", "ProductServiceBindingQSService",    "ProductServiceBindingQSPort",    _Имя+"services/ProductService"+_суф);
		
		ВетисПеременные.Установить("wsdl", _таблица);
		
		_имя = "http://api.vetrf.ru/schema/platform/mercury/g2b/";
		_суф = "_v1.4.xsd";
		
		_таблица = Неопределено;
		
		XSDДобавить(_таблица, _Имя+"application"+_суф,         "http://api.vetrf.ru/schema/cdm/application");
		XSDДобавить(_таблица, _Имя+"argus_common"+_суф,        "http://api.vetrf.ru/schema/cdm/argus/common");
		XSDДобавить(_таблица, _Имя+"production"+_суф,          "http://api.vetrf.ru/schema/cdm/argus/production");
		XSDДобавить(_таблица, _Имя+"argus_shipment"+_суф,      "http://api.vetrf.ru/schema/cdm/argus/shipment");
		XSDДобавить(_таблица, _Имя+"base"+_суф,                "http://api.vetrf.ru/schema/cdm/base");
		XSDДобавить(_таблица, _Имя+"enterprise"+_суф,          "http://api.vetrf.ru/schema/cdm/cerberus/enterprise");
		XSDДобавить(_таблица, _Имя+"ikar"+_суф,                "http://api.vetrf.ru/schema/cdm/ikar");
		XSDДобавить(_таблица, _Имя+"applications"+_суф,        "http://api.vetrf.ru/schema/cdm/mercury/applications");
		XSDДобавить(_таблица, _Имя+"veterinary_document"+_суф, "http://api.vetrf.ru/schema/cdm/mercury/vet-document");
		
		ВетисПеременные.Установить("xsd", _таблица);
		
	КонецЕсли;
	
	СброситьКеш();
	
	ФабрикаОбщая();
	
КонецПроцедуры

Процедура СброситьКеш()
	
	ВетисПеременные.Установить("КешФабрики", Новый Соответствие);
	ВетисПеременные.Установить("КешОпределения", Новый Соответствие);
	
КонецПроцедуры


Функция ФабрикаОбщая() Экспорт
	
	_Кеш = ВетисПеременные.Получить("КешФабрики", Новый Соответствие);
	
	_фабрика = _Кеш.Получить("Общая");
	
	Если НЕ _фабрика = Неопределено Тогда
		Возврат _фабрика;
	КонецЕсли;
	
	_url = XSDПолучитьURL();
	
	Если _url.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	_фабрика = СоздатьФабрикуXDTO(_url);
	
	_Кеш.Вставить("Общая", _фабрика);
	
	ВетисПеременные.Установить("КешФабрики", _Кеш);
	
	Возврат _фабрика;
	
КонецФункции

Функция Прокси(пИмя) Экспорт
	
	_ТаймАутПрокси = 600;
	_ЗащищенноеСоедиенение = Неопределено;
	
	Попытка
		
		_wsdl = WSDLПолучить(пИмя);
		
		_Определение = ОпределенияПоURL(_wsdl.URL);
		
		Если _ЗащищенноеСоедиенение = Неопределено Тогда
			_Прокси = Новый WSПрокси(_Определение, _wsdl.URI, _wsdl.Сервис, _wsdl.Порт,, _ТаймАутПрокси);
		Иначе
			_Прокси = Новый WSПрокси(_Определение, _wsdl.URI, _wsdl.Сервис, _wsdl.Порт,, _ТаймАутПрокси, _ЗащищенноеСоедиенение);
		КонецЕсли;
		
		_Прокси.Пользователь = ВетисПараметрыСоединения.Логин();
		_Прокси.Пароль = ВетисПараметрыСоединения.Пароль();
		
		Возврат _Прокси;
		
	Исключение
		ВетисЖурнал.ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке());
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

Функция Определения(пИмя, пЗащищенноеСоедиенение = Неопределено) Экспорт
	
	Возврат ОпределенияПоURL(WSDLПолучить(пИмя).URL, пЗащищенноеСоедиенение);
	
КонецФункции

Функция ОпределенияПоURL(пURL, пЗащищенноеСоедиенение = Неопределено)
	
	_Кеш = ВетисПеременные.Получить("КешОпределения", Новый Соответствие);
	
	_Определения = _Кеш.Получить(пURL);
	
	Если НЕ _Определения = Неопределено Тогда
		Возврат _Определения;
	КонецЕсли;
	
	_ТаймАутПрокси = 60;
	
	Попытка
		Если пЗащищенноеСоедиенение = Неопределено Тогда
			_Определения = Новый WSОпределения(пURL, ВетисПараметрыСоединения.Логин(), ВетисПараметрыСоединения.Пароль(), , _ТаймАутПрокси);
		Иначе
			_Определения = Новый WSОпределения(пURL, ВетисПараметрыСоединения.Логин(), ВетисПараметрыСоединения.Пароль(), , _ТаймАутПрокси, пЗащищенноеСоедиенение);
		КонецЕсли;
	Исключение
		ВетисЖурнал.ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке());
		ВызватьИсключение;
	КонецПопытки;
	
	_Кеш.Вставить(пURL, _Определения);
	
	ВетисПеременные.Установить("КешОпределения", _Кеш);
	
	Возврат _Определения;
	
КонецФункции


Процедура WSDLДобавить(пТаблица, пИмя, пURI, пСервис, пПорт, пURL)
	
	Если пТаблица = Неопределено Тогда
		пТаблица = Новый ТаблицаЗначений;
		пТаблица.Колонки.Добавить("Имя");
		пТаблица.Колонки.Добавить("URI");
		пТаблица.Колонки.Добавить("Сервис");
		пТаблица.Колонки.Добавить("Порт");
		пТаблица.Колонки.Добавить("URL");
	КонецЕсли;
	
	_строка = пТаблица.Добавить();
	_строка.Имя    = пИмя;
	_строка.URI    = пURI;
	_строка.Сервис = пСервис;
	_строка.Порт   = пПорт;
	_строка.URL    = пURL;
	
КонецПроцедуры

Функция WSDLПолучить(пИмя)
	
	_таблица = ВетисПеременные.Получить("wsdl");
	
	Возврат _таблица.Найти(пИмя, "Имя");
	
КонецФункции

Функция WSDLПолучитьURL() Экспорт
	
	_таблица = ВетисПеременные.Получить("wsdl");
	
	_список = Новый Массив;
	
	Для каждого _строка Из _таблица Цикл
		_список.Добавить(_строка.url);
	КонецЦикла;
	
	Возврат _список;
	
КонецФункции


Процедура XSDДобавить(пТаблица, пURL, пURI)
	
	Если пТаблица = Неопределено Тогда
		пТаблица = Новый ТаблицаЗначений;
		пТаблица.Колонки.Добавить("URI");
		пТаблица.Колонки.Добавить("URL");
	КонецЕсли;
	
	_строка = пТаблица.Добавить();
	_строка.URI    = пURI;
	_строка.URL    = пURL;
	
КонецПроцедуры

Функция XSDПолучить()
	
	_таблица = ВетисПеременные.Получить("xsd");
	
	Возврат _таблица;
	
КонецФункции

Функция XSDПолучитьURL() Экспорт
	
	_таблица = ВетисПеременные.Получить("xsd");
	
	_список = Новый Массив;
	
	Для каждого _строка Из _таблица Цикл
		_список.Добавить(_строка.url);
	КонецЦикла;
	
	Возврат _список;
	
КонецФункции
