
// Предназначена для получения записи продукции
Функция GetProductByGuid(guid, ПоУмолчанию = Неопределено, пВызватьИсключение = Истина) Экспорт
	
	Если Ветис.Настройки_СправочникПродукцииЛокально() Тогда
		Возврат ВетисХозяйственныеОперации.GetProductByGuid(guid);
	КонецЕсли;
	
	Попытка
		Возврат Прокси().GetProductByGuid(guid);
	Исключение
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetProductByGuid");
		Если пВызватьИсключение = Истина Тогда ВызватьИсключение; КонецЕсли;
		Возврат ПоУмолчанию;
	КонецПопытки;
	
КонецФункции

// Предназначена для получения записи о виде продукции
Функция GetSubProductByGuid(guid, ПоУмолчанию = Неопределено, пВызватьИсключение = Истина) Экспорт
	
	Если Ветис.Настройки_СправочникПродукцииЛокально() Тогда
		Возврат ВетисХозяйственныеОперации.GetSubProductByGuid(guid);
	КонецЕсли;
	
	Попытка
		Возврат Прокси().GetSubProductByGuid(guid);
	Исключение
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetSubProductByGuid");
		Если пВызватьИсключение = Истина Тогда ВызватьИсключение; КонецЕсли;
		Возврат ПоУмолчанию;
	КонецПопытки;
	
КонецФункции

// предназначена для получения записи о номенклатуре производителя
Функция GetProductItemByGuid(guid, ПоУмолчанию = Неопределено, пВызватьИсключение = Истина) Экспорт
	
	Попытка
		Возврат Прокси().GetProductItemByGuid(guid);
	Исключение
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetProductItemByGuid");
		Если пВызватьИсключение = Истина Тогда ВызватьИсключение; КонецЕсли;
		Возврат ПоУмолчанию;
	КонецПопытки;
	
КонецФункции


// Предназначена для получения списка продукции по указанному типу.
Функция GetProductByTypeList(productType, пListOptions = Неопределено) Экспорт
	
	Если пListOptions = Неопределено Тогда
		пListOptions = ВетисBase_2_0.ListOptions();
	Иначе
		пListOptions.offset = пListOptions.offset + пListOptions.count;
	КонецЕсли;
	
	Попытка
		Возврат Прокси().GetProductByTypeList(пListOptions, productType)
	Исключение
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetProductByTypeList");
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

// Предназначена для Выборки продукции по указанному типу.
// Используется самостоятельно в цикле Пока.
// Параметры:
//  productType - тип продукта
//  Item - элемент списка, возвращаемый параметр
//  пПараметры - для внутреннего использования, начальное значение - Неопределено
// Возвращаемое значение:
//	Булево - Ложь значит список пройден весь
Функция GetProductByTypeListСледующий(productType, пItem, пПараметры) Экспорт
	
	Если Ветис.Настройки_СправочникПродукцииЛокально() Тогда
		Если пПараметры = Неопределено Тогда
			пПараметры = Новый Структура;
			_Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ _Таблица.Product КАК guid, _Таблица.ProductName КАК name ИЗ РегистрСведений.ВетисСправочникПродукции КАК _Таблица ГДЕ _Таблица.ProductType = &productType");
			_Запрос.УстановитьПараметр("productType", productType);
			_Результат = _Запрос.Выполнить();
			пПараметры.Вставить("ListResponse", _Результат.Выбрать());
		КонецЕсли;
		_Результат = пПараметры.ListResponse.Следующий();
		пItem = пПараметры.ListResponse;
		Возврат _Результат;
	Иначе
		Если пПараметры = Неопределено Тогда
			пПараметры = Новый Структура;
			пПараметры.Вставить("ListOptions", ВетисBase_2_0.ListOptions());
			пПараметры.Вставить("Индекс", 0);
		Иначе
			пПараметры.Индекс = пПараметры.Индекс + 1;
			Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
				Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
					Возврат Ложь;
				Иначе
					пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
					пПараметры.Индекс = 0;
				КонецЕсли;
			Иначе
				пItem = пПараметры.ListResponse.product[пПараметры.Индекс];
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			_ListResponse = Прокси().GetProductByTypeList(пПараметры.ListOptions, productType);
		Исключение
			Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetProductByTypeListСледующий");
			ВызватьИсключение;
		КонецПопытки;
		
		пПараметры.Вставить("ListResponse", _ListResponse);
		
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Возврат Ложь;
		Иначе
			пItem = пПараметры.ListResponse.product[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


// Предназначена для получения списка видов продукции, отфильтрованного по указанному продукту.
Функция GetSubProductByProductList(productGuid, пListOptions = Неопределено) Экспорт
	
	Если пListOptions = Неопределено Тогда
		пListOptions = ВетисBase_2_0.ListOptions();
	Иначе
		пListOptions.offset = пListOptions.offset + пListOptions.count;
	КонецЕсли;
	
	Попытка
		Возврат Прокси().GetSubProductByProductList(пListOptions, productGuid);
	Исключение
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetSubProductByProductList");
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

// Предназначена для выборки видов продукции, отфильтрованного по указанному продукту.
// Используется самостоятельно в цикле Пока.
// Параметры:
//  productGuid - ссылка на продукт
//  Item - элемент списка, возвращаемый параметр
//  пПараметры - для внутреннего использования, начальное значение - Неопределено
// Возвращаемое значение:
//	Булево - Ложь значит список пройден весь
Функция GetSubProductByProductListСледующий(productGuid, пItem, пПараметры) Экспорт
	
	Если Ветис.Настройки_СправочникПродукцииЛокально() Тогда
		Если пПараметры = Неопределено Тогда
			пПараметры = Новый Структура;
			_Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ _Таблица.SubProduct КАК guid, _Таблица.SubProductName КАК name, _Таблица.Product КАК productGuid, _Таблица.ProductType КАК ProductType ИЗ РегистрСведений.ВетисСправочникПродукции КАК _Таблица ГДЕ _Таблица.Product = &productGuid");
			_Запрос.УстановитьПараметр("productGuid", productGuid);
			_Результат = _Запрос.Выполнить();
			пПараметры.Вставить("ListResponse", _Результат.Выбрать());
		КонецЕсли;
		_Результат = пПараметры.ListResponse.Следующий();
		пItem = пПараметры.ListResponse;
		Возврат _Результат;
	Иначе
		Если пПараметры = Неопределено Тогда
			пПараметры = Новый Структура;
			пПараметры.Вставить("ListOptions", ВетисBase_2_0.ListOptions());
			пПараметры.Вставить("Индекс", 0);
		Иначе
			пПараметры.Индекс = пПараметры.Индекс + 1;
			Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
				Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
					Возврат Ложь;
				Иначе
					пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
					пПараметры.Индекс = 0;
				КонецЕсли;
			Иначе
				пItem = пПараметры.ListResponse.subProduct[пПараметры.Индекс];
				Возврат Истина;
			КонецЕсли;
		КонецЕсли;
		
		Попытка
			_ListResponse = Прокси().GetSubProductByProductList(пПараметры.ListOptions, productGuid);
		Исключение
			Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetSubProductByProductListСледующий");
			ВызватьИсключение;
		КонецПопытки;
		
		пПараметры.Вставить("ListResponse", _ListResponse);
		
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Возврат Ложь;
		Иначе
			пItem = пПараметры.ListResponse.subProduct[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


//Предназначена для получения списка наименований продукции с возможностью фильтрации по уровням продукции иерархического справочника,
// по номенклатуре определенного предприятия-производителя или всех предприятий определенного хозяйствующего субъекта.
//Параметры:
// subProductGuid - Глобальный идентификатор вида продукции, для которого нужно получить номенклатуру. bs:UUID 0..1
// enterpriseGuid - Глобальный идентификатор предприятия, на котором производится или хранится продукция. bs:UUID 1
Функция GetProductItemList(productType, product, subProduct, businessEntity, enterprise, ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase_2_0.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Попытка
		Возврат Прокси().GetProductItemList(ListOptions, productType, product, subProduct, businessEntity, enterprise);
	Исключение
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetProductItemList");
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

//Предназначена для получения списка наименований продукции с возможностью фильтрации по уровням продукции иерархического справочника,
// по номенклатуре определенного предприятия-производителя или всех предприятий определенного хозяйствующего субъекта.
//Используется самостоятельно в цикле Пока.
//Параметры:
// productType - Тип продукции
// product - Продукция
// subProduct - Вид продукции
// businessEntity - Данные о хозяйствующем субъекте
// enterprise - Данные о предприятии-производителе продукции
// productItem - элемент списка, возвращаемый параметр
// пПараметры - для внутреннего использования, начальное значение - Неопределено
//Возвращаемое значение:
// Булево - Ложь значит список пройден весь
Функция GetProductItemListСледующий(productType, product, subProduct, businessEntity, enterprise, productItem, пПараметры) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("ListOptions", ВетисBase_2_0.ListOptions());
		пПараметры.Вставить("Индекс", 0);
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			productItem = пПараметры.ListResponse.productItem[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ListResponse = Прокси().GetProductItemList(пПараметры.ListOptions, productType, product, subProduct, businessEntity, enterprise);
	Исключение
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetProductItemListСледующий");
		ВызватьИсключение;
	КонецПопытки;
	
	пПараметры.Вставить("ListResponse", ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		productItem = пПараметры.ListResponse.productItem[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


Функция ИмяСервиса() Экспорт
	
	Возврат "ProductService";
	
КонецФункции

Функция Прокси()
	
	_Прокси = Ветис.Прокси("ProductService");
	
	Возврат _Прокси;
	
КонецФункции
