
Функция Десериализовать(пФабрика, пТекст, пТип) Экспорт
	
	_Чтение = Новый ЧтениеXML;
	
	_Чтение.УстановитьСтроку(пТекст);
	
	Возврат пФабрика.ПрочитатьXML(_Чтение, пТип);
	
КонецФункции

Функция Сериализовать(пФабрика, пОбъект, пЛокальноеИмя = Неопределено, пURI = Неопределено) Экспорт
	
	_ЗаписьXML = Новый ЗаписьXML;  
	_ЗаписьXML.УстановитьСтроку("UTF-8");  
	
	Если пЛокальноеИмя = Неопределено Тогда
		пФабрика.ЗаписатьXML(_ЗаписьXML, пОбъект);  
	Иначе
		пФабрика.ЗаписатьXML(_ЗаписьXML, пОбъект, пЛокальноеИмя, пURI);  
	КонецЕсли;
	
	Возврат _ЗаписьXML.Закрыть();
	
КонецФункции

Функция ПривестиКТипу(пОбъект, пИмя, пURI, пФабрика = Неопределено) Экспорт
	
	Если пФабрика = Неопределено Тогда
		_Фабрика = ВетисXSD.ФабрикаОбщая();
	Иначе
		_Фабрика = пФабрика;
	КонецЕсли;
	
	_Строка = Сериализовать(_Фабрика, пОбъект, пИмя, пURI);
	
	Возврат Десериализовать(_Фабрика, _Строка, _Фабрика.Тип(пURI, пИмя));
	
КонецФункции


Функция ВывестиСообщение(пТекст, пКонтекст = "") Экспорт
	
	ВетисЖурнал.Добавить(?(пКонтекст = "", "", Строка(пКонтекст) + ": ") + пТекст);
	
КонецФункции

Функция ВывестиСообщениеОбОшибке(пИнформацияОбОшибке, пКонтекст = "", пКратко = Ложь) Экспорт
	
	Если пКратко Тогда
		_Текст = КраткоеПредставлениеОшибки(пИнформацияОбОшибке);
	Иначе
		_Текст = ПодробноеПредставлениеОшибки(пИнформацияОбОшибке);
	КонецЕсли;
	
	ВетисЖурнал.Добавить(?(пКонтекст = "", "", Строка(пКонтекст) + ": ") + _Текст);
	
КонецФункции

Функция ПолучитьНакопленныеСообщения(пУдалять = Истина) Экспорт
	
	ТекстОшибки = "";
	
	СообщенияПользователю = ПолучитьСообщенияПользователю(пУдалять);
	
	Для Каждого Сообщение Из СообщенияПользователю Цикл
		ТекстОшибки = ТекстОшибки + Сообщение.Текст + Символы.ПС;
	КонецЦикла;

	Возврат ТекстОшибки;
	
КонецФункции


Функция ПолучитьИтератор(List, пИмя) Экспорт
	
	Если ТипЗнч(List) = Тип("ОбъектXDTO") Тогда
		Если List.Свойства().Получить(пИмя) = Неопределено Тогда
			Возврат Новый Массив;
		Иначе
			Если ТипЗнч(List[пИмя]) = Тип("СписокXDTO") Тогда
				Возврат List[пИмя];
			Иначе
				_Список = Новый Массив;
				_Список.Добавить(List[пИмя]);
				Возврат _Список;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция Создать(пИмя, пURI, пФабрика = Неопределено) Экспорт
	
	Если пФабрика = Неопределено Тогда
		_Фабрика = ВетисXSD.ФабрикаОбщая();
	Иначе
		_Фабрика = пФабрика;
	КонецЕсли;
	
	Возврат _Фабрика.Создать(_Фабрика.Тип(пURI, пИмя));
	
КонецФункции


Процедура УстановитьЗначение(пОбъект, пЗначение, пИмяСвойства = "guid", пФункцияЗначения = Неопределено) Экспорт
	
	Если ТипЗнч(пЗначение) = Тип("Структура") Тогда
		Для каждого _Значение Из пЗначение Цикл
			Если НЕ _Значение.Значение = Неопределено Тогда
				УстановитьЗначение(пОбъект, _Значение.Значение, _Значение.Ключ, пФункцияЗначения);
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли пОбъект.Свойства().Получить(пИмяСвойства) = Неопределено Тогда
		//выходим
		//дальше имя свойства обязательно
	ИначеЕсли ТипЗнч(пОбъект[пИмяСвойства]) = Тип("СписокXDTO") Тогда
		Если ТипЗнч(пЗначение) = Тип("Массив") Тогда
			Для каждого _Значение Из пЗначение Цикл
				Если НЕ пФункцияЗначения = Неопределено Тогда
					_Значение = Вычислить(пФункцияЗначения+"(_Значение)");
				КонецЕсли;
				пОбъект[пИмяСвойства].Добавить(_Значение);
			КонецЦикла;
		ИначеЕсли НЕ пЗначение = Неопределено Тогда
			Если пФункцияЗначения = Неопределено Тогда
				_Значение = пЗначение;
			Иначе
				_Значение = Вычислить(пФункцияЗначения+"(пЗначение)");
			КонецЕсли;
			пОбъект[пИмяСвойства].Добавить(_Значение);
		КонецЕсли;
	ИначеЕсли НЕ пЗначение = Неопределено Тогда
		Если пФункцияЗначения = Неопределено Тогда
			_Значение = пЗначение;
		Иначе
			_Значение = Вычислить(пФункцияЗначения+"(пЗначение)");
		КонецЕсли;
		пОбъект[пИмяСвойства] = _Значение;
	КонецЕсли;
	
КонецПроцедуры


//Отладка работает только в текущем сеансе
Функция РазрешенаОтладка(пУстановить = Неопределено) Экспорт
	
	Если пУстановить = Неопределено Тогда
		Возврат ВетисПеременные.Получить("ВетисОтладка", Ложь);
	Иначе
		ВетисПеременные.Установить("ВетисОтладка", пУстановить);
	КонецЕсли;
	
КонецФункции


Функция ВремяВетис(пДата = Неопределено) Экспорт
	
	Возврат УниверсальноеВремя(?(пДата = Неопределено, ТекущаяДата(), пДата), "GMT+2");
	
КонецФункции

Функция ВремяБД(пДата = Неопределено) Экспорт
	//похоже время в шлюзе конвертируется
	
	//Возврат УниверсальноеВремя(?(пДата = Неопределено, ТекущаяДата(), пДата), "GMT-2");
	Возврат ?(пДата = Неопределено, ТекущаяДата(), пДата);
	
КонецФункции


Процедура УдалитьПомеченныеОбъекты(пИмяТаблицы) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	_Таблица.Ссылка
	|ИЗ
	|	"+пИмяТаблицы+" КАК _Таблица
	|ГДЕ
	|	Истина
	|	И ПометкаУдаления";
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Попытка
			УдалитьПоСсылке(Выборка.Ссылка);
		Исключение
			ВетисЖурнал.ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), Выборка.Ссылка);
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьПоСсылке(пСсылка, пРодитель = Неопределено) Экспорт
	
	_Ссылки = Новый Массив;
	_Ссылки.Добавить(пСсылка);
	
	_ТабСсылок = НайтиПоСсылкам(_Ссылки);
	
	_МассивНайденных = _ТабСсылок.НайтиСтроки(Новый Структура("Ссылка", пСсылка));
	Если _МассивНайденных.Количество() = 0 Тогда
		_Объект = пСсылка.ПолучитьОбъект();
		УдалитьДанные(_Объект);
	Иначе
		_МожноУдалять = Истина;
		Для каждого _Найденная Из _МассивНайденных Цикл
			Если _Найденная.Данные = пРодитель Тогда
				Продолжить;
				
			ИначеЕсли Метаданные.РегистрыСведений.Содержит(_Найденная.Метаданные) Тогда
				_мз = РегистрыСведений[_Найденная.Метаданные.Имя].СоздатьМенеджерЗаписи();
				ЗаполнитьЗначенияСвойств(_мз, _Найденная.Данные);
				Попытка
					_мз.Удалить();
				Исключение
					ВетисЖурнал.ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "УдалитьПоСсылке:"+_Найденная.Метаданные.Имя);
					_МожноУдалять = Ложь;
					Прервать;
				КонецПопытки;
				
			Иначе
				Попытка
					_ОбъектСсылка = _Найденная.Данные;
				Исключение
					ВетисЖурнал.ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "УдалитьПоСсылке:"+_Найденная.Данные);
					_ОбъектСсылка = Неопределено;
					_МожноУдалять = Ложь;
					Прервать;
				КонецПопытки;
				
				Если НЕ _ОбъектСсылка = Неопределено Тогда
					УдалитьПоСсылке(_ОбъектСсылка, пСсылка);
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
		Если _МожноУдалять Тогда
			_Объект = пСсылка.ПолучитьОбъект();
			УдалитьДанные(_Объект);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьДанные(Данные, пЗагрузка = Истина) Экспорт
	
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Данные.ОбменДанными.Загрузка = пЗагрузка = Истина;
	
	Попытка
		Данные.Удалить();
	Исключение
		ВетисЖурнал.ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), Данные.Ссылка);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры
