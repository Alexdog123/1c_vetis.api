
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеСервер Тогда
		Элементы.ОтборХС.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборПредприятие.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборПредприятиеGuid.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборТипПродукции.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборПродукция.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборВидПродукции.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.СправочникСписок.Видимость = Ложь;
	#КонецЕсли
	
	ОтборПредприятиеПредставление = "Представление";
	
	Элементы.ГруппаОтбор.Видимость = Ложь;
	
	МаксимальноеКоличество = 100;
	
	СправочникСписок.ПроизвольныйЗапрос = Истина;
	СправочникСписок.ОсновнаяТаблица = ВетисИмяСправочника.ProductItem("Справочник");
	СправочникСписок.ТекстЗапроса =
	"ВЫБРАТЬ
	|	_Таблица.Ссылка,
	|	_Таблица.Код,
	|	_Таблица.Наименование,
	|	_Ветис.guid КАК guid,
	|	_Ветис.Предприятие КАК Предприятие,
	|	_Ветис.name КАК name,
	|	_Ветис.Описание КАК Описание
	|{ВЫБРАТЬ
	|	Ссылка.*,
	|	Код,
	|	Наименование,
	|	guid,
	|	Предприятие,
	|	name,
	|	Описание}
	|ИЗ
	|	Справочник."+ВетисИмяСправочника.ProductItem()+" КАК _Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _Ветис
	|		ПО _Таблица.Ссылка = _Ветис.Ссылка
	|ГДЕ
	|	ИСТИНА
	|	И НЕ _Таблица.ПометкаУдаления
	|	И НЕ _Таблица.ЭтоГруппа
	|	И ИСТИНА";
	
	ОтборТипПродукции = ПредопределенноеЗначение("Перечисление.ВетисProductType.МясоИМясопродукты");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЗаполнитьСписокПродукцииСервер();
	
	ЗаполнитьСписокВидовПродукцииСервер();
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Параметр = ЭтаФорма.ИмяФормы Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисВерсия" Тогда
		УстановитьВидимость();
	ИначеЕсли ИмяСобытия = "ВетисОтладка" Тогда
		//
	ИначеЕсли ИмяСобытия = "ВетисТестовый" Тогда
		//
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаНайти(Команда)
	
	КомандаНайтиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСоздатьСвязь(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		_ТекущиеДанные = Элементы.СписокВетис.ТекущиеДанные;
		
		_Параметры = Новый Структура("enterprise, guid, name", _ТекущиеДанные.enterpriseguid, _ТекущиеДанные.guid, _ТекущиеДанные.name);
		
		ВетисФормы.ProductItemФормаВыбора(ЭтаФорма, "КомандаСоздатьСвязьВыборОбработкаОповещения",,,_Параметры);
		
		Возврат;
	#Иначе
		
		_ТекущиеДанные1с = Элементы.СправочникСписок.ТекущиеДанные;
		
		Если _ТекущиеДанные1с = Неопределено Тогда
			Предупреждение("Необходимо заполнить справочник ""Номенклатура""");
			Возврат;
		КонецЕсли;
		
		_ТекущиеДанныеВетис = СписокВетис.НайтиПоИдентификатору(Элементы.СписокВетис.ТекущаяСтрока);
		
		Если Элементы.СправочникСписок.ВыделенныеСтроки.Количество() > 0 Тогда
			
			Для Каждого _ТекущиеДанные1сСсылка из Элементы.СправочникСписок.ВыделенныеСтроки Цикл
				ВетисСоответствие.ДобавитьProductItem(_ТекущиеДанные1сСсылка, _ТекущиеДанныеВетис.enterpriseguid, _ТекущиеДанныеВетис.guid, _ТекущиеДанныеВетис.name);
			КонецЦикла;
			
		Иначе
			ВетисСоответствие.ДобавитьProductItem(_ТекущиеДанные1с.Ссылка, _ТекущиеДанныеВетис.enterpriseguid, _ТекущиеДанныеВетис.guid, _ТекущиеДанныеВетис.name);
			
		КонецЕсли;
		
		ОбновитьСписок();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьСвязь(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		
		ВетисСоответствие.УдалитьProductItem(Элементы.СписокВетис.ТекущиеДанные.guid, Элементы.СписокВетис.ТекущиеДанные.enterpriseguid);
		
	#Иначе

		Если Элементы.СправочникСписок.ВыделенныеСтроки.Количество() > 0 Тогда
			Для Каждого _ТекущиеДанные из Элементы.СправочникСписок.ВыделенныеСтроки Цикл
				ВетисСоответствие.УдалитьProductItem(_ТекущиеДанные.Ссылка, _ТекущиеДанные.Предприятие);
			КонецЦикла;
		Иначе
			ВетисСоответствие.УдалитьProductItem(Элементы.СправочникСписок.ТекущиеДанные.Ссылка, Элементы.СправочникСписок.ТекущиеДанные.Предприятие);
		КонецЕсли;

	#КонецЕсли
	
	ОбновитьСписок();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьИзСервиса(Команда)
	
	_ТекущиеДанные = Элементы.СписокВетис.ТекущиеДанные;
	
	_Параметры = Новый Структура("name", _ТекущиеДанные.name);
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Если Элементы.СписокВетис.ВыделенныеСтроки.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ВетисФормы.ProductItemФормаВыбора(ЭтаФорма, "КомандаЗаполнитьИзСервисаВыборОбработкаОповещения",,,_Параметры);
		
		Возврат;
	#Иначе
		
		Если Элементы.СправочникСписок.ТекущаяСтрока = Неопределено 
			ИЛИ Элементы.СписокВетис.ВыделенныеСтроки.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		КомандаЗаполнитьИзСервисаВыборОбработкаОповещения(Элементы.СправочникСписок.ТекущаяСтрока, _Параметры);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьИзСервиса(Команда)
	
	Если Элементы.СписокВетис.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	_форма = ОткрытьФорму(ВетисИмяСправочника.ProductItem("Справочник", "ФормаОбъекта"));
	
	_форма.Объект.Наименование = Элементы.СписокВетис.ТекущиеДанные.name;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСправочникДобавить(Команда)
	
	Форма = ОткрытьФорму(ВетисИмяСправочника.ProductItem("Справочник", "ФормаОбъекта"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтбор(Команда)
	
	Элементы.ФормаКомандаОтбор.Пометка = НЕ Элементы.ФормаКомандаОтбор.Пометка;
	
	Элементы.ГруппаОтбор.Видимость = Элементы.ФормаКомандаОтбор.Пометка;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСоздатьСвязьВыборОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		
		ВетисСоответствие.ДобавитьProductItem(пРезультат, пПараметры.enterpriseguid, пПараметры.guid, пПараметры.name);
		
		ОбновитьСписок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьИзСервисаВыборОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		
		_форма = ОткрытьФорму(ВетисИмяСправочника.ProductItem("Справочник", "ФормаОбъекта"), Новый Структура("Ключ", пРезультат));
		
		_форма.Объект.Наименование = пПараметры.name;
		
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ХСНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.BusinessEntityФормаВыбора(ЭтаФорма, "ХСОбработкаОповещения", СтандартнаяОбработка, Объект.ХозяйствующийСубъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ХСОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Объект.ХозяйствующийСубъект = Результат;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "ПредприятиеОбработкаОповещения", СтандартнаяОбработка, Объект.ХозяйствующийСубъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредприятиеОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Объект.Предприятие = Результат;
		ОтборПредприятиеGuid = "";
		ОтборПредприятиеПредставление = "Представление";
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОтборИспользованиеПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	
	Выполнить(Элемент.Имя+"Использование=ЗначениеЗаполнено("+Элемент.Имя+");");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПредприятиеGuidПриИзменении(Элемент)
	
	ОтборПредприятиеИспользование = ЗначениеЗаполнено(ОтборПредприятиеGuid);
	Объект.Предприятие = ВетисDictionaryСлой1с.Enterprise(ОтборПредприятиеGuid);
	ОтборПредприятиеПредставление = Объект.Предприятие;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПредприятиеПриИзменении(Элемент)
	
	ОтборПредприятиеИспользование = ЗначениеЗаполнено(Объект.Предприятие);
	ОтборПредприятиеGuid = "";
	ОтборПредприятиеПредставление = Объект.Предприятие;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборТипПродукцииПриИзменении(Элемент)
	
	ОтборТипПродукцииИспользование = ЗначениеЗаполнено(ОтборТипПродукции);
	
	ОтборПродукция = "";
	
	ОтборПродукцияИспользование = Ложь;
	
	ОтборВидПродукции = "";
	
	ОтборВидПродукцииИспользование = Ложь;
	
	ЗаполнитьСписокПродукцииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПродукцияПриИзменении(Элемент)
	
	ОтборПродукцияИспользование = ЗначениеЗаполнено(ОтборПродукция);
	
	ОтборВидПродукции = "";
	
	ОтборВидПродукцииИспользование = Ложь;
	
	ЗаполнитьСписокВидовПродукцииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидПродукцииПриИзменении(Элемент)
	
	ОтборВидПродукцииИспользование = ЗначениеЗаполнено(ОтборВидПродукции);
	
КонецПроцедуры


&НаКлиенте
Процедура СписокВетисВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	_ТекущиеДанные = Элементы.СписокВетис.ТекущиеДанные;
	
	_оо = Новый ОписаниеОповещения("СписокВетисВыборОбработкаОповещения", ЭтаФорма, Новый Структура("name, guid", _ТекущиеДанные.enterprise, _ТекущиеДанные.enterpriseguid));
	
	ПоказатьВопрос(_оо, "Взять """ + _ТекущиеДанные.enterprise + """ в отбор?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВетисВыборОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		ОтборПредприятиеGuid = Параметр.guid;
		ОтборПредприятиеПредставление = Параметр.name;
		Объект.Предприятие = ВетисDictionaryСлой1с.Enterprise(ОтборПредприятиеGuid);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура УстановитьВидимость()
	
	_Версия20 = ВетисXSD.Версия_2_0();
	
	Если _Версия20 Тогда
		ОтборПродукцияИспользование = ОтборПродукцияИспользование;
		ОтборВидПродукцииИспользование = ОтборВидПродукцииИспользование И ОтборПродукцияИспользование;
		
		Элементы.ОтборХСИспользование.Видимость = Истина;
		Элементы.ОтборПредприятиеИспользование.Видимость = Истина;
		Элементы.ОтборПредприятиеИспользование1.Видимость = Истина;
		Элементы.ОтборТипПродукцииИспользование.Видимость = Ложь;
		
		Элементы.ОтборХС.Видимость = Истина;
		
		//Элементы.ОтборХС.Доступность = ОтборХСИспользование;
		//Элементы.ОтборПредприятие.Доступность = ОтборПредприятиеИспользование;
		//Элементы.ОтборПредприятиеGuid.Доступность = ОтборПредприятиеИспользование;
		//Элементы.ОтборТипПродукции.Доступность = Истина;
		//Элементы.ОтборПродукция.Доступность = ОтборПродукцияИспользование;
		//Элементы.ОтборВидПродукции.Доступность = ОтборВидПродукцииИспользование;
		//
		//Элементы.ОтборПредприятиеПредставление.Доступность = ОтборПредприятиеИспользование;
	Иначе
		ОтборВидПродукцииИспользование = ОтборВидПродукцииИспользование И ОтборПродукцияИспользование;
		
		Элементы.ОтборХСИспользование.Видимость = Ложь;
		Элементы.ОтборПредприятиеИспользование.Видимость = Ложь;
		Элементы.ОтборПредприятиеИспользование1.Видимость = Ложь;
		Элементы.ОтборТипПродукцииИспользование.Видимость = Ложь;
		
		Элементы.ОтборХС.Видимость = Ложь;
		
		//Элементы.ОтборХС.Доступность = Истина;
		Элементы.ОтборПредприятие.Доступность = Истина;
		Элементы.ОтборПредприятиеGuid.Доступность = Истина;
		Элементы.ОтборТипПродукции.Доступность = Истина;
		//Элементы.ОтборПродукция.Доступность = ОтборПродукцияИспользование;
		//Элементы.ОтборВидПродукции.Доступность = ОтборВидПродукцииИспользование;
		//
		//Элементы.ОтборПредприятиеПредставление.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок()
	
	#Если НЕ МобильноеПриложениеКлиент Тогда
	Элементы.СправочникСписок.Обновить();
	#КонецЕсли
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСписокПродукцииСервер()
	
	Перем _Product, _параметры;
	
	Если НЕ ЗначениеЗаполнено(ОтборТипПродукции) Тогда
		Возврат;
	КонецЕсли;
	
	_Версия20 = ВетисXSD.Версия_2_0();
	_ВетисProductService = ?(_Версия20, ВетисProductService_2_0, ВетисProductService);
	_ВетисArgusProductionСлой1с = ?(_Версия20, ВетисDictionaryСлой1с, ВетисArgusProductionСлой1с);
	
	_productType = _ВетисArgusProductionСлой1с.ProductType(ОтборТипПродукции);
	
	Элементы.ОтборПродукция.СписокВыбора.Очистить();
	
	Пока _ВетисProductService.GetProductByTypeListСледующий(_productType, _Product, _параметры) Цикл
		
		Элементы.ОтборПродукция.СписокВыбора.Добавить(_product.guid, _product.name);
		
	КонецЦикла;
	
	Элементы.ОтборПродукция.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВидовПродукцииСервер()
	
	Перем _subProduct, _параметры;
	
	Если НЕ ЗначениеЗаполнено(ОтборПродукция) Тогда
		Возврат;
	КонецЕсли;
	
	_Версия20 = ВетисXSD.Версия_2_0();
	_ВетисProductService = ?(_Версия20, ВетисProductService_2_0, ВетисProductService);
	
	Элементы.ОтборВидПродукции.СписокВыбора.Очистить();
	
	Пока _ВетисProductService.GetSubProductByProductListСледующий(ОтборПродукция, _subProduct, _параметры) Цикл
		
		Элементы.ОтборВидПродукции.СписокВыбора.Добавить(_subProduct.guid, _subProduct.name);
		
	КонецЦикла;
	
	Элементы.ОтборВидПродукции.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

&НаСервере
Процедура КомандаНайтиСервер()
	
	Перем _product, _subProduct, _productItem, _параметрыP;
	
	_Версия20 = ВетисXSD.Версия_2_0();
	
	СписокВетис.Очистить();
	
	_ОтборПродукцияЭлемент = Элементы.ОтборПродукция.СписокВыбора.НайтиПоЗначению(ОтборПродукция);
	_ОтборВидПродукцииЭлемент = Элементы.ОтборВидПродукции.СписокВыбора.НайтиПоЗначению(ОтборВидПродукции);
	_ОтборПродукцияПредставление = ?(_ОтборПродукцияЭлемент = Неопределено, "", _ОтборПродукцияЭлемент.Представление);
	_ОтборВидПродукцииПредставление = ?(_ОтборВидПродукцииЭлемент = Неопределено, "", _ОтборВидПродукцииЭлемент.Представление);
	
	Если _Версия20 Тогда
		_параметрыPI = Неопределено;
		
		_businessEntity = ВетисDictionary_2_0.BusinessEntity(?(ОтборХСИспользование, ВетисDictionaryСлой1с.BusinessEntity(Объект.ХозяйствующийСубъект), Неопределено));
		_enterprise  = ВетисDictionary_2_0.Enterprise(?(ОтборПредприятиеИспользование, ?(ЗначениеЗаполнено(Объект.Предприятие), ВетисDictionaryСлой1с.Enterprise(Объект.Предприятие), ОтборПредприятиеGuid), Неопределено));
		
		_productType = ВетисDictionaryСлой1с.ProductType(ОтборТипПродукции);
		_product     = ВетисDictionary_2_0.Product(?(ОтборПродукцияИспользование, ОтборПродукция, Неопределено));
		_subProduct  = ВетисDictionary_2_0.SubProduct(?(ОтборВидПродукцииИспользование, ОтборВидПродукции, Неопределено));
		
		_subProductСоответствие = Новый Соответствие;
		//_subProductСоответствие.Вставить(ОтборВидПродукции, ОтборВидПродукцииПредставление);
		
		_productСоответствие = Новый Соответствие;
		//_productСоответствие.Вставить(ОтборПродукция, ОтборПродукцияПредставление);
		
		_enterpriseСоответствие = Новый Соответствие;
		//_enterpriseСоответствие.Вставить(_enterprise.guid, ?(ЗначениеЗаполнено(Объект.Предприятие), Объект.Предприятие, ОтборПредприятиеПредставление));
		
		Пока ВетисProductService_2_0.GetProductItemListСледующий(_productType, _product, _subProduct, _businessEntity, _enterprise, _productItem, _параметрыPI) Цикл
			
			Если НЕ _productItem.producing = Неопределено И _productItem.producing.Количество() > 0
				И _enterpriseСоответствие.Получить(_productItem.producing[0].location.guid) = Неопределено Тогда
				_item = ВетисEnterpriseService_2_0.GetEnterpriseByGuid(_productItem.producing[0].location.guid);
				Если _item = Неопределено Тогда
					_enterpriseСоответствие.Вставить(_productItem.producing[0].location.guid, "");
				Иначе
					_enterpriseСоответствие.Вставить(_item.guid, _item.name);
				КонецЕсли;
			КонецЕсли;
			
			Если _subProductСоответствие.Получить(_productItem.subProduct.guid) = Неопределено Тогда
				//2017-11-01
				//entityNotFoundFault
				//SubProduct with guid [02eec86c-54f3-2d20-6311-d3db41a2e2d1] not found.
				//т.е. то что вернул GetProductItemList
				_item = ВетисProductService_2_0.GetSubProductByGuid(_productItem.subProduct.guid,,Ложь);
				Если _item = Неопределено Тогда
					_subProductСоответствие.Вставить(_productItem.subProduct.guid, "");
				Иначе
					_subProductСоответствие.Вставить(_item.guid, _item.name);
				КонецЕсли;
			КонецЕсли;
			
			Если _productСоответствие.Получить(_productItem.product.guid) = Неопределено Тогда
				//здесь, видимо, тоже самое
				_item = ВетисProductService_2_0.GetProductItemByGuid(_productItem.product.guid,,Ложь);
				Если _item = Неопределено Тогда
					_productСоответствие.Вставить(_productItem.product.guid, "");
				Иначе
					_productСоответствие.Вставить(_item.guid, _item.name);
				КонецЕсли;
			КонецЕсли;
			
			_СписокСтрока = СписокВетис.Добавить();
			_СписокСтрока.name = _productItem.name;
			_СписокСтрока.guid = _productItem.guid;
			_СписокСтрока.productType = ВетисDictionaryСлой1с.ProductType(_productItem.productType);
			_СписокСтрока.subProduct = _subProductСоответствие.Получить(_productItem.subProduct.guid);
			_СписокСтрока.product = _productСоответствие.Получить(_productItem.product.guid);
			
			Если НЕ _productItem.producing = Неопределено И _productItem.producing.Количество() > 0 Тогда
				_СписокСтрока.enterpriseguid = _productItem.producing[0].location.guid;
				_СписокСтрока.enterprise = _enterpriseСоответствие.Получить(_СписокСтрока.enterpriseguid);
			КонецЕсли;
			
			Если МаксимальноеКоличество > 0 И СписокВетис.Количество() >= МаксимальноеКоличество Тогда Прервать; КонецЕсли;
			
		КонецЦикла;
	Иначе
		_productType = ВетисArgusProductionСлой1с.ProductType(ОтборТипПродукции);
		_enterpriseGuid = ВетисCerberusEnterpriseСлой1с.Enterprise(Объект.Предприятие);
		Если ОтборВидПродукцииИспользование Тогда
			_параметрыPI = Неопределено;
			Пока ВетисProductService.GetProductItemListСледующий(ОтборВидПродукции, _enterpriseGuid, _productItem, _параметрыPI) Цикл
				_СписокСтрока = СписокВетис.Добавить();
				_СписокСтрока.name = _productItem.name;
				_СписокСтрока.guid = _productItem.guid;
				_СписокСтрока.subProduct = _ОтборВидПродукцииПредставление;
				_СписокСтрока.product = _ОтборПродукцияПредставление;
				Если МаксимальноеКоличество > 0 И СписокВетис.Количество() >= МаксимальноеКоличество Тогда Прервать; КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ОтборПродукцияИспользование Тогда
			_параметрыS = Неопределено;
			Пока ВетисProductService.GetSubProductByProductListСледующий(ОтборПродукция, _subProduct, _параметрыS) Цикл
				_параметрыPI = Неопределено;
				Пока ВетисProductService.GetProductItemListСледующий(_subProduct.guid, _enterpriseGuid, _productItem, _параметрыPI) Цикл
					_СписокСтрока = СписокВетис.Добавить();
					_СписокСтрока.name = _productItem.name;
					_СписокСтрока.guid = _productItem.guid;
					_СписокСтрока.subProduct = _subProduct.name;
					_СписокСтрока.product = _ОтборПродукцияПредставление;
					Если МаксимальноеКоличество > 0 И СписокВетис.Количество() >= МаксимальноеКоличество Тогда Прервать; КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			Пока ВетисProductService.GetProductByTypeListСледующий(_productType, _product, _параметрыP) Цикл
				_параметрыS = Неопределено;
				Пока ВетисProductService.GetSubProductByProductListСледующий(_product.guid, _subProduct, _параметрыS) Цикл
					_параметрыPI = Неопределено;
					Пока ВетисProductService.GetProductItemListСледующий(_subProduct.guid, _enterpriseGuid, _productItem, _параметрыPI) Цикл
						_СписокСтрока = СписокВетис.Добавить();
						_СписокСтрока.name = _productItem.name;
						_СписокСтрока.guid = _productItem.guid;
						_СписокСтрока.subProduct = _subProduct.name;
						_СписокСтрока.product = _product.name;
						Если МаксимальноеКоличество > 0 И СписокВетис.Количество() >= МаксимальноеКоличество Тогда Прервать; КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
	СписокВетис.Сортировать("name");
		
КонецПроцедуры
