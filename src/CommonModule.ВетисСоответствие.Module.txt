
//Ссылка на справочник по Guid
Функция ПолучитьСсылку(guid, пТип, ПоУмолчанию = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	_Таблица.Ссылка
	|ИЗ
	|	РегистрСведений.ВетисСоответствие КАК _Таблица
	|ГДЕ
	|	_Таблица.guid = &guid
	|	И _Таблица.Ссылка ССЫЛКА {Тип}
	|	И ИСТИНА";
	
	Запрос.УстановитьПараметр("guid", guid);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "{Тип}", пТип);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат ПоУмолчанию;
	КонецЕсли;
	
КонецФункции

//Guid по ссылка на справочник
Функция ПолучитьGuid(пСсылка, ПоУмолчанию = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	_Таблица.guid
	|ИЗ
	|	РегистрСведений.ВетисСоответствие КАК _Таблица
	|ГДЕ
	|	_Таблица.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", пСсылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.guid;
	Иначе
		Возврат ПоУмолчанию;
	КонецЕсли;
	
КонецФункции

//Добавление связи
Функция Добавить(пСсылка, guid, name = "", пОписание = "") Экспорт
	
	мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	мз.Ссылка = пСсылка;
	мз.guid   = guid;
	мз.name   = СокрЛП(name);
	мз.Описание = СокрЛП(пОписание);
	
	мз.Записать();
	
КонецФункции

//Удаление связи
Функция Удалить(пСсылка, пТип) Экспорт
	
	мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	мз.Ссылка = ?(ТипЗнч(пСсылка) = Тип("Строка"), ПолучитьСсылку(пСсылка, пТип), пСсылка);
	
	мз.Удалить();
	
КонецФункции


//Конвертирует номенклатуру в обе стороны
//Параметры:
// Ссылка - наша номенклатура или guid или name
// Предприятие - Предприятие производителя
Функция ПолучитьProductItem(пСсылка, пПредприятие, ПоУмолчанию = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(пСсылка) Тогда
		Возврат ПоУмолчанию;
	КонецЕсли;
	
	Если ТипЗнч(пПредприятие) = Тип("Строка") Тогда
		_Предприятие = ?(ВетисXSD.Версия_2_0(), ВетисDictionaryСлой1с, ВетисCerberusEnterpriseСлой1с).Enterprise(пПредприятие);
	Иначе
		_Предприятие = пПредприятие;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|//1	ВЫБОР КОГДА _Таблица.guid = """" ТОГДА _Таблица.name ИНАЧЕ _Таблица.guid КОНЕЦ КАК Ссылка,
	|//1	_Таблица.guid,
	|//1	_Таблица.name
	|//2	_Таблица.Ссылка
	|//3	_Таблица.Ссылка
	|ИЗ
	|	РегистрСведений.ВетисСоответствие КАК _Таблица
	|ГДЕ
	|	ИСТИНА
	|//1	И _Таблица.Ссылка = &Ссылка
	|//2	И _Таблица.guid = &Ссылка
	|//3	И _Таблица.name = &Ссылка
	|	И _Таблица.Предприятие = &Предприятие
	|	И _Таблица.Ссылка ССЫЛКА "+ВетисИмяСправочника.ProductItem("Справочник")+"
	|	И ИСТИНА";
	
	Запрос.УстановитьПараметр("Ссылка", пСсылка);
	Запрос.УстановитьПараметр("Предприятие", _Предприятие);
	
	_ЭтоСсылка = ТипЗнч(пСсылка) = Тип(ВетисИмяСправочника.ProductItem("СправочникСсылка"));
	
	Если _ЭтоСсылка Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//1", "");
	Иначе
		Попытка
			_проверка = Новый УникальныйИдентификатор(пСсылка);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//2", "");
		Исключение
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//3", "");
		КонецПопытки;
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если _ЭтоСсылка Тогда
			Возврат Новый Структура("guid, name", ?(Выборка.guid = "", Неопределено, Выборка.guid), Выборка.name);
		Иначе
			Возврат Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Если _ЭтоСсылка Тогда
			Возврат Новый Структура("guid, name");
		Иначе
			Возврат ПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

//Добавление связи
Процедура ДобавитьProductItem(пСсылка, пПредприятие, guid, name = "", пОписание = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(guid) И НЕ ЗначениеЗаполнено(name)Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(пПредприятие) = Тип("Строка") Тогда
		_Предприятие = ?(ВетисXSD.Версия_2_0(), ВетисDictionaryСлой1с, ВетисCerberusEnterpriseСлой1с).Enterprise(пПредприятие);
	Иначе
		_Предприятие = пПредприятие;
	КонецЕсли;
	
	_мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	_мз.Ссылка      = пСсылка;
	_мз.Предприятие = _Предприятие;
	_мз.guid        = guid;
	_мз.name        = name;
	_мз.Описание    = пОписание;
	
	_мз.Записать();
	
КонецПроцедуры

//Удаление связи
//Параметры:
// пСсылка - ссылка на справочник, guid или наименование
Процедура УдалитьProductItem(пСсылка, пПредприятие) Экспорт
	
	Если ТипЗнч(пПредприятие) = Тип("Строка") Тогда
		_Предприятие = ?(ВетисXSD.Версия_2_0(), ВетисDictionaryСлой1с, ВетисCerberusEnterpriseСлой1с).Enterprise(пПредприятие);
	Иначе
		_Предприятие = пПредприятие;
	КонецЕсли;
	
	_мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	_мз.Ссылка = ?(ТипЗнч(пСсылка) = Тип("Строка"), ПолучитьProductItem(пСсылка, _Предприятие), пСсылка);
	_мз.Предприятие = _Предприятие;
	
	_мз.Удалить();
	
КонецПроцедуры
