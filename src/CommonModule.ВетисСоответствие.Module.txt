
Функция ПолучитьСсылку(guid, пТип, ПоУмолчанию = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	_Таблица.Ссылка
	|ИЗ
	|	РегистрСведений.ВетисСоответствие КАК _Таблица
	|ГДЕ
	|	_Таблица.guid = &guid
	|	И _Таблица.Ссылка ССЫЛКА {Тип}
	|	И ИСТИНА";
	
	Запрос.УстановитьПараметр("guid", guid);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "{Тип}", пТип);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат ПоУмолчанию;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьGuid(Ссылка, ПоУмолчанию = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	_Таблица.guid
	|ИЗ
	|	РегистрСведений.ВетисСоответствие КАК _Таблица
	|ГДЕ
	|	_Таблица.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.guid;
	Иначе
		Возврат ПоУмолчанию;
	КонецЕсли;
	
КонецФункции

Функция Добавить(Ссылка, guid, name = "", Описание = "") Экспорт
	
	мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	мз.Ссылка = Ссылка;
	мз.guid   = guid;
	мз.name   = name;
	мз.Описание = Описание;
	
	мз.Записать();
	
КонецФункции

Функция Удалить(Ссылка) Экспорт
	
	мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	мз.Ссылка = Ссылка;
	
	мз.Удалить();
	
КонецФункции


//Конвертирует номенклатуру в обе стороны
//Параметры:
// Ссылка - наша номенклатура или guid или name
// Предприятие - Предприятие производителя
Функция ПолучитьProductItem(пСсылка, пПредприятие, ПоУмолчанию = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(пСсылка) Тогда
		Возврат ПоУмолчанию;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|//1	ВЫБОР КОГДА _Таблица.guid = """" ТОГДА _Таблица.name ИНАЧЕ _Таблица.guid КОНЕЦ КАК Ссылка,
	|//1	_Таблица.guid,
	|//1	_Таблица.name
	|//2	_Таблица.Ссылка
	|//3	_Таблица.Ссылка
	|ИЗ
	|	РегистрСведений.ВетисСоответствие КАК _Таблица
	|ГДЕ
	|	ИСТИНА
	|//1	И _Таблица.Ссылка = &Ссылка
	|//2	И _Таблица.guid = &Ссылка
	|//3	И _Таблица.name = &Ссылка
	|	И _Таблица.Предприятие = &Предприятие
	|	И _Таблица.Ссылка ССЫЛКА "+ВетисИмяСправочника.ProductItem("Справочник")+"
	|	И ИСТИНА";
	
	Запрос.УстановитьПараметр("Ссылка", пСсылка);
	Запрос.УстановитьПараметр("Предприятие", пПредприятие);
	
	_ЭтоСсылка = ТипЗнч(пСсылка) = Тип(ВетисИмяСправочника.ProductItem("СправочникСсылка"));
	
	Если _ЭтоСсылка Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//1", "");
	Иначе
		Попытка
			_проверка = Новый УникальныйИдентификатор(пСсылка);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//2", "");
		Исключение
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//3", "");
		КонецПопытки;
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если _ЭтоСсылка Тогда
			Возврат Новый Структура("guid, name", ?(Выборка.guid = "", Неопределено, Выборка.guid), Выборка.name);
		Иначе
			Возврат Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Если _ЭтоСсылка Тогда
			Возврат Новый Структура("guid, name");
		Иначе
			Возврат ПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ДобавитьProductItem(пСсылка, пПредприятие, guid, name = "", пОписание = "") Экспорт
	
	Если НЕ ЗначениеЗаполнено(guid) И НЕ ЗначениеЗаполнено(name)Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	_мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	_мз.Ссылка      = пСсылка;
	_мз.Предприятие = пПредприятие;
	_мз.guid        = guid;
	_мз.name        = name;
	_мз.Описание    = пОписание;
	
	_мз.Записать();
	
КонецФункции

Функция УдалитьProductItem(пСсылка, пПредприятие) Экспорт
	
	_мз = РегистрыСведений.ВетисСоответствие.СоздатьМенеджерЗаписи();
	
	_мз.Ссылка = пСсылка;
	_мз.Предприятие = пПредприятие;
	
	_мз.Удалить();
	
КонецФункции
