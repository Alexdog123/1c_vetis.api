
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	#Если МобильноеПриложениеСервер Тогда
		Элементы.ОтборИмя.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборПолноеИмя.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборИНН.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборФИО.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборРегион.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборРайон.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.ОтборGuid.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		//Элементы.МаксимальноеКоличество.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.СправочникСписок.Видимость = Ложь;
		Элементы.ВариантПоиска.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элементы.СписокВетисКомандаСоздатьСвязь.Видимость = Ложь;
		Элементы.СписокВетисКомандаУдалитьСвязь.Видимость = Ложь;
		Элементы.СписокВетисКомандаДобавить.Видимость = Ложь;
		Элементы.СписокВетисКомандаЗаполнить.Видимость = Ложь;
	#КонецЕсли
	
	Элементы.ГруппаОтбор.Видимость = Ложь;
	
	//2017-11-07
	//в 2.0 не ищет по полному имени, выдает большую выборку не связанную со строкой поиска
	Если ВетисXSD.Версия_2_0() Тогда
		ОтборПолноеИмяИспользование = Ложь;
		Элементы.ГруппаОтборПолноеИмя.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ВариантПоиска.СписокВыбора.Добавить("NameAdr", "Имя, ИНН, Адрес");
	Элементы.ВариантПоиска.СписокВыбора.Добавить("Guid", "guid");
	
	ВариантПоиска = "NameAdr";
	
	СправочникСписок.ПроизвольныйЗапрос = Истина;
	СправочникСписок.ОсновнаяТаблица = ВетисИмяСправочника.BusinessEntity("Справочник");
	СправочникСписок.ТекстЗапроса =
	"ВЫБРАТЬ
	|	_Таблица.Ссылка,
	|	_Таблица.Код,
	|	_Таблица.Наименование,
	|	_Таблица.ИНН,
	|	_Таблица.КПП,
	|	_Ветис.guid КАК guid,
	|	_Ветис.name КАК Ветисname,
	|	_Ветис.Описание КАК ВетисОписание
	|{ВЫБРАТЬ
	|	Ссылка.*,
	|	Код,
	|	Наименование,
	|	ИНН,
	|	КПП,
	|	guid,
	|	Ветисname,
	|	ВетисОписание}
	|ИЗ
	|	Справочник."+ВетисИмяСправочника.BusinessEntity()+" КАК _Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _Ветис
	|		ПО _Таблица.Ссылка = _Ветис.Ссылка
	|ГДЕ
	|	ИСТИНА
	|	И НЕ _Таблица.ПометкаУдаления
	|	И НЕ _Таблица.ЭтоГруппа";
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаНайти(Команда)
	
	Если ВариантПоиска = "Guid" Тогда
		СписокВетисЗаполнитьПоGuidСервер();
	Иначе
		СписокВетисЗаполнитьСервер();
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСопоставитьАвтоматически(Команда)
	
	_оо = Новый ОписаниеОповещения("КомандаСопоставитьАвтоматическиВопросОбработкаОповещения", ЭтаФорма, "Только выделенные");
	
	ПоказатьВопрос(_оо, "Только выделенные?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСопоставитьАвтоматическиВопросОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если Параметр = "Только выделенные" Тогда
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			
			СопоставитьАвтоматическиСервер(Истина);
			
			Элементы.СправочникСписок.Обновить();
			
		Иначе
			
			_оо = Новый ОписаниеОповещения("КомандаСопоставитьАвтоматическиВопросОбработкаОповещения", ЭтаФорма, "длительная операция");
			
			ПоказатьВопрос(_оо, "Это длительная операция без отображения прогресса. Продолжить?", РежимДиалогаВопрос.ДаНет);
			
		КонецЕсли;
		
	ИначеЕсли Параметр = "длительная операция" Тогда
		
		Если Результат = КодВозвратаДиалога.Да Тогда
			
			СопоставитьАвтоматическиСервер();
			
			Элементы.СправочникСписок.Обновить();
			
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры


&НаКлиенте
Процедура КомандаСоздатьСвязь(Команда)
	
	#Если МобильноеПриложениеКлиент Тогда
		_ТекущиеДанные = Элементы.СписокВетис.ТекущиеДанные;
		
		_Параметры = Новый Структура("guid,name,inn", _ТекущиеДанные.guid, _ТекущиеДанные.name, _ТекущиеДанные.inn);
		
		ВетисФормы.BusinessEntityФормаВыбора(ЭтаФорма, "КомандаСоздатьСвязьВыборОбработкаОповещения",,,_Параметры);
		
		Возврат;
	#Иначе
		
		ТекущиеДанные = Элементы.СправочникСписок.ТекущиеДанные;
		
		Если ТекущиеДанные = Неопределено Тогда
			Предупреждение("Необходимо заполнить справочник ""Контрагенты""");
			Возврат;
		КонецЕсли;
		
		СоздатьСвязьСервер(ТекущиеДанные.Ссылка);
		
		Элементы.СправочникСписок.Обновить();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУдалитьСвязь(Команда)
	
	УдалитьСвязьСервер();
	
	Элементы.СправочникСписок.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьИзСервиса(Команда)
	
	_ТекущиеДанные = Элементы.СписокВетис.ТекущиеДанные;
	
	_Параметры = Новый Структура("guid,name,inn,type", _ТекущиеДанные.name, _ТекущиеДанные.fullName, _ТекущиеДанные.inn, _ТекущиеДанные.type);
	
	#Если МобильноеПриложениеКлиент Тогда
		
		Если Элементы.СписокВетис.ВыделенныеСтроки.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Если ВариантПоиска = 1 Тогда
			ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "КомандаЗаполнитьИзСервисаВыборОбработкаОповещения", Объект.ХозяйствующийСубъект,,_Параметры);
		Иначе
			ВетисФормы.EnterpriseФормаВыбора(ЭтаФорма, "КомандаЗаполнитьИзСервисаВыборОбработкаОповещения",,,_Параметры);
		КонецЕсли;
		
		Возврат;
	#Иначе
		
		Если Элементы.СправочникСписок.ТекущаяСтрока = Неопределено 
			ИЛИ Элементы.СписокВетис.ВыделенныеСтроки.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		КомандаЗаполнитьИзСервисаВыборОбработкаОповещения(Элементы.СправочникСписок.ТекущаяСтрока, _Параметры);
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьИзСервиса(Команда)
	
	Если Элементы.СписокВетис.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	//_оо = Новый ОписаниеОповещения("КомандаДобавитьОбработкаОповещенияОЗакрытии", ЭтаФорма);
	//
	//_форма = ОткрытьФорму(ВетисИмяСправочника.BusinessEntity("Справочник", "ФормаОбъекта"),,,,,,_оо);
	_форма = ОткрытьФорму(ВетисИмяСправочника.BusinessEntity("Справочник", "ФормаОбъекта"));
	
	_форма.Объект.Наименование = Элементы.СписокВетис.ТекущиеДанные.name;
	_форма.Объект.НаименованиеПолное = Элементы.СписокВетис.ТекущиеДанные.fullName;
	_форма.Объект.ИНН = Элементы.СписокВетис.ТекущиеДанные.inn;
	_форма.Объект.ЮрФизЛицо = ?(Элементы.СписокВетис.ТекущиеДанные.type = ПредопределенноеЗначение("Перечисление.ВетисBusinessEntityType.ЮрЛицо"), ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"), ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСправочникДобавить(Команда)
	
	Форма = ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта");
	
	//_ТекущиеДанные = Элементы.СписокВетис.ТекущиеДанные;
	//
	//Если НЕ _ТекущиеДанные = Неопределено Тогда
	//	Форма.Объект.Наименование = _ТекущиеДанные.name;
	//	Форма.Объект.ИНН = _ТекущиеДанные.inn;
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтбор(Команда)
	
	Элементы.КомандаОтбор.Пометка = НЕ Элементы.КомандаОтбор.Пометка;
	
	Элементы.ГруппаОтбор.Видимость = Элементы.КомандаОтбор.Пометка;
	
КонецПроцедуры


&НаКлиенте
Процедура ВариантПоискаПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаСоздатьСвязьВыборОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		
		ВетисСоответствие.Добавить(пРезультат, пПараметры.guid, пПараметры.name, пПараметры.inn);
		
		Элементы.СправочникСписок.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьИзСервисаВыборОбработкаОповещения(пРезультат, пПараметры) Экспорт
	
	Если НЕ пРезультат = Неопределено Тогда
		
		_форма = ОткрытьФорму(ВетисИмяСправочника.BusinessEntity("Справочник", "ФормаОбъекта"), Новый Структура("Ключ", пРезультат));
		
		_форма.Объект.Наименование = пПараметры.name;
		_форма.Объект.НаименованиеПолное = пПараметры.fullName;
		_форма.Объект.ИНН = пПараметры.inn;
		_форма.Объект.ЮрФизЛицо = ?(пПараметры.type = ПредопределенноеЗначение("Перечисление.ВетисBusinessEntityType.ЮрЛицо"), ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"), ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИспользованиеПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВетисВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	_ТекущиеДанные = Элементы.СписокВетис.ТекущиеДанные;
	
	Элементы.СправочникСписок.ТекущаяСтрока = СправочникСписокНайти(_ТекущиеДанные.name)
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникСписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	_ТекущиеДанные = Элементы.СправочникСписок.ТекущиеДанные;
	
	ОтборПолноеИмя = _ТекущиеДанные.Наименование;
	
	ОтборИНН = _ТекущиеДанные.ИНН;
	
КонецПроцедуры


&НаКлиенте
Процедура УстановитьВидимость()
	
	Элементы.ГруппаОтборGuid.Видимость = ВариантПоиска = "Guid";
	Элементы.ГруппаОтбор1.Видимость = ВариантПоиска = "NameAdr";
	Элементы.ГруппаОтбор2.Видимость = ВариантПоиска = "NameAdr";
	
	Элементы.ОтборИмя.Доступность = ОтборИмяИспользование;
	Элементы.ОтборИНН.Доступность = ОтборИННИспользование;
	Элементы.ОтборФИО.Доступность = ОтборФИОИспользование;
	Элементы.ОтборПолноеИмя.Доступность = ОтборПолноеИмяИспользование;
	Элементы.ОтборРайон.Доступность = ОтборРайонИспользование;
	Элементы.ОтборРегион.Доступность = ОтборРегионИспользование;
	
КонецПроцедуры


&НаСервере
Процедура СписокВетисЗаполнитьСервер()
	
	Перем _businessEntity, _параметры;
	
	_Версия20 = ВетисXSD.Версия_2_0();
	_ВетисEnterpriseService = ?(_Версия20, ВетисEnterpriseService_2_0, ВетисEnterpriseService);
	_ВетисCerberusEnterpriseСлой1с = ?(_Версия20, ВетисDictionaryСлой1с, ВетисCerberusEnterpriseСлой1с);
	
	СписокВетис.Очистить();
	
	_Отбор = Новый Структура;
	
	Если ОтборИмяИспользование Тогда
		_Отбор.Вставить("name", ОтборИмя);
	КонецЕсли;
	
	Если ОтборПолноеИмяИспользование Тогда
		_Отбор.Вставить("fullName", ОтборПолноеИмя);
	КонецЕсли;
	
	Если ОтборИННИспользование Тогда
		_Отбор.Вставить("inn", ОтборИНН);
	КонецЕсли;
	
	Если ОтборФИОИспользование Тогда
		_Отбор.Вставить("fio", ОтборФИО);
	КонецЕсли;
	
	Если ОтборРайонИспользование ИЛИ ОтборРегионИспользование Тогда
		_отборадрес = Новый Структура();
		Если ОтборРегионИспользование Тогда
			_отборадрес.Вставить("region", Новый Структура("guid", ?(_Версия20, ВетисDictionaryСлой1с, ВетисIkarСлой1с).Region(ОтборРегион)));
		КонецЕсли;
		Если ОтборРайонИспользование Тогда
			_отборадрес.Вставить("district", Новый Структура("guid", ?(_Версия20, ВетисDictionaryСлой1с, ВетисIkarСлой1с).District(ОтборРайон)));
		КонецЕсли;
		_отбор.Вставить("juridicalAddress", _отборадрес);
	КонецЕсли;
	
	Пока _ВетисEnterpriseService.GetBusinessEntityListСледующий(_Отбор, _businessEntity, _параметры) Цикл
		
		_СписокВетисСтрока = СписокВетис.Добавить();
		
		ЗаполнитьЗначенияСвойств(_СписокВетисСтрока, _businessEntity);
		
		_СписокВетисСтрока.type = _ВетисCerberusEnterpriseСлой1с.BusinessEntityType(_businessEntity.type);
		
		Если _СписокВетисСтрока.type = Перечисления.ВетисBusinessEntityType.ЮрЛицо Тогда
			//Если ЗначениеЗаполнено(_businessEntity.fullName) Тогда
				_СписокВетисСтрока.name = _businessEntity.name;
				_СписокВетисСтрока.fullName = _businessEntity.fullName;
			//КонецЕсли;
		Иначе
			_СписокВетисСтрока.name = _businessEntity.fio;
		КонецЕсли;
		
		_СписокВетисСтрока.address = _businessEntity.juridicalAddress.addressView;
		
		_СписокВетисСтрока.link = НЕ ВетисСоответствие.ПолучитьСсылку(_businessEntity.guid, ВетисИмяСправочника.BusinessEntity("Справочник"), Ложь) = Ложь;
		
		Если МаксимальноеКоличество > 0 И СписокВетис.Количество() >= МаксимальноеКоличество Тогда Прервать; КонецЕсли;
		
	КонецЦикла;
	
	СписокВетис.Сортировать("name");
	
КонецПроцедуры

&НаСервере
Процедура СписокВетисЗаполнитьПоGuidСервер()
	
	_Версия20 = ВетисXSD.Версия_2_0();
	_ВетисEnterpriseService = ?(_Версия20, ВетисEnterpriseService_2_0, ВетисEnterpriseService);
	_ВетисCerberusEnterpriseСлой1с = ?(_Версия20, ВетисDictionaryСлой1с, ВетисCerberusEnterpriseСлой1с);

	_businessEntity = _ВетисEnterpriseService.GetBusinessEntityByGuid(ОтборGuid);
	
	_СписокВетисСтрока = СписокВетис.Добавить();
	
	ЗаполнитьЗначенияСвойств(_СписокВетисСтрока, _businessEntity);
	
	_СписокВетисСтрока.type = _ВетисCerberusEnterpriseСлой1с.BusinessEntityType(_businessEntity.type);
	
	Если _СписокВетисСтрока.type = Перечисления.ВетисBusinessEntityType.ЮрЛицо Тогда
		//Если ЗначениеЗаполнено(_businessEntity.fullName) Тогда
			_СписокВетисСтрока.name = _businessEntity.name;
			_СписокВетисСтрока.fullName = _businessEntity.fullName;
		//КонецЕсли;
	Иначе
		_СписокВетисСтрока.name = _businessEntity.fio;
	КонецЕсли;
	
	_СписокВетисСтрока.address = _businessEntity.juridicalAddress.addressView;
	
КонецПроцедуры

&НаСервере
Процедура СопоставитьАвтоматическиСервер(пТолькоВыделенныеСтроки = Ложь)
	
	//сопоставляет автоматически по ИНН
	
	_Версия20 = ВетисXSD.Версия_2_0();
	_ВетисEnterpriseService = ?(_Версия20, ВетисEnterpriseService_2_0, ВетисEnterpriseService);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	_Таблица.Ссылка,
	|	_Таблица.ИНН,
	|	_Таблица.Наименование
	|ИЗ
	|	Справочник.Контрагенты КАК _Таблица
	|ГДЕ
	|	ИСТИНА
	|//ос	И _Таблица.Ссылка В (&Список)
	|	И НЕ _Таблица.ПометкаУдаления
	|	И НЕ _Таблица.ЭтоГруппа
	|	И ИСТИНА";
	
	//если больше одной строки, то пройдем только по выделенным
	Если пТолькоВыделенныеСтроки = Ложь Тогда
		//
	ИначеЕсли Элементы.СправочникСписок.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	Иначе
		Запрос.УстановитьПараметр("Список", Элементы.СправочникСписок.ВыделенныеСтроки);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//ос", "");
	КонецЕсли;
	
	_список = Запрос.Выполнить().Выгрузить();
	
	Для Каждого _ТекущиеДанные из _список Цикл 
		
		Если ЗначениеЗаполнено(_ТекущиеДанные.ИНН) Тогда
			
			_Отбор          = Новый Структура("inn", _ТекущиеДанные.ИНН);
			_businessEntity = Неопределено;
			_параметры      = Неопределено;
			
			Пока _ВетисEnterpriseService.GetBusinessEntityListСледующий(_Отбор, _businessEntity, _параметры) Цикл
				
				_name = _businessEntity.name;
				
				Если _businessEntity.type = ВетисКонстанты.BusinessEntityType_JURIDICAL() Тогда
					Если ЗначениеЗаполнено(_businessEntity.fullName) Тогда
						_name = _businessEntity.fullName;
					Иначе
						_name = _businessEntity.Name;
					КонецЕсли;
				Иначе
					_name = _businessEntity.fio;
				КонецЕсли;
				
				ВетисСоответствие.Добавить(_ТекущиеДанные.Ссылка, _businessEntity.guid, _name, _businessEntity.inn);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Процедура СоздатьСвязьСервер(пСсылка) Экспорт
	
	_ТекущиеДанные = СписокВетис.НайтиПоИдентификатору(Элементы.СписокВетис.ТекущаяСтрока);
	
	Если Элементы.СправочникСписок.ВыделенныеСтроки.Количество() > 0 Тогда
		Для Каждого _ТекущиеДанные1с из Элементы.СправочникСписок.ВыделенныеСтроки Цикл
			
			ВетисСоответствие.Добавить(_ТекущиеДанные1с.Ссылка, _ТекущиеДанные.guid, _ТекущиеДанные.name, _ТекущиеДанные.inn);
			
		КонецЦикла;
		
	Иначе
		ВетисСоответствие.Добавить(пСсылка, _ТекущиеДанные.guid, _ТекущиеДанные.name, _ТекущиеДанные.inn);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСвязьСервер()
	
	Если Элементы.СправочникСписок.ВыделенныеСтроки.Количество() > 0 Тогда
		Для Каждого _ТекущиеДанные из Элементы.СправочникСписок.ВыделенныеСтроки Цикл
			
			ВетисСоответствие.Удалить(_ТекущиеДанные.Ссылка);
			
		КонецЦикла;
	Иначе
		_ТекущиеДанные = Элементы.СправочникСписок.ТекущиеДанные;
		
		ВетисСоответствие.Удалить(_ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Функция СправочникСписокНайти(пНаименование)
	
	Возврат Справочники[ВетисИмяСправочника.BusinessEntity()].НайтиПоНаименованию(пНаименование);
	
КонецФункции


