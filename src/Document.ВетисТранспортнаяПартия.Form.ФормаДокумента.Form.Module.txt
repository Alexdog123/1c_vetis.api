
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаЗаполнитьТовары(Команда)
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправить(Команда)
	
	Если НЕ Объект.Проведен Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьПартии(Команда)
	
	ОбновитьПартииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаРаспределитьПартии(Количеств)
	
	РаспределитьПартии();
	
	ОбновитьПартииНаСервере();
	
КонецПроцедуры


&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)
	
	ОбновитьПартииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.КлючСтроки = ПолучитьКлючСтроки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	_ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	_ПартииСтроки = Объект.Партии.НайтиСтроки(Новый Структура("КлючСвязи", _ТекущиеДанные.КлючСтроки));
	
	Для каждого _ПартииСтрока Из _ПартииСтроки Цикл
		Объект.Партии.Удалить(_ПартииСтрока);
	КонецЦикла;
	
	ПартииСписок.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПартииИспользоватьПриИзменении(Элемент)
	
	_ТекущиеДанные = Элементы.ПартииСписок.ТекущиеДанные;
	
	Если _ТекущиеДанные.Использовать Тогда
		_ПартииСтрока = Объект.Партии.Добавить();
		ЗаполнитьЗначенияСвойств(_ПартииСтрока, _ТекущиеДанные);
	Иначе
		Объект.Партии.Удалить(Объект.Партии.НайтиСтроки(Новый Структура("ВСД", _ТекущиеДанные.ВСД))[0]);
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьНаСервере()
	
	_Объект = РеквизитФормыВЗначение("Объект");
	
	_Объект.Заполнить(Объект.ДокументОснование);
	
	ЗначениеВРеквизитФормы(_Объект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьНаСервере()
	
	_Отказ = Ложь;
	
	Для каждого _СтрокаСЖ Из Объект.Товары Цикл
		
		_ПартииСтроки = Объект.Партии.НайтиСтроки(Новый Структура("КлючСвязи", _СтрокаСЖ.КлючСтроки));
		
		ВетисMercuryApplicationsСлой1с.TransportOperation(_СтрокаСЖ, _ПартииСтроки, _Отказ);
		
	КонецЦикла;
	
	Если НЕ _Отказ = Ложь Тогда
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПартииНаСервере()
	
	Если Элементы.Товары.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПартииСписок.Очистить();
	
	_ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(_Таблица.ВСД КАК Документ.ВетисВетеринарноСопроводительныйДокумент) КАК ВСД,
	|	ВЫРАЗИТЬ(_Таблица.Количество КАК Число) Количество,
	|	ВЫРАЗИТЬ(_Таблица.КоличествоМест КАК Число) КоличествоМест
	|ПОМЕСТИТЬ ВПартии
	|ИЗ
	|	&Партии КАК _Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА _Партии.ВСД ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использовать,
	|	&КлючСвязи КАК КлючСвязи,
	|	_Таблица.НоменклатурнаяГруппа,
	|	_Таблица.Номенклатура,
	|	_Таблица.SubProductGuid,
	|	_Таблица.ProductItemGuid,
	|	Выбор Когда _Партии.Количество есть null Тогда _Таблица.КоличествоОстаток Иначе _Партии.Количество Конец КАК Количество,
	|	Выбор Когда _Партии.КоличествоМест есть null Тогда _Таблица.КоличествоМестОстаток Иначе _Партии.КоличествоМест Конец КАК КоличествоМест,
	|	_Таблица.КоличествоОстаток КАК КоличествоПартии,
	|	_Таблица.КоличествоМестОстаток КАК КоличествоМестПартии,
	|	_Таблица.ВСД КАК ВСД
	|ИЗ
	|	РегистрНакопления.ВетисСкладскойЖурнал.Остатки(
	|			,
	|			ИСТИНА
	|//о1				И Номенклатура = &Номенклатура
	|//о2				И НоменклатурнаяГруппа = &НоменклатурнаяГруппа
	|				И ИСТИНА) КАК _Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВПартии КАК _Партии
	|		ПО _Таблица.ВСД = _Партии.ВСД";
	
	Запрос.УстановитьПараметр("Партии", Объект.Партии.Выгрузить(Новый Структура("КлючСвязи", _ТекущиеДанные.КлючСтроки)));
	Запрос.УстановитьПараметр("Номенклатура", _ТекущиеДанные.Номенклатура);
	Запрос.УстановитьПараметр("НоменклатурнаяГруппа", _ТекущиеДанные.НоменклатурнаяГруппа);
	Запрос.УстановитьПараметр("КлючСвязи", _ТекущиеДанные.КлючСтроки);
	
	Если ЗначениеЗаполнено(_ТекущиеДанные.Номенклатура) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//о1", "");
		Запрос.УстановитьПараметр("ВСД", Объект.Партии.Выгрузить(Новый Структура("Номенклатура", _ТекущиеДанные.Номенклатура)).ВыгрузитьКолонку("ВСД"));
		
	ИначеЕсли ЗначениеЗаполнено(_ТекущиеДанные.НоменклатурнаяГруппа) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//о2", "");
		Запрос.УстановитьПараметр("ВСД", Объект.Партии.Выгрузить(Новый Структура("НоменклатурнаяГруппа", _ТекущиеДанные.НоменклатурнаяГруппа)).ВыгрузитьКолонку("ВСД"));
		
	Иначе
		Возврат;
	КонецЕсли;
	
	_Результат = Запрос.Выполнить();
	_Выборка = _Результат.Выбрать();
	
	Пока _Выборка.Следующий() Цикл
		
		_строка = ПартииСписок.Добавить();
		
		ЗаполнитьЗначенияСвойств(_строка, _Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьПартии()
	
	Если Элементы.Товары.ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	_ТекущиеДанные = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	
	_ПартииСтроки = Объект.Партии.НайтиСтроки(Новый Структура("КлючСвязи", _ТекущиеДанные.КлючСтроки));
	
	_Количество = _ТекущиеДанные.Количество;
	
	_КоэффициентМест = ?(_ТекущиеДанные.Количество = 0, 0, _ТекущиеДанные.КоличествоМест / _ТекущиеДанные.Количество);
	
	_Структура = Новый Структура("КлючСвязи");
	
	Для каждого _ПартииСтрока Из _ПартииСтроки Цикл
		
		_Структура.КлючСвязи = _ПартииСтрока.КлючСвязи;
		
		_ПартииСписокСтроки = ПартииСписок.НайтиСтроки(_Структура);
		
		Если _ПартииСписокСтроки.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		_ПартииСписокСтрока = _ПартииСписокСтроки[0];
		
		Если _Количество <= 0 Тогда
			_ПартииСтрока.Количество = 0;
		ИначеЕсли _ПартииСписокСтрока.КоличествоПартии >= _Количество Тогда
			_ПартииСтрока.Количество = _Количество;
			_Количество = 0;
		Иначе
			_ПартииСтрока.Количество = _ПартииСписокСтрока.КоличествоПартии;
			_Количество = _Количество - _ПартииСписокСтрока.КоличествоПартии;
		КонецЕсли;
		
		_ПартииСтрока.КоличествоМест = _ПартииСтрока.Количество * _КоэффициентМест;
		
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
Функция ПолучитьКлючСтроки()
	
	_структура = Новый Структура("КлючСтроки");
	
	Для _Индекс = 1 По 999 Цикл
		_структура.КлючСтроки = _Индекс;
		_строки = Объект.Товары.НайтиСтроки(_Структура);
		Если _строки.Количество() = 0 Тогда
			Возврат _Индекс;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции
