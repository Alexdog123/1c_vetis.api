
//Используется самостоятельно в цикле Пока.
//Параметры:
// disease - Заболевание, к которому относятся запрашиваемые условия перемещения груза. bs:String255 0..1
// пУсловие - элемент списка, возвращаемый параметр
// пПараметры - для внутреннего использования, начальное значение - Неопределено
//Возвращаемое значение:
// Булево - Ложь значит список пройден весь
Функция GetR13nConditionListСледующий(пУсловие, пПараметры, пОтказ = Ложь, пОшибка = "") Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		//2017-11-24
		//их там около 600, если ставить меньше, то при втором обращении со смещением идет ошибка
		пПараметры.Вставить("ListOptions", ВетисBase_2_0.ListOptions(1000));
		пПараметры.Вставить("AnimalDisease", ВетисDictionary_2_0.AnimalDisease());
		пПараметры.Вставить("Индекс", 0);
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			пУсловие = пПараметры.ListResponse.condition[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		ListResponse = Прокси().GetR13nConditionList(пПараметры.ListOptions, пПараметры.AnimalDisease);
	Исключение
		пОтказ = Истина;
		пОшибка = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Ветис.Журнал_ДобавитьИнформациюОбОшибке(ИнформацияОбОшибке(), "GetR13nConditionListСледующий");
		ВызватьИсключение;
	КонецПопытки;
	
	пПараметры.Вставить("ListResponse", ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		пУсловие = пПараметры.ListResponse.condition[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции


Функция Прокси()
	
	_Прокси = Ветис.Прокси("RegionalizationService");
	
	Возврат _Прокси;
	
КонецФункции
