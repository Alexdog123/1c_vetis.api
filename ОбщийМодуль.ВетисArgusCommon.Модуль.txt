
// Тип, описывающий пользователя. Наследуется от типа bs:GenericEntity.
Функция User(login = Неопределено) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "User"));
	
	Если НЕ login = Неопределено Тогда
		_Объект.login = login;
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции


// Тип, описывающий единицу измерения.
Функция Unit(guid) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "Unit"));
	
	_Объект.guid = guid;
	
	Возврат _Объект;
	
КонецФункции

Функция UnitListИтератор(List) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПолучитьИтератор(List, "unit");
	
КонецФункции


// Тип, описывающий упаковку.
// http://help.vetrf.ru/wiki/PackingForm
Функция PackingForm(uuid) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "packingForm"));
	
	_Объект.uuid = uuid;
	
	Возврат _Объект;
	
КонецФункции

// Тип, описывающий список форм упаковки.
Функция PackingFormList(uuid = Неопределено) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "PackingFormList"));
	
	PackingFormListДобавить(_Объект, uuid);
	
	Возврат _Объект;
	
КонецФункции

Функция PackingFormListДобавить(List, uuid) Экспорт
	
	Если ТипЗнч(uuid) = Тип("Массив") Тогда
		Для каждого _uuid Из uuid Цикл
			List.packingForm.Добавить(ФормаXML.Элемент, URI(), "packingForm", packingForm(_uuid));
		КонецЦикла;
	ИначеЕсли ТипЗнч(uuid) = Тип("Строка") Тогда
		List.packingForm.Добавить(ФормаXML.Элемент, URI(), "packingForm", packingForm(uuid));
	КонецЕсли;
	
КонецФункции

Функция PackingFormListИтератор(List) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПолучитьИтератор(List, "packingForm");
	
КонецФункции


// Тип, описывающий назначение груза.
Функция Purpose(guid) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "Purpose"));
	
	_Объект.guid = guid;
	
	Возврат _Объект;
	
КонецФункции

Функция PurposeListИтератор(List) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПолучитьИтератор(List, "purpose");
	
КонецФункции

Функция PurposeListТаблица() Экспорт
	
	_Таблица = Новый ТаблицаЗначений;
	_Таблица.Колонки.Добавить("name", Новый ОписаниеТипов("Строка"));
	_Таблица.Колонки.Добавить("guid", Новый ОписаниеТипов("Строка"));
	_Таблица.Колонки.Добавить("forSubstandard", Новый ОписаниеТипов("Булево"));
	
	Возврат _Таблица;
	
КонецФункции


#Область DocumentNature
// Тип, описывающий единицу измерения.

// Электронный документ.
Функция DocumentNature_ELECTRONIC() Экспорт
	
	Возврат "ELECTRONIC";
	
КонецФункции

// Бумажный документ.
Функция DocumentNature_PAPER() Экспорт
	
	Возврат "PAPER";
	
КонецФункции

// Описание
Функция DocumentNatureОписание(nature) Экспорт
	
	Если nature = DocumentNature_ELECTRONIC() Тогда
		Возврат "Электронный документ";
	ИначеЕсли nature = DocumentNature_PAPER() Тогда
		Возврат "Бумажный документ";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Список
Функция DocumentNatureСписок() Экспорт
	
	_Список = Новый СписокЗначений();
	
	_Список.Добавить(DocumentNature_ELECTRONIC(), DocumentNatureОписание(DocumentNature_ELECTRONIC()));
	_Список.Добавить(DocumentNature_PAPER(),  DocumentNatureОписание(DocumentNature_PAPER()));
	
	Возврат _Список;
	
КонецФункции

#КонецОбласти


#Область Service

// Предназначена для получения данных о единице измерения по её глобальному идентификатору.
// Этот идентификатор используется при оформлении транспортных сертификатов.
Функция GetUnitByGuid(guid) Экспорт
	
	Возврат Прокси().GetUnitByGuid(guid);
	
КонецФункции

// Предназначена для поиска сведений о назначении груза по глобальному идентификатору.
Функция GetPurposeByGuid(guid) Экспорт
	
	Возврат Прокси().GetPurposeByGuid(guid);
	
КонецФункции


// Предназначена для получения списка актуальных версий единиц измерения.
Функция GetUnitList(ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Возврат Прокси().GetUnitList(ListOptions);
	
КонецФункции

// Предназначена для выборки актуальных версий единиц измерения.
// Используется самостоятельно в цикле Пока.
// Параметры:
//  Item - элемент списка, возвращаемый параметр
//  пПараметры - для внутреннего использования, начальное значение - Неопределено, обязательный
// Возвращаемое значение:
//	Булево - Ложь значит список пройден весь
Функция GetUnitListСледующий(Item, пПараметры = Неопределено) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("ListOptions", ВетисBase.ListOptions());
		пПараметры.Вставить("Индекс", 0);
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			Item = пПараметры.ListResponse.unit[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ListResponse = Прокси().GetUnitList(пПараметры.ListOptions);
	
	пПараметры.Вставить("ListResponse", ListResponse);
	
	Item = пПараметры.ListResponse.unit[пПараметры.Индекс];
	
	Возврат Истина;
	
КонецФункции


// Предназначена для получения списка актуальных версий назначений груза.
Функция GetPurposeList(ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Возврат Прокси().GetPurposeList(ListOptions);
	
КонецФункции

// Предназначена для выборки актуальных версий назначений груза.
// Используется самостоятельно в цикле Пока.
// Параметры:
//  Item - элемент списка, возвращаемый параметр
//  пПараметры - для внутреннего использования, начальное значение - Неопределено, обязательный
// Возвращаемое значение:
//	Булево - Ложь значит список пройден весь
Функция GetPurposeListСледующий(Item, пПараметры = Неопределено) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("ListOptions", ВетисBase.ListOptions());
		пПараметры.Вставить("Индекс", 0);
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			Item = пПараметры.ListResponse.purpose[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ListResponse = Прокси().GetPurposeList(пПараметры.ListOptions);
	
	пПараметры.Вставить("ListResponse", ListResponse);
	
	Item = пПараметры.ListResponse.purpose[пПараметры.Индекс];
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти


Функция Прокси()
	
	_Прокси = WSСсылки.DictionaryServiceTest.СоздатьWSПрокси("http://api.vetrf.ru/schema/cdm/registry/service", "DictionaryServiceBindingQSService", "DictionaryServiceBindingQSPort");
	_Прокси.Пароль       = ВетисПараметрыСоединения.Пароль();
	_Прокси.Пользователь = ВетисПараметрыСоединения.Логин();
	
	Возврат _Прокси;
	
КонецФункции

Функция URI()
	
	Возврат "http://api.vetrf.ru/schema/cdm/argus/common";
	
КонецФункции

