
// тип, описывающий хозяйствующий субъект (ХС)
Функция BusinessEntity(fullName = Неопределено, inn = Неопределено, name = Неопределено, пПараметры = Неопределено) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "BusinessEntity"));
	
	_Объект.name = ?(name = Неопределено, "", name);
	_Объект.fullName = ?(fullName = Неопределено, "", fullName);
	_Объект.inn  = ?(inn = Неопределено, "", inn);
	
	Если НЕ пПараметры = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(_Объект, пПараметры);
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

Функция BusinessEntityTypeОписание(type) Экспорт
	
	Если type = 1 Тогда
		Возврат "Юр. лицо";
	ИначеЕсли type = 2 Тогда
		Возврат "Физ. лицо";
	ИначеЕсли type = 3 Тогда
		Возврат "ИП";
	Иначе
		Возврат "<Ошибка>";
	КонецЕсли;
	
КонецФункции

Функция BusinessEntityListИтератор(List) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПолучитьИтератор(List, "businessEntity");
	
КонецФункции


// тип, описывающий связку хозяйствующего субъекта и площадки (поднадзорного объекта)
Функция BusinessMember(businessEntity, enterprise) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "BusinessMember"));
	
	_Объект.businessEntity = BusinessEntity();
	_Объект.businessEntity.guid = businessEntity;
	
	_Объект.enterprise = Enterprise();
	_Объект.enterprise.guid = enterprise;
	
	Возврат _Объект;
	
КонецФункции


// тип, описывающий предприятие
Функция Enterprise(name = Неопределено, пПараметры = Неопределено) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "Enterprise"));
	
	_Объект.name = ?(name = Неопределено, "", name);
	
	Если НЕ пПараметры = Неопределено Тогда
		ЗаполнитьЗначенияСвойств(_Объект, пПараметры);
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

Функция EnterpriseListИтератор(List) Экспорт
	
	Возврат ВетисОбщегоНазначения.ПолучитьИтератор(List, "enterprise");
	
КонецФункции

Функция BEModificationOperation(type, affectedList, reason) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "BEModificationOperation"));
	
	_Объект.type = type;
	_Объект.affectedList = affectedList;
	_Объект.reason  = reason;
	
	Возврат _Объект;
	
КонецФункции



#Область Service

// предназначена для получения последней в истории записи хозяйствующего субъекта
// по её глобальному идентификатору (active=true, last=true).
// Выполнение операции заканчивается либо возвратом записи хозяйствующего субъекта, либо ошибкой,
// в случае если параметры запроса не корректны, если запись по запросу не найдена или произошла иная ошибка выполнения запроса.
Функция GetBusinessEntityByGuid(guid, пИмяСвойства = Неопределено, ПоУмолчанию = Неопределено) Экспорт
	
	_объект = Прокси().GetBusinessEntityByGuid(guid);
	
	Если пИмяСвойства = Неопределено Тогда
		Возврат _объект;
	Иначе
		Возврат ?(_объект.Свойства().Получить(пИмяСвойства) = Неопределено, ПоУмолчанию, _объект[пИмяСвойства]);
	КонецЕсли;
	
КонецФункции

// предназначена для получения последней в истории записи предприятия
// по его глобальному идентификатору (active=true, last=true).
// Выполнение операции заканчивается либо возвратом записи предприятия, либо ошибкой,
// в случае если параметры запроса не корректны, если запись по запросу не найдена или произошла иная ошибка выполнения запроса.
Функция GetEnterpriseByGuid(guid, пИмяСвойства = Неопределено, ПоУмолчанию = Неопределено) Экспорт
	
	_объект = Прокси().GetEnterpriseByGuid(guid);
	
	Если пИмяСвойства = Неопределено Тогда
		Возврат _объект;
	Иначе
		Возврат ?(_объект.Свойства().Получить(пИмяСвойства) = Неопределено, ПоУмолчанию, _объект[пИмяСвойства]);
	КонецЕсли;
	
КонецФункции


// предназначена для получения списка хозяйствующих субъектов из реестра Россельхознадзора.
// В список попадают только актуальные записи (active = true).
// Поддерживается возможность постраничного вывода и фильтрации по шаблону.
// Выполнение операции заканчивается либо возвратом списка хозяйствующих субъектов, либо ошибкой,
// в случае если параметры запроса не корректны или произошла иная ошибка выполнения запроса.
Функция GetBusinessEntityList(fullName = Неопределено, inn = Неопределено, name = Неопределено, ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Возврат Прокси().GetBusinessEntityList(ListOptions, BusinessEntity(fullName, inn, name));
	
КонецФункции

// Предназначена для выборки хозяйствующих субъектов из реестра Россельхознадзора.
// Используется самостоятельно в цикле Пока.
// Параметры:
//  fullName - полное имя
//  inn - ИНН
//  name - имя
//  Item - элемент списка, возвращаемый параметр
//  пПараметры - для внутреннего использования, начальное значение - Неопределено, обязательный
// Возвращаемое значение:
//	Булево - Ложь значит список пройден весь
Функция GetBusinessEntityListСледующий(fullName = Неопределено, inn = Неопределено, name = Неопределено, Item, пПараметры = Неопределено) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("ListOptions", ВетисBase.ListOptions());
		пПараметры.Вставить("Индекс", 0);
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			Item = пПараметры.ListResponse.businessEntity[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ListResponse = Прокси().GetBusinessEntityList(пПараметры.ListOptions, BusinessEntity(fullName, inn, name));
	
	пПараметры.Вставить("ListResponse", ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		Item = пПараметры.ListResponse.businessEntity[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
КонецФункции


// предназначена для получения списка предприятий, зарегистрированных на территории Российской Федерации.
// Выводятся актуальные записи (active=true, last=true).
// Операция поддерживает фильтрацию по полям предприятия и постраничный вывод.
// Выполнение операции заканчивается либо возвратом списка предприятий, либо ошибкой,
// в случае если параметры запроса не корректны или произошла иная ошибка выполнения запроса.
Функция GetRussianEnterpriseList(name = Неопределено, ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Возврат Прокси().GetBusinessEntityList(ListOptions, Enterprise(name));
	
КонецФункции

// Предназначена для выборки предприятий, зарегистрированных на территории Российской Федерации.
// Используется самостоятельно в цикле Пока.
// Параметры:
//  name - имя
//  Item - элемент списка, возвращаемый параметр
//  пПараметры - для внутреннего использования, начальное значение - Неопределено, обязательный
// Возвращаемое значение:
//	Булево - Ложь значит список пройден весь
Функция GetRussianEnterpriseListСледующий(name = Неопределено, Item, пПараметры = Неопределено) Экспорт
	
	Если пПараметры = Неопределено Тогда
		пПараметры = Новый Структура;
		пПараметры.Вставить("ListOptions", ВетисBase.ListOptions());
		пПараметры.Вставить("Индекс", 0);
	Иначе
		пПараметры.Индекс = пПараметры.Индекс + 1;
		Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
			Если пПараметры.ListOptions.offset + пПараметры.ListOptions.count >= Число(пПараметры.ListResponse.Total) Тогда
				Возврат Ложь;
			Иначе
				пПараметры.ListOptions.offset = пПараметры.ListOptions.offset + пПараметры.ListOptions.count;
				пПараметры.Индекс = 0;
			КонецЕсли;
		Иначе
			Item = пПараметры.ListResponse.enterprise[пПараметры.Индекс];
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	ListResponse = Прокси().GetRussianEnterpriseList(пПараметры.ListOptions, Enterprise(name));
	
	пПараметры.Вставить("ListResponse", ListResponse);
	
	Если пПараметры.Индекс >= пПараметры.ListResponse.count Тогда
		Возврат Ложь;
	Иначе
		Item = пПараметры.ListResponse.enterprise[пПараметры.Индекс];
		Возврат Истина;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти


Функция Прокси()
	
	_Прокси = WSСсылки.EnterpriseServiceTest.СоздатьWSПрокси("http://api.vetrf.ru/schema/cdm/registry/service", "EnterpriseServiceBindingQSService", "EnterpriseServiceBindingQSPort");
	_Прокси.Пароль       = ВетисПараметрыСоединения.Пароль();
	_Прокси.Пользователь = ВетисПараметрыСоединения.Логин();
	
	Возврат _Прокси;
	
КонецФункции

Функция URI()
	
	Возврат "http://api.vetrf.ru/schema/cdm/cerberus/enterprise";
	
КонецФункции
