
// тип, описывающий cведения о содержимом заявки
Функция Application(пОбъект = Неопределено, пИмя = Неопределено) Экспорт
	
	_Объект = Фабрика().Создать(URI(), "Application");
	
	_Объект.serviceId = ВетисПараметрыСоединения.serviceId();
	_Объект.issuerId  = ВетисПараметрыСоединения.issuerId();
	_Объект.issueDate = ТекущаяДата();
	
	Если НЕ пОбъект = Неопределено Тогда
		_Объект.data = ApplicationDataWrapper(пОбъект, пИмя);
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

// результат обработки заявки
Функция ApplicationResult(пОбъект) Экспорт
	
	Возврат пОбъект.result;
	
КонецФункции

// тип, описывающий cведения о содержимом заявки
Функция ApplicationDataWrapper(пОбъект = Неопределено, пИмя = Неопределено) Экспорт
	
	_Объект = Фабрика().Создать(URI(), "ApplicationDataWrapper");
	
	Если НЕ пОбъект = Неопределено Тогда
		
		_Объект.Добавить(ФормаXML.Элемент, пОбъект.Тип().URIПространстваИмен, пИмя, пОбъект);
		
	КонецЕсли;
	
	Возврат _Объект;
	
КонецФункции

// тип, описывающий cведения о результате обработки заявки
Функция ApplicationResultWrapper(пОбъект) Экспорт
	
	Возврат пОбъект.applicationResultData;
	
КонецФункции


#Область Service

// Заявка в единой заявочной системе на выполнение определенной операции,
// определяет пользователя, который инициирует запрос и содержит сведения самого запроса.
Функция submitApplicationRequest(пApplication = Неопределено) Экспорт
	
	_Объект = Фабрика().Создать(ServiceURI(), "submitApplicationRequest");
	
	_Объект.apiKey      = ВетисПараметрыСоединения.apiKey();
	_Объект.application = пApplication;
	
	Возврат _Объект;
	
КонецФункции

// Заявка в заявочной системе, содержит сведения запроса.
Функция submitApplicationResponse(пОбъект) Экспорт
	
	Возврат пОбъект.application;
	
КонецФункции

// Запрос на получение результатов обработки заявки.
Функция receiveApplicationResultRequest(applicationId) Экспорт
	
	_Объект = Фабрика().Создать(ServiceURI(), "receiveApplicationResultRequest");
	
	_Объект.apiKey        = ВетисПараметрыСоединения.apiKey();
	_Объект.issuerId      = ВетисПараметрыСоединения.issuerId();
	_Объект.applicationId = applicationId;
	
	Возврат _Объект;
	
КонецФункции

// Получение результатов обработки заявки.
Функция receiveApplicationResultResponse(пОбъект) Экспорт
	
	Возврат пОбъект.application;
	
КонецФункции

#КонецОбласти


Функция ServiceURI()
	
	Возврат "http://api.vetrf.ru/schema/cdm/application/ws-definitions";
	
КонецФункции

Функция URI() Экспорт
	
	Возврат "http://api.vetrf.ru/schema/cdm/application";
	
КонецФункции

Функция Фабрика()
	
	Возврат WSСсылки.ApplicationManagementsServiceTest.ПолучитьWSОпределения().ФабрикаXDTO;
	
КонецФункции

