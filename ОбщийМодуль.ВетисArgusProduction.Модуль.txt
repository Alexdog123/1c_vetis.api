
// Тип, описывающий тип продукции.
Функция ProductTypeОписание(type) Экспорт
	
	Если type = 1 Тогда
		Возврат "Мясо и мясопродукты";
	ИначеЕсли type = 2 Тогда
		Возврат "Корма и кормовые добавки";
	ИначеЕсли type = 3 Тогда
		Возврат "Живые животные";
	ИначеЕсли type = 4 Тогда
		Возврат "Лекарственные средства";
	ИначеЕсли type = 5 Тогда
		Возврат "Пищевые продукты";
	ИначеЕсли type = 6 Тогда
		Возврат "Непищевые продукты и другое";
	ИначеЕсли type = 7 Тогда
		Возврат "Рыба и морепродукты";
	ИначеЕсли type = 8 Тогда
		Возврат "Продукция, не требующая разрешения";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ProductTypeСписок() Экспорт
	
	_Список = Новый СписокЗначений();
	
	_Индекс = 1;
	
	Пока Истина Цикл
		
		_описание = ProductTypeОписание(_Индекс);
		
		Если _описание = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		_Список.Добавить(_Индекс, _описание);
		
		_Индекс = _Индекс + 1;
		
	КонецЦикла;
	
	Возврат _Список;
	
КонецФункции

// Тип, описывающий продукцию.
Функция Product(guid) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "Product"));
	
	_Объект.guid = guid;
	
	Возврат _Объект;
	
КонецФункции

Функция ProductListИтератор(List) Экспорт
	
	Возврат List.product;
	
КонецФункции


// Тип, описывающий вид продукции.
Функция SubProduct(guid) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "SubProduct"));
	
	_Объект.guid = guid;
	
	Возврат _Объект;
	
КонецФункции

Функция SubProductListИтератор(List) Экспорт
	
	Возврат List.subProduct;
	
КонецФункции


// Тип, описывающий наименование продукции.
Функция ProductItem(guid) Экспорт
	
	_Объект = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип(URI(), "Product"));
	
	_Объект.guid = guid;
	//_Объект.name = name;// Название продукции.
	//_Объект.code = articul;// Артикул (код) продукции.
	//_Объект.productType = productType;
	//_Объект.product = "";
	//_Объект.subProduct = "";
	//_Объект.correspondToGost = Ложь;
	//_Объект.gost = "";
	
	Возврат _Объект;
	
КонецФункции

Функция ProductItemListИтератор(List) Экспорт
	
	Возврат List.productItem;
	
КонецФункции


#Область Service

// предназначена для получения записи продукции
Функция GetProductByGuid(guid) Экспорт
	
	Возврат Прокси().GetProductByGuid(guid);
	
КонецФункции

// предназначена для получения списка продукции по указанному типу.
Функция GetProductByTypeList(productType, ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Возврат Прокси().GetProductByTypeList(ListOptions, productType);
	
КонецФункции

// предназначена для получения записи о виде продукции
Функция GetSubProductByGuid(guid) Экспорт
	
	Возврат Прокси().GetSubProductByGuid(guid);
	
КонецФункции

// предназначена для получения списка видов продукции, отфильтрованного по указанному продукту.
Функция GetSubProductByProductList(productGuid, ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Возврат Прокси().GetSubProductByProductList(ListOptions, productGuid);
	
КонецФункции

// предназначена для получения записи о номенклатуре производителя
Функция GetProductItemByGuid(guid) Экспорт
	
	Возврат Прокси().GetProductItemByGuid(guid);
	
КонецФункции

// предназначена для получения списка наименований продукции отфильтрованных по указанному продукту
// по номенклатуре определенного предприятия-производителя
Функция GetProductItemList(subProductGuid, enterpriseGuid, ListOptions = Неопределено) Экспорт
	
	Если ListOptions = Неопределено Тогда
		ListOptions = ВетисBase.ListOptions();
	Иначе
		ListOptions.offset = ListOptions.offset + ListOptions.count;
	КонецЕсли;
	
	Возврат Прокси().GetProductItemList(ListOptions, subProductGuid, enterpriseGuid);
	
КонецФункции

#КонецОбласти


Функция Прокси()
	
	_Прокси = WSСсылки.ProductServiceTest.СоздатьWSПрокси("http://api.vetrf.ru/schema/cdm/registry/service", "ProductServiceBindingQSService", "ProductServiceBindingQSPort");
	_Прокси.Пароль       = ВетисПараметрыСоединения.Пароль();
	_Прокси.Пользователь = ВетисПараметрыСоединения.Логин();
	
	Возврат _Прокси;
	
КонецФункции

Функция URI()
	
	Возврат "http://api.vetrf.ru/schema/cdm/argus/production";
	
КонецФункции
