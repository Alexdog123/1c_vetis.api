
Функция Фабрика() Экспорт
	
	_Определения = WSСсылки.ApplicationManagementsServiceTest.ПолучитьWSОпределения();
	
	// создаем врем. фабрику на основе пакетов из конфиги и web сервиса
	// нам нужны 2 пакета из ws, но пакет с application нужен локальный
	_URI = Новый Массив;  
	_URI.Добавить("http://api.vetrf.ru/schema/cdm/application/ws-definitions");  
	_URI.Добавить("http://api.vetrf.ru/schema/cdm/base/ws-definitions");  
	
	_Фабрика = Новый ФабрикаXDTO(_Определения.ФабрикаXDTO.ЭкспортМоделиXDTO(_URI), ФабрикаXDTO.Пакеты);  
	
	_Пакеты = Новый Массив;  
	//_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://api.vetrf.ru/schema/cdm/argus/common"));
	//_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://api.vetrf.ru/schema/cdm/argus/shipment"));
	//_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://api.vetrf.ru/schema/cdm/argus/production"));
	//_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://api.vetrf.ru/schema/cdm/cerberus/enterprise"));
	//_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://api.vetrf.ru/schema/cdm/ikar"));
	_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://api.vetrf.ru/schema/cdm/mercury/applications"));
	//_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://api.vetrf.ru/schema/cdm/mercury/vet-document"));
	_Пакеты.Добавить(ФабрикаXDTO.Пакеты.Получить("http://schemas.xmlsoap.org/soap/envelope/"));
	
	Для Каждого _Пакет Из _Фабрика.Пакеты Цикл  
		_Пакеты.Добавить(_Пакет);
	КонецЦикла;  
	
	Возврат Новый ФабрикаXDTO(, _Пакеты);
	
КонецФункции

Функция ВыполнитьЗапрос(пRequest, пИмяФункции, issuerId = Неопределено, ПоУмолчанию = Неопределено, пОтладка = Неопределено, пОжидание = 5) Экспорт
	
	Если пОтладка = Неопределено Тогда
		_Отладка = ВетисОбщегоНазначения.РазрешенаОтладка();
	Иначе
		_Отладка = пОтладка;
	КонецЕсли;
	
	ВетисПараметрыСоединения.ИнициализироватьНастройкиХС(issuerId);
	
	_Фабрика = Фабрика();
	
	_Соединение = ПолучитьСоединение();
	
	//запрос на создание заявки
	
	_Application = ВетисApplication.Application(пRequest, пИмяФункции);
	
		_SubmitRequest = ВетисApplication.submitApplicationRequest(_Application);
		
			_Envelope = ВетисSoapEnvelope.Envelope(_SubmitRequest, "submitApplicationRequest");
			
				_ТекстЗапроса = Сериализовать(_Фабрика, _Envelope);
				
				Если _Отладка = Истина Тогда
					ВывестиСообщение(_ТекстЗапроса);
				КонецЕсли;
				
				_Запрос = ПолучитьЗапрос(_ТекстЗапроса);  
				
				_Ответ = ПолучитьОтвет(_Запрос, _Соединение);  
				
				Если НЕ _Ответ.КодСостояния = 200 Тогда
					ВывестиСообщение("Ошибка при отправке запроса");
					ВывестиСообщение(_ТекстЗапроса);
					Возврат ПоУмолчанию;
				КонецЕсли;
				
			_Envelope = Десериализовать(_Фабрика, _Ответ.ПолучитьТелоКакСтроку("UTF-8"), _Envelope.Тип());
		
		_SubmitResponse = ВетисSoapEnvelope.Извлечь(_Envelope).submitApplicationResponse;
	
	Если НЕ (_SubmitResponse.Application.status = ВетисКонстанты.Status_ACCEPTED()) Тогда 
		ВывестиСообщение("Запрос вернулся со статусом " + _SubmitResponse.Application.status);
		Возврат ПоУмолчанию; 
	КонецЕсли;
	
	//запрос на получение результатов обработки заявки
	
	_receiveRequest = ВетисApplication.receiveApplicationResultRequest(_SubmitResponse.Application.applicationId);
	
	_Envelope = ВетисSoapEnvelope.Envelope(_receiveRequest, "receiveApplicationResultRequest");
	
	_ТекстЗапроса = Сериализовать(_Фабрика, _Envelope);
	
	_Запрос = ПолучитьЗапрос(_ТекстЗапроса);  
	
	Пока Истина Цикл
		
		_Ответ = ПолучитьОтвет(_Запрос, _Соединение);  
		
		Если НЕ _Ответ.КодСостояния = 200 Тогда
			ВывестиСообщение("Ошибка при отправке запроса");
			ВывестиСообщение(_ТекстЗапроса);
			Возврат ПоУмолчанию;
		КонецЕсли;
		
		_Envelope = Десериализовать(_Фабрика, _Ответ.ПолучитьТелоКакСтроку("UTF-8"), _Envelope.Тип());
		
		_receiveResponse = ВетисSoapEnvelope.Извлечь(_Envelope).receiveApplicationResultResponse;
		
		Если _receiveResponse.Application.Status = ВетисКонстанты.Status_IN_PROCESS() Тогда
			//ВывестиСообщение("Ждем ответа", 1);
		Иначе
			Если НЕ _receiveResponse.Application.status = ВетисКонстанты.Status_COMPLETED() Тогда
				ВывестиСообщение(Сериализовать(_Фабрика, _receiveResponse.Application.errors));
				Возврат ПоУмолчанию;
			Иначе
				Если _Отладка = Истина Тогда
					ВывестиСообщение(Сериализовать(_Фабрика, _receiveResponse.Application));
				КонецЕсли;
				
				Возврат ВетисApplication.ApplicationResult(_receiveResponse.Application);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции


Функция Десериализовать(пФабрика, пТекст, пТип)
	
	_Чтение = Новый ЧтениеXML;
	
	_Чтение.УстановитьСтроку(пТекст);
	
	Возврат пФабрика.ПрочитатьXML(_Чтение, пТип);
	
КонецФункции

Функция Сериализовать(пФабрика, пОбъект, пЛокальноеИмя = Неопределено, пURI = Неопределено)
	
	_ЗаписьXML = Новый ЗаписьXML;  
	_ЗаписьXML.УстановитьСтроку("UTF-8");  
	
	Если пЛокальноеИмя = Неопределено Тогда
		пФабрика.ЗаписатьXML(_ЗаписьXML, пОбъект);  
	Иначе
		пФабрика.ЗаписатьXML(_ЗаписьXML, пОбъект, пЛокальноеИмя, пURI);  
	КонецЕсли;
	
	Возврат _ЗаписьXML.Закрыть();
	
КонецФункции

Функция ПривестиКТипу(пОбъект, пИмя, пURI, пФабрика = Неопределено) Экспорт
	
	Если пФабрика = Неопределено Тогда
		_Фабрика = ФабрикаXDTO;
	Иначе
		_Фабрика = пФабрика;
	КонецЕсли;
	
	_Строка = Сериализовать(_Фабрика, пОбъект, пИмя, пURI);
	
	Возврат Десериализовать(_Фабрика, _Строка, _Фабрика.Тип(пURI, пИмя));
	
КонецФункции


Функция ПолучитьЗапрос(пТекстЗапроса)
	
	_Запрос = Новый HTTPЗапрос("platform/services/ApplicationManagementService");  
	
	_Запрос.УстановитьТелоИзСтроки(пТекстЗапроса);
	
	Возврат _Запрос;
	
КонецФункции

Функция ПолучитьОтвет(пЗапрос, пСоединение = Неопределено)
	
	Если ТипЗнч(пЗапрос) = Тип("Строка") Тогда
		_Запрос = ПолучитьЗапрос(пЗапрос);
	Иначе
		_Запрос = пЗапрос;
	КонецЕсли;
	
	Если пСоединение = Неопределено Тогда
		_Соединение = ПолучитьСоединение();
	Иначе
		_Соединение = пСоединение;
	КонецЕсли;
	
	_Ответ = _Соединение.ОтправитьДляОбработки(_Запрос);
	
	//Если НЕ _Ответ.КодСостояния = 200 Тогда
	//	ВывестиСообщение("Ошибка при отправке запроса");
	//КонецЕсли;
	
	Возврат _Ответ;
	
КонецФункции

Функция ПолучитьСоединение()
	
	_пс = ВетисПараметрыСоединения;
	
	Возврат Новый HTTPСоединение(_пс.Адрес(), _пс.Порт(), _пс.Логин(), _пс.Пароль(), , Истина);  
	
КонецФункции


Процедура УстановитьЗначение(пОбъект, пИмяСвойства, пЗначение, пТекстОшибки = Неопределено) Экспорт
	
	Если НЕ пОбъект.Свойства().Получить(пИмяСвойства) = Неопределено И НЕ пЗначение = Неопределено Тогда
		пОбъект[пИмяСвойства] = пЗначение;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначение(пОбъект, пИмяСвойства, ПоУмолчанию = Неопределено) Экспорт
	
	Возврат ?(пОбъект.Свойства().Получить(пИмяСвойства) = Неопределено, ПоУмолчанию, пОбъект[пИмяСвойства]);
	
КонецФункции


Функция ВывестиСообщение(пТекст, пКонтекст = "") Экспорт
	
	ВетисЖурнал.Добавить(?(пКонтекст = "", "", Строка(пКонтекст) + ": ") + пТекст);
	
КонецФункции

Функция ВывестиСообщениеОбОшибке(пИнформацияОбОшибке, пКонтекст = "", пКратко = Ложь) Экспорт
	
	Если пКратко Тогда
		_Текст = КраткоеПредставлениеОшибки(пИнформацияОбОшибке);
	Иначе
		_Текст = ПодробноеПредставлениеОшибки(пИнформацияОбОшибке);
	КонецЕсли;
	
	ВетисЖурнал.Добавить(?(пКонтекст = "", "", Строка(пКонтекст) + ": ") + _Текст);
	
КонецФункции

Функция ПолучитьНакопленныеСообщения(пУдалять = Истина) Экспорт
	
	ТекстОшибки = "";
	
	СообщенияПользователю = ПолучитьСообщенияПользователю(пУдалять);
	
	Для Каждого Сообщение Из СообщенияПользователю Цикл
		ТекстОшибки = ТекстОшибки + Сообщение.Текст + Символы.ПС;
	КонецЦикла;

	Возврат ТекстОшибки;
	
КонецФункции



Функция ПолучитьИтератор(List, пИмя) Экспорт
	
	Если ТипЗнч(List) = Тип("ОбъектXDTO") Тогда
		Если List.Свойства().Получить(пИмя) = Неопределено Тогда
			Возврат Новый Массив;
		Иначе
			Если ТипЗнч(List[пИмя]) = Тип("СписокXDTO") Тогда
				Возврат List[пИмя];
			Иначе
				_Список = Новый Массив;
				_Список.Добавить(List[пИмя]);
				Возврат _Список;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция ListRequestСледующий(ListRequest, ListResponse) Экспорт
	
	Если ListRequest.listOptions.offset + ListRequest.ListOptions.count >= Число(ListResponse.Total) Тогда
		Возврат Ложь;
	Иначе
		ListRequest.listOptions.offset = ListRequest.listOptions.offset + ListRequest.ListOptions.count;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Функция Создать(пИмя, пURI, пФабрика = Неопределено) Экспорт
	
	Если пФабрика = Неопределено Тогда
		_Фабрика = ФабрикаXDTO;
	Иначе
		_Фабрика = пФабрика;
	КонецЕсли;
	
	Возврат _Фабрика.Создать(_Фабрика.Тип(пURI, пИмя));
	
КонецФункции

Процедура ЗаполнитьОбъект(пОбъект, value, СвойствоПоУмолчанию = "guid") Экспорт

	Если ТипЗнч(value) = Тип("Структура") Тогда
		Для каждого _value Из value Цикл
			пОбъект[_value.Ключ] = _value.Значение;
		КонецЦикла;
	ИначеЕсли ТипЗнч(value) = Тип("ОбъектXDTO") Тогда
		пОбъект[СвойствоПоУмолчанию] = value[СвойствоПоУмолчанию];
	ИначеЕсли НЕ value = Неопределено Тогда
		пОбъект[СвойствоПоУмолчанию] = value;
	КонецЕсли;

КонецПроцедуры

Функция РазрешенаОтладка() Экспорт
	
	Возврат ВетисПеременные.Получить("ВетисОтладка", Ложь);
	
КонецФункции


Функция ДобавитьОтбор(пОтбор, пПоле, пЗначение = Неопределено, пВидСравнения = Неопределено) Экспорт
	
	Элемент = ОтборПолучитьЭлементы(пОтбор).Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Элемент.Использование = Истина;
	Если НЕ пВидСравнения = Неопределено Тогда
		Элемент.ВидСравнения = ПредопределенноеЗначение("ВидСравненияКомпоновкиДанных."+пВидСравнения);
	КонецЕсли;
	Элемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(пПоле);
	Элемент.ПравоеЗначение = пЗначение;
	
КонецФункции

Функция ОтборПолучитьЭлементы(Источник) Экспорт
	
	Результат = Неопределено;
	
	Если ТипЗнч(Источник) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Результат = Источник.Настройки.Отбор.Элементы;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("НастройкиКомпоновкиДанных") Тогда
		Результат = Источник.Отбор.Элементы;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("ОтборКомпоновкиДанных") Тогда
		Результат = Источник.Элементы;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("КоллекцияЭлементовОтбораКомпоновкиДанных") Тогда
		Результат = Источник;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

