
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если кпс.РазрешенаОтладка() Тогда
		Элементы.ДатаИзменения.Видимость = Истина;
	КонецЕсли;
	
	ТипПродукции = ПредопределенноеЗначение("Перечисление.ВетисProductType.МясоИМясопродукты");
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Список.Отбор, "ТипПродукции", ТипПродукции);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПродукцииПриИзменении(Элемент)
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Список.Отбор, "ТипПродукции", ТипПродукции);
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьСервер();
	
	кпс.ВывестиНакопленныеСообщения();
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСервер()
	
	_ТаблицаВетис = Новый ТаблицаЗначений;
	_ТаблицаВетис.Колонки.Добавить("name", Новый ОписаниеТипов("Строка"));
	_ТаблицаВетис.Колонки.Добавить("guid", Новый ОписаниеТипов("Строка"));
	_ТаблицаВетис.Колонки.Добавить("code", Новый ОписаниеТипов("Строка"));
	_ТаблицаВетис.Колонки.Добавить("ProductType", Новый ОписаниеТипов("Число"));
	
	_ListOptions = Неопределено;
	
	_ТипПродукции = Перечисления.ВетисProductType.Индекс(ТипПродукции) + 1;
	
	Пока Истина Цикл
		
		Попытка
			_List = ВетисArgusProduction.GetProductByTypeList(_ТипПродукции, _ListOptions);
		Исключение
			ВетисОбщегоНазначения.ВывестиСообщениеОбОшибке(ИнформацияОбОшибке(), "Ветис.Product");
			Прервать;
		КонецПопытки;
		
		Для Каждого _Item из ВетисArgusProduction.ProductListИтератор(_List) Цикл
			
			ТаблицаСтрока = _ТаблицаВетис.Добавить();
			
			ЗаполнитьЗначенияСвойств(ТаблицаСтрока, _Item);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(_Таблица.name КАК СТРОКА(150)) КАК name,
	|	ВЫРАЗИТЬ(_Таблица.guid КАК СТРОКА(36)) КАК guid,
	|	ВЫРАЗИТЬ(_Таблица.code КАК СТРОКА(10)) КАК code,
	|	ВЫРАЗИТЬ(_Таблица.ProductType КАК ЧИСЛО(10, 0)) КАК ProductType
	|ПОМЕСТИТЬ ВТаблицаВетис
	|ИЗ
	|	&ТаблицаВетис КАК _Таблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	_Таблица.Ссылка,
	|	_Таблица.Наименование,
	|	_Таблица.КодТНВЭД,
	|	_Таблица.ТипПродукции.Порядок + 1 КАК ТипПродукции,
	|	_Ветис.name,
	|	_Ветис.guid,
	|	_Ветис.code,
	|	_Ветис.ProductType
	|ИЗ
	|	ВТаблицаВетис КАК _Ветис
	|		ПОЛНОЕ СОЕДИНЕНИЕ Справочник.ВетисProduct КАК _Таблица
	|		ПО _Ветис.guid = _Таблица.guid
	|ГДЕ
	|	ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	_Ветис.name";
	
	Запрос.УстановитьПараметр("ТаблицаВетис", _ТаблицаВетис);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		_Новый = Ложь;
		
		_ЕстьИзменения = Ложь;
		
		Если Выборка.guid = NULL Тогда
			Выборка.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
			ВетисОбщегоНазначения.ВывестиСообщение("- " + Выборка.Наименование, "Ветис.Product");
			Продолжить;
			
		ИначеЕсли Выборка.Ссылка = NULL Тогда
			_Новый = Истина;
			
		Иначе
			_ЕстьИзменения = Ложь
			ИЛИ НЕ Выборка.Наименование = СокрЛП(Выборка.name)
			ИЛИ НЕ Выборка.КодТНВЭД = СокрЛП(Выборка.code)
			ИЛИ НЕ Выборка.ТипПродукции = Выборка.ProductType;
			
		КонецЕсли;
		
		Если _ЕстьИзменения ИЛИ _Новый Тогда
			
			Если _Новый Тогда
				_Элемент = Справочники.ВетисProduct.СоздатьЭлемент();
				ВетисОбщегоНазначения.ВывестиСообщение("+ " + СокрЛП(Выборка.name), "Ветис.Product");
			Иначе
				_Элемент = Выборка.Ссылка.ПолучитьОбъект();
				ВетисОбщегоНазначения.ВывестиСообщение("~ " + СокрЛП(Выборка.name), "Ветис.Product");
			КонецЕсли;
			
			_Элемент.guid          = СокрЛП(Выборка.guid);
			_Элемент.Наименование  = СокрЛП(Выборка.name);
			_Элемент.КодТНВЭД      = СокрЛП(Выборка.code);
			_Элемент.ТипПродукции  = ТипПродукции;//Перечисления.ВетисProductType.Получить(Выборка.ProductType-1);
			_Элемент.ДатаИзменения = ТекущаяДата();
			
			Попытка
				_Элемент.Записать();
			Исключение
				ВетисОбщегоНазначения.ВывестиСообщениеОбОшибке(ИнформацияОбОшибке(), "Ветис.Product");
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
