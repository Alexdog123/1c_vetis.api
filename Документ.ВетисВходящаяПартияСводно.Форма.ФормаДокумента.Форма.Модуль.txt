
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьРеквизитыФормы();
	
	Для каждого _Элемент Из Элементы Цикл
		Если ТипЗнч(_Элемент) = Тип("ПолеФормы") Тогда
			Если Лев(_Элемент.ПутьКДанным, 2) = "сж" Тогда
				_Элемент.УстановитьДействие("ПриИзменении", "СкладскойЖурналПриИзменении");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//Элементы.ГруппаАктНесоответствияКолонка.Доступность = Объект.СоставитьАктНесоответствия;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	//ТекущийОбъект.АктНесоответствия.Очистить();
	//
	//_Строка = ТекущийОбъект.АктНесоответствия.Добавить();
	//
	//Для каждого _Реквизит Из ПолучитьРеквизиты() Цикл
	//	Если Лев(_Реквизит.Имя, 17) = "АктНесоответствия" Тогда
	//		_Строка[Сред(_Реквизит.Имя, 18)] = ЭтаФорма[_Реквизит.Имя];
	//	КонецЕсли;
	//КонецЦикла;
	
КонецПроцедуры



&НаКлиенте
Процедура ПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	оо = Новый ОписаниеОповещения("ПолучательОбработкаОповещения", ЭтаФорма);
	
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораУФ", Новый Структура("Ключ,Запрос", Объект.Получатель, ПолучитьТекстЗапросаСправочникКонтрагентыФормаВыбора()), ЭтаФорма,,,,оо);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.Получатель = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	оо = Новый ОписаниеОповещения("ПредприятиеОбработкаОповещения", ЭтаФорма, Элемент_ПутьКДанным(Элемент.Имя));
	
	ОткрытьФорму("Справочник.кпсПунктыНазначения.ФормаВыбора", Новый Структура("Владелец, Заголовок", ПолучитьСписок("Объект.Получатель"), "Предприятия"), ,,,, оо);
	
КонецПроцедуры


&НаКлиенте
Процедура ОтправительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	оо = Новый ОписаниеОповещения("ОтправительОбработкаОповещения", ЭтаФорма);
	
	ОткрытьФорму("Справочник.Контрагенты.Форма.ФормаВыбораУФ", Новый Структура("Ключ,Запрос", Объект.Отправитель, ПолучитьТекстЗапросаСправочникКонтрагентыФормаВыбора()), ЭтаФорма,,,,оо);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительОбработкаОповещения(Результат, Параметр) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Модифицированность = Истина;
		Объект.Отправитель = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительПредприятиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	оо = Новый ОписаниеОповещения("ПредприятиеОбработкаОповещения", ЭтаФорма, Элемент_ПутьКДанным(Элемент.Имя));
	
	ОткрытьФорму("Справочник.кпсПунктыНазначения.ФормаВыбора", Новый Структура("Владелец, Заголовок", ПолучитьСписок("Объект.Отправитель"), "Предприятия"), ,,,, оо);
	
КонецПроцедуры


&НаКлиенте
Процедура СкладскойЖурналВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СкладскойЖурналВозвратныйВСД" Тогда
		ОткрытьЗначение(Элементы.СкладскойЖурнал.ТекущиеДанные.ВозвратныйВСД);
	ИначеЕсли Поле.Имя = "СкладскойЖурналВСД" И ЗначениеЗаполнено(Элементы.СкладскойЖурнал.ТекущиеДанные.guid) Тогда
		ОткрытьЗначение(Элементы.СкладскойЖурнал.ТекущиеДанные.ВСД);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкладскойЖурналПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Копирование;
	
КонецПроцедуры

&НаКлиенте
Процедура СкладскойЖурналПередУдалением(Элемент, Отказ)
	
	Отказ = ЗначениеЗаполнено(Элемент.ТекущиеДанные.guid);
	
КонецПроцедуры

&НаКлиенте
Процедура СкладскойЖурналВСДПриИзменении(Элемент)
	
	_ТекущиеДанные = Элементы.СкладскойЖурнал.ТекущиеДанные;
	_всд = Элементы.СкладскойЖурнал.ТекущиеДанные.ВСД;
	_всдСтрокаСЖ = _всд.СкладскойЖурнал[0];
	_ТекущиеДанные.НоменклатурнаяГруппа = _всдСтрокаСЖ.НоменклатурнаяГруппа;
	_ТекущиеДанные.Количество = _всдСтрокаСЖ.Количество;
	_ТекущиеДанные.КоличествоМест = _всдСтрокаСЖ.КоличествоМест;
	//_ТекущиеДанные.СтатусВСД = _всд.Статус;
	
	Объект.Отправитель = _всд.Отправитель;
	Объект.Получатель  = _всд.Получатель;
	
	Объект.ОтправительПредприятие = _всд.ОтправительПредприятие;
	Объект.ПолучательПредприятие  = _всд.ПолучательПредприятие;
	
КонецПроцедуры



&НаКлиенте
Процедура СоставитьАктНесоответствияПриИзменении(Элемент)
	
	Элементы.ГруппаАктНесоответствияКолонка.Доступность = Объект.СоставитьАктНесоответствия;
	
КонецПроцедуры


&НаКлиенте
Процедура Отправить(Команда)
	
	Если НЕ Объект.Проведен Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого _СтрокаСЖ Из Объект.СкладскойЖурнал Цикл
		
		Если ЗначениеЗаполнено(_СтрокаСЖ.guid) Тогда
			Продолжить;
		КонецЕсли;
		
		ОтправитьСервер(_СтрокаСЖ.НомерСтроки-1);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОтправитьСервер(пНомерСтроки)
	
	Перем _discrepancyReport, _returnedDelivery;
	
	_СтрокаСЖ = Объект.СкладскойЖурнал.Получить(пНомерСтроки);
	
	Если НЕ _СтрокаСЖ.ВСД.Проведен Тогда
		Возврат "";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(_СтрокаСЖ.ВСД.Статус) И НЕ _СтрокаСЖ.ВСД.Статус = Перечисления.ВетисVetDocumentStatus.Оформлен Тогда
		Возврат "";
	КонецЕсли;
	
	_Отладка = кпс.РазрешенаОтладка();
	
	_всд = _СтрокаСЖ.ВСД.ПолучитьОбъект();
	
	_delivery = ВетисMercuryVetdocument.Delivery(
		ВетисCerberusEnterprise.BusinessMember(
			ВетисCerberusEnterpriseСлой1с.BusinessEntityКонтрагент(Объект.Отправитель),
			ВетисCerberusEnterpriseСлой1с.EnterpriseПунктНазначения(Объект.ОтправительПредприятие)),
		ВетисCerberusEnterprise.BusinessMember(
			ВетисCerberusEnterpriseСлой1с.BusinessEntityКонтрагент(Объект.Получатель),
			ВетисCerberusEnterpriseСлой1с.EnterpriseПунктНазначения(Объект.ПолучательПредприятие)),
		ПолучитьBatch(_всд.СкладскойЖурнал[0], _СтрокаСЖ.Количество, _СтрокаСЖ.КоличествоМест, ВетисMercuryVetdocument.Consignment()),
		ВетисMercuryVetdocument.ConsignmentDocumentList(ПолучитьWaybill(_всд), ПолучитьVetDocument(_всд)));
	
	_всдСтрокаСЖ = _СтрокаСЖ.ВСД.СкладскойЖурнал[0];
	
	_КоличествоФакт     = _СтрокаСЖ.Количество + _СтрокаСЖ.КоличествоВозврат;
	_КоличествоМестФакт = _СтрокаСЖ.КоличествоМест + _СтрокаСЖ.КоличествоМестВозврат;
	
	Объект.РезультатПриема = Перечисления.ВетисDeliveryInspectionResult[?(_КоличествоФакт = _всдСтрокаСЖ.Количество, "CORRESPONDS", "MISMATCH")];
	Объект.РешениеОПриеме = Перечисления.ВетисDeliveryDecision[?(_СтрокаСЖ.Количество = 0, "RETURN_ALL", ?(_СтрокаСЖ.КоличествоВозврат = 0, "ACCEPT_ALL", "PARTIALLY"))];
	Объект.РезультатВетКонтроля = Объект.РезультатПриема;
	
	_deliveryFacts = ВетисMercuryVetdocument.DeliveryFactList(
	  ВетисArgusCommonСлой1с.DocumentNature(Перечисления.ВетисDocumentNature[?(ЗначениеЗаполнено(_всд.Статус), "ELECTRONIC", "PAPER")]),
	  ВетисMercuryVetdocument.DeliveryInspection(ВетисMercuryVetdocumentСлой1с.DeliveryInspectionResult(Объект.РезультатПриема)),
	  ВетисMercuryVetdocument.DeliveryInspection(ВетисMercuryVetdocumentСлой1с.DeliveryInspectionResult(Объект.РезультатВетКонтроля), ВетисArgusCommon.User(ВетисПараметрыСоединения.ЛогинВрача())),
	  ВетисMercuryVetdocumentСлой1с.DeliveryDecision(Объект.РешениеОПриеме));
	
	_reason = "";
	_description = "";
	
	Если НЕ _КоличествоФакт = _всдСтрокаСЖ.Количество ИЛИ НЕ _КоличествоМестФакт = _всдСтрокаСЖ.КоличествоМест Тогда
		_discrepancyReport = ВетисMercuryVetdocument.Создать("DiscrepancyReport");
		_discrepancyReport.issueDate = Объект.Дата;
		Если НЕ _КоличествоФакт = _всдСтрокаСЖ.Количество Тогда
			_КоличествоРазница = ?(_КоличествоФакт > _всдСтрокаСЖ.Количество, _КоличествоФакт - _всдСтрокаСЖ.Количество, _всдСтрокаСЖ.Количество - _КоличествоФакт);
			_КоличествоСтрока  = ?(_КоличествоФакт > _всдСтрокаСЖ.Количество, "больше", "меньше");
			_reason = _reason + "Количество продукции " + _КоличествоСтрока + " указанного в ВСД на " + _КоличествоРазница + " кг.";
			_description = _description + "В ВСД заявлено " + _всдСтрокаСЖ.Количество + " кг, прибыло " + _КоличествоФакт + " кг.";
		КонецЕсли;
		Если НЕ _КоличествоМестФакт = _всдСтрокаСЖ.КоличествоМест Тогда
			_КоличествоРазница = ?(_КоличествоМестФакт > _всдСтрокаСЖ.КоличествоМест, _КоличествоМестФакт - _всдСтрокаСЖ.КоличествоМест, _всдСтрокаСЖ.КоличествоМест - _КоличествоМестФакт);
			_КоличествоСтрока  = ?(_КоличествоМестФакт > _всдСтрокаСЖ.КоличествоМест, "больше", "меньше");
			_reason = _reason + "Количество упаковок " + _КоличествоСтрока + " указанного в ВСД на " + _КоличествоРазница + " ед.";
			_description = _description + "В ВСД заявлено " + _всдСтрокаСЖ.КоличествоМест + " ед, прибыло " + _КоличествоФакт + " ед.";
		КонецЕсли;
		_discrepancyReport.reason = ВетисMercuryVetdocument.DiscrepancyReason(_reason);
		_discrepancyReport.description = _description;
	КонецЕсли;
	
	Если Объект.АктНесоответствия.Количество() <= пНомерСтроки Тогда
		Объект.АктНесоответствия.Добавить();
	КонецЕсли;
	Объект.АктНесоответствия[пНомерСтроки].Причина = _reason;
	Объект.АктНесоответствия[пНомерСтроки].Описание = _description;
	
	Если _СтрокаСЖ.Возврат = Истина Тогда
		_returnedDelivery = ВетисMercuryVetdocument.Delivery(
			ВетисCerberusEnterprise.BusinessMember(
				ВетисCerberusEnterpriseСлой1с.BusinessEntityКонтрагент(Объект.Получатель),
				ВетисCerberusEnterpriseСлой1с.EnterpriseПунктНазначения(Объект.ПолучательПредприятие)),
			ВетисCerberusEnterprise.BusinessMember(
				ВетисCerberusEnterpriseСлой1с.BusinessEntityКонтрагент(Объект.Отправитель),
				ВетисCerberusEnterpriseСлой1с.EnterpriseПунктНазначения(Объект.ОтправительПредприятие)),
			ПолучитьBatch(_всд.СкладскойЖурнал[0], _СтрокаСЖ.КоличествоВозврат, _СтрокаСЖ.КоличествоМестВозврат, ВетисMercuryVetdocument.Consignment()),
			ВетисMercuryVetdocument.ConsignmentDocumentList(ПолучитьWaybill(_всд, Истина), ПолучитьVetDocument(_всд, Истина)));
	КонецЕсли;
	
	
	_Request = ВетисMercuryApplications.processIncomingConsignmentRequest(_delivery, _deliveryFacts, _discrepancyReport, _returnedDelivery);
	
	__Response = ВетисОбщегоНазначения.ВыполнитьЗапрос(_Request, "processIncomingConsignmentRequest", Ложь, _Отладка);
	
	Если __Response = Ложь Тогда Возврат ""; КонецЕсли;
	
	Модифицированность = Истина;
	
	_Response = ВетисMercuryApplications.processIncomingConsignmentResponse(__Response);
	
	Для каждого _vetDocument Из _Response.vetDocument Цикл
		Если _vetDocument.type = ВетисКонстанты.VetDocumentType_TRANSPORT() Тогда
			
			_всд.Uuid   = _vetDocument.uuid;
			_всд.Тип    = ВетисMercuryVetdocumentСлой1с.VetDocumentType(_vetDocument.type);
			_всд.Статус = ВетисMercuryVetdocumentСлой1с.VetDocumentStatus(_vetDocument.status);
			
			_всд.ОбменДанными.Загрузка = Истина;
			
			Попытка
				_всд.Записать();
			Исключение
				ВетисОбщегоНазначения.ВывестиСообщениеОбОшибке(ИнформацияОбОшибке());
				ВызватьИсключение;
			КонецПопытки;
			
		ИначеЕсли _vetDocument.type = ВетисКонстанты.VetDocumentType_RETURNABLE() Тогда
			
			Если ЗначениеЗаполнено(_СтрокаСЖ.ВозвратныйВСД) Тогда
				_возвратный = _СтрокаСЖ.ВозвратныйВСД.ПолучитьОбъект();
				//_возвратный.Заполнить(_всд);
			Иначе
				_возвратный = _всд.Скопировать();
				_возвратный.Дата = ТекущаяДата();
				_возвратный.Записать();
			КонецЕсли;
			
			ВетисMercuryVetdocumentСлой1с.VetDocumentКонвертировать(_возвратный, _vetDocument);
			
			_возвратный.ОбменДанными.Загрузка = Истина;
			
			Попытка
				_возвратный.Записать(РежимЗаписиДокумента.Проведение);
			Исключение
				ВетисОбщегоНазначения.ВывестиСообщениеОбОшибке(ИнформацияОбОшибке(), "Возвратный ВСД");
				ВызватьИсключение;
			КонецПопытки;
			
			_СтрокаСЖ.ВозвратныйВСД = _возвратный.Ссылка;
			
		КонецЕсли;
	КонецЦикла;
	
	Для каждого _stockEntry Из _Response.stockEntry Цикл
		_СтрокаСЖ.guid = _stockEntry.guid;
		Прервать;
	КонецЦикла;
	
КонецФункции

Функция ПолучитьBatch(пСтрокаСЖ, пКоличество = Неопределено, пКоличествоМест = Неопределено, пОбъект = Неопределено)
	
	Если пОбъект = Неопределено Тогда
		_batch = ВетисMercuryVetdocument.Batch();
	Иначе
		_batch = пОбъект;
	КонецЕсли;
	
	ВетисMercuryVetdocumentСлой1с.BatchКонвертировать(_batch, пСтрокаСЖ);
	
	_batch.volume           = ?(пКоличество     = Неопределено, пСтрокаСЖ.Количество, пКоличество);
	_batch.packingAmount    = ?(пКоличествоМест = Неопределено, пСтрокаСЖ.КоличествоМест, пКоличествоМест);
	
	Возврат _batch;
	
КонецФункции

Функция ПолучитьVetDocument(пВСД, пВозврат = Ложь)
	
	_VetDocument = ВетисMercuryVetdocument.Создать("VetDocument");
	
	Если пВСД.Статус = Перечисления.ВетисVetDocumentStatus.Оформлен Тогда
		_VetDocument.uuid = пВСД.uuid;
	Иначе
		Если пВозврат = Ложь Тогда
			ВетисMercuryVetdocumentСлой1с.VetDocumentКонвертировать(_VetDocument, пВСД);
		Иначе
			_СтрокаВСД = пВСД.ВСД[0];
			_VetDocument.issueDate          = Объект.Дата;
			_VetDocument.purpose            = ВетисArgusCommon.Purpose(ВетисArgusCommonСлой1с.Purpose(Справочники.ВетисPurpose.НайтиПоНаименованию("возврат груза")));
			_VetDocument.cargoInspected     = _СтрокаВСД.КонтрольГосВетврачем;
			_VetDocument.cargoExpertized    = _СтрокаВСД.Ветсанэкспертиза;
			_VetDocument.confirmedBy        = ВетисArgusCommon.User(ВетисПараметрыСоединения.ЛогинВрача());
			_VetDocument.confirmedDate      = Объект.Дата;
			_VetDocument.locationProsperity = _СтрокаВСД.БлагополучиеМестности;
			_VetDocument.specialMarks       = _СтрокаВСД.ОсобыеОтметки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат _VetDocument;
	
КонецФункции

Функция ПолучитьWaybill(пВСД, пВозврат = Ложь)
	
	_СтрокаТТН = пВСД.ТТН[0];
	
	_waybill = ВетисArgusShipment.Создать("Waybill");
	_waybill.issueSeries   = _СтрокаТТН.Серия;
	_waybill.issueNumber   = _СтрокаТТН.Номер;
	_waybill.issueDate     = _СтрокаТТН.Дата;
	_waybill.type          = ВетисArgusShipmentСлой1с.WaybillType(_СтрокаТТН.Тип);
	_waybill.broker        = ВетисCerberusEnterprise.BusinessEntity(ВетисCerberusEnterpriseСлой1с.BusinessEntityКонтрагент(_СтрокаТТН.Посредник));
	_waybill.transportInfo = ВетисArgusShipmentСлой1с.TransportInfo(_СтрокаТТН.Транспорт);
	_waybill.transportStorageType = ВетисArgusShipmentСлой1с.TransportationStorageType(_СтрокаТТН.СпособХранения);
	
	Если пВозврат = Истина Тогда
		_waybill.issueSeries   = "";
		_waybill.issueNumber   = "";
		_waybill.issueDate     = Объект.Дата;
	КонецЕсли;
	
	Возврат _waybill;
	
КонецФункции


&НаКлиенте
Процедура ПредприятиеОбработкаОповещения(пЗначение, пПараметр) Экспорт
	
	Если НЕ пЗначение = Неопределено Тогда
		Модифицированность = Истина;
		Выполнить пПараметр + " = пЗначение;";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСписок(ИмяВладельца)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	_Таблица.Владелец КАК Ссылка
	|ИЗ
	|	Справочник.кпсПунктыНазначения КАК _Таблица
	|ГДЕ
	|	_Таблица.Владелец = &Владелец
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	_Таблица.Владелец
	|ИЗ
	|	Справочник.кпсПунктыНазначения КАК _Таблица
	|ГДЕ
	|	_Таблица.Владелец ССЫЛКА Справочник.Склады
	|	И 1 В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				1
	|			ИЗ
	|				РегистрСведений.СобственныеКонтрагенты
	|			ГДЕ
	|				РегистрСведений.СобственныеКонтрагенты.Контрагент = &Владелец)";
	
	Запрос.УстановитьПараметр("Владелец", Вычислить(ИмяВладельца));
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	_Список = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		_Список.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат _Список;
	
КонецФункции

&НаСервере
Функция Элемент_ПутьКДанным(пЭлементИмя)
	
	Возврат Элементы[пЭлементИмя].ПутьКДанным;
	
КонецФункции

&НаСервере
Функция ПолучитьТекстЗапросаСправочникКонтрагентыФормаВыбора()
	
	Возврат
	"ВЫБРАТЬ
	|	_Таблица.Ссылка,
	|	_Таблица.ПометкаУдаления,
	|	_Таблица.ЭтоГруппа,
	|	_Таблица.Код,
	|	_Таблица.Наименование,
	|	_Таблица.ИНН,
	|	_Таблица.КПП,
	|	_Таблица.ОсновнойМенеджерПокупателя КАК Менеджер,
	|	ВЫБОР
	|		КОГДА _Ветис.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Подсветка
	|ИЗ
	|	Справочник.Контрагенты КАК _Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВетисСоответствие КАК _Ветис
	|		ПО _Таблица.Ссылка = _Ветис.Ссылка
	|ГДЕ
	|	ИСТИНА
	|	И НЕ _Таблица.ПометкаУдаления
	|	И НЕ _Таблица.ЭтоГруппа";
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыФормы()
	
	//Для каждого _Строка Из Объект.АктНесоответствия Цикл
	//	Для каждого _Реквизит Из ПолучитьРеквизиты() Цикл
	//		Если Лев(_Реквизит.Имя, 17) = "АктНесоответствия" Тогда
	//			ЭтаФорма[_Реквизит.Имя] = _Строка[Сред(_Реквизит.Имя, 18)];
	//		КонецЕсли;
	//	КонецЦикла;
	//КонецЦикла;
	
КонецПроцедуры


