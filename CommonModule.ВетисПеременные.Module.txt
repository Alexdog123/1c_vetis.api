
Процедура Инициализировать() Экспорт
	
	ПараметрыСеанса.ВетисПеременные = ПоместитьВоВременноеХранилище(Завернуть(ПолучитьНовыйКеш()), Новый УникальныйИдентификатор);
	
КонецПроцедуры

Функция ПолучитьНовыйКеш()
	
	Возврат Новый Структура;
	
КонецФункции

Функция Получить(ИмяПараметра, ПоУмолчанию = Неопределено) Экспорт
	
	Хранилище = ПолучитьИзВременногоХранилища(ПараметрыСеанса.ВетисПеременные);
	
	Если Хранилище = Неопределено Тогда
		Кэш = ПолучитьНовыйКеш();
	Иначе
		Кэш = Развернуть(Хранилище);
		НайденноеЗначение = Неопределено;
		Если Кэш.Свойство(ИмяПараметра, НайденноеЗначение) Тогда
			Возврат НайденноеЗначение;
		КонецЕсли;
	КонецЕсли;
	
	Кэш.Вставить(ИмяПараметра, ПоУмолчанию);
	
	ПоместитьВоВременноеХранилище(Завернуть(Кэш), ПараметрыСеанса.ВетисПеременные);
	
	Возврат ПоУмолчанию;
	
КонецФункции

Процедура Установить(ИмяПараметра, Значение) Экспорт
	
	Хранилище = ПолучитьИзВременногоХранилища(ПараметрыСеанса.ВетисПеременные);
	
	Если Хранилище = Неопределено Тогда
		Кэш = ПолучитьНовыйКеш();
	Иначе
		Кэш = Развернуть(Хранилище);
		НайденноеЗначение = Неопределено;
		Если Кэш.Свойство(ИмяПараметра, НайденноеЗначение) Тогда
			Если НайденноеЗначение = Значение Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Кэш.Вставить(ИмяПараметра, Значение);
	
	ПоместитьВоВременноеХранилище(Завернуть(Кэш), ПараметрыСеанса.ВетисПеременные);
	
КонецПроцедуры

Функция Завернуть(Значение)
	//Возврат Новый ХранилищеЗначения(Значение, Новый СжатиеДанных(9));
	Возврат Значение;
КонецФункции

Функция Развернуть(Значение)
	//Возврат Значение.Получить();
	Возврат Значение;
КонецФункции
